Перем МассивТиповВыгружаемыхВресторан;
Перем МассивНевыгружаемыхТипов;
Перем ЭтоРесторан;

Функция МестоНужно(Место)
	Возврат Место.ИнформационнаяБаза = ИнформационнаяБаза ИЛИ Место.ИнформационнаяБаза.Пустая();
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ОБЪЕКТА

// Обработчик события ПередЗаписью.
//
Процедура ПередЗаписью(Отказ)
			
	Если Не ЗначениеЗаполнено(Наименование) Тогда 
		ТекстСообщения = "Не заполнено наименование.";
		СообщитьОбОшибке(ТекстСообщения, Отказ);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Код) Тогда 
		ТекстСообщения = "Не заполнен код.";
		СообщитьОбОшибке(ТекстСообщения, Отказ);
	КонецЕсли;                                     
	
	Если ПланыОбмена.ГлавныйУзел() = Неопределено И ПланыОбмена.Основной.ЭтотУзел() = Ссылка 
		И ИнформационнаяБаза <> Справочники.ИнформационныеБазы.Центр Тогда
		
		ТекстСообщения = "В главном узле необходимо выбрать информационную базу " + Справочники.ИнформационныеБазы.Центр.Наименование + ".";
		СообщитьОбОшибке(ТекстСообщения, Отказ);		
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ИнформационнаяБаза) Тогда
		
		ТекстСообщения = "Не заполнена информационная база.";
		СообщитьОбОшибке(ТекстСообщения, Отказ);		
		
	КонецЕсли;	
	
	Если НЕ Отказ Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ИнформационнаяБаза", ИнформационнаяБаза);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Основной.Ссылка
		|ИЗ
		|	ПланОбмена.Основной КАК Основной
		|ГДЕ
		|	Основной.ИнформационнаяБаза = &ИнформационнаяБаза
		|	И Основной.Ссылка <> &Ссылка";
		
		Если НЕ Запрос.Выполнить().Пустой() Тогда
			ТекстСообщения = "Для информационной базы уже создан узел.";
			СообщитьОбОшибке(ТекстСообщения, Отказ);
		КонецЕсли;	
			
	КонецЕсли;
	
	Если Константы.ИБВедениеСправочников.Получить() = ИнформационнаяБаза И НЕ Центр Тогда
		ТекстСообщения = "Узел информационной базы с ведением справочников должен быть центром.";
		СообщитьОбОшибке(ТекстСообщения, Отказ);
	КонецЕсли;

КонецПроцедуры

// Обработчик события ПриОтправкеДанныхПодчиненному.
//
Процедура ПриОтправкеДанныхПодчиненному(ЭлементДанных, ОтправкаЭлемента, СозданиеНачальногоОбраза)
	Объект = ЭлементДанных;

	Тип = ТипЗнч(Объект);
	СтрТип = Строка(Тип);

	Если СозданиеНачальногоОбраза Тогда
		Если МассивНевыгружаемыхТипов.Найти(Тип) <> Неопределено Тогда
			ОтправкаЭлемента = ОтправкаЭлементаДанных.Игнорировать;
			Возврат;
		КонецЕсли;	
		
		Если Не ЭтоРесторан И МассивТиповВыгружаемыхВресторан.Найти(Тип) <> Неопределено Тогда
			ОтправкаЭлемента = ОтправкаЭлементаДанных.Игнорировать;
			Возврат;
		КонецЕсли;	

	КонецЕсли;
	
	Если НЕ СозданиеНачальногоОбраза ИЛИ Центр Тогда
		Возврат;
	КонецЕсли;
	
	ОтправлятьОбъект = Истина;
	
	Если Лев(СтрТип, 8) = "Документ" Тогда
		ОтправлятьОбъект = Ложь;		
	ИначеЕсли Тип = Тип("СправочникОбъект.МестаРеализации") Тогда
		ОтправлятьОбъект = Истина;
			
	ИначеЕсли Тип = Тип("СправочникОбъект.ПосадочныеМеста") Тогда
		ОтправлятьОбъект = Ложь;
		
	ИначеЕсли Тип = Тип("СправочникОбъект.Сотрудники") Тогда
		ОтправлятьОбъект = Ложь;
				
	ИначеЕсли Тип = Тип("СправочникОбъект.РабочиеМеста") Тогда
		ОтправлятьОбъект = МестоНужно(Объект);
		
	ИначеЕсли Тип = Тип("СправочникОбъект.ТорговоеОборудование") Тогда
		//:Объект = Справочники.ТорговоеОборудование.СоздатьЭлемент();
		Если Объект.ИнформационнаяБаза.Пустая() Тогда
			ОтправлятьОбъект = Объект.Регион = ИнформационнаяБаза.Регион ИЛИ Объект.Регион.Пустая();
		Иначе
			ОтправлятьОбъект = МестоНужно(Объект);
		КонецЕсли;
		
		
	ИначеЕсли Тип = Тип("СправочникОбъект.Фирмы") Тогда
		ОтправлятьОбъект = МестоНужно(Объект);
	ИначеЕсли ТипЗнч(Объект) = Тип("СправочникОбъект.КаталогиТоваров") Тогда
		ОтправлятьОбъект = Истина;
	
	ИначеЕсли Тип = Тип("СправочникОбъект.ИнформационныеБазы") Тогда
		
		ОтправлятьОбъект = Объект.Ссылка = ИнформационнаяБаза;
		ОтправлятьОбъект = ОтправлятьОбъект ИЛИ Объект.ПолучитьУзелРИБ() = ПланыОбмена.Основной.ЭтотУзел();
	ИначеЕсли Тип = Тип("РегистрСведенийНаборЗаписей.ДополнительныеСвойства") Тогда
		ИБ = Объект[0].ИнформационнаяБаза;
		
		ОтправлятьОбъект = ИБ = ИнформационнаяБаза ИЛИ Не ЗначениеЗаполнено(ИБ);		
		
	ИначеЕсли Найти(СтрТип, "Справочник") Тогда
		
		МетаданныеСправочника = Объект.Метаданные();
		Если МетаданныеСправочника.Реквизиты.Найти("ИнформационнаяБаза") <> Неопределено Тогда
			ОтправлятьОбъект = Объект.ИнформационнаяБаза = ИнформационнаяБаза;

		ИначеЕсли МетаданныеСправочника.Реквизиты.Найти("ИнформационнаяБазаГруппа") <> Неопределено Тогда
			
			Если ТипЗнч(Объект.ИнформационнаяБазаГруппа) = Тип("СправочникСсылка.ИнформационныеБазы") Тогда
				ОтправлятьОбъект = Объект.ИнформационнаяБазаГруппа = ИнформационнаяБаза;

			ИначеЕсли ТипЗнч(Объект.ИнформационнаяБазаГруппа) = Тип("СправочникСсылка.ГруппыИнформационныхБаз") Тогда
				
				ОтправлятьОбъект = Справочники.ГруппыИнформационныхБаз.ИБпоГруппе(Объект.ИнформационнаяБазаГруппа).Найти(ИнформационнаяБаза) <> Неопределено;
			ИначеЕсли Объект.ИнформационнаяБазаГруппа = Null Тогда
				Если ТипЗнч(Объект.Родитель.ИнформационнаяБазаГруппа) = Тип("СправочникСсылка.ИнформационныеБазы") Тогда
					ОтправлятьОбъект = Объект.Родитель.ИнформационнаяБазаГруппа = ИнформационнаяБаза;
				ИначеЕсли ТипЗнч(Объект.Родитель.ИнформационнаяБазаГруппа) = Тип("СправочникСсылка.ГруппыИнформационныхБаз") Тогда
					ОтправлятьОбъект = Справочники.ГруппыИнформационныхБаз.ИБпоГруппе(Объект.Родитель.ИнформационнаяБазаГруппа).Найти(ИнформационнаяБаза) <> Неопределено;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	
	Если НЕ ОтправлятьОбъект Тогда
		ОтправкаЭлемента = ОтправкаЭлементаДанных.Игнорировать;
	КонецЕсли; 
	
КонецПроцедуры

//{ Создание образа
Функция МассивТиповВыгружаемыхВресторан() Экспорт
	Результат = Массив(
	Тип("СправочникОбъект.Номенклатура"),
	Тип("СправочникОбъект.Товары"),
	Тип("СправочникОбъект.Специфики"),
	Тип("РегистрСведенийНаборЗаписей.ДействующиеРасписания"),
	Тип("СправочникОбъект.НаборыПейджеров"),
	Тип("СправочникОбъект.Пейджеры"),
	Тип("РегистрСведенийНаборЗаписей.ДоступностьКаталоговТоваров"),
	Тип("РегистрСведенийНаборЗаписей.ДействующиеРасписания"),
	Тип("РегистрСведенийНаборЗаписей.ВыгруженныеЦены")
	);	
	Возврат Результат;
КонецФункции

Функция МассивНевыгружаемыхТипов() Экспорт
	Результат = Массив(
	Тип("СправочникОбъект.Задачи"),
	Тип("СправочникОбъект.КартыДоступа"),
	Тип("СправочникОбъект.Сотрудники"),
	Тип("СправочникОбъект.Клиенты"),
	Тип("ДокументОбъект.РеестрЦен"),
	Тип("РегистрСведенийНаборЗаписей.ЦеныНоменклатуры")
	);	
	Возврат Результат;
КонецФункции


МассивНевыгружаемыхТипов = МассивНевыгружаемыхТипов();
МассивТиповВыгружаемыхВресторан = МассивТиповВыгружаемыхВресторан();

ЭтоРесторан = Справочники.ГруппыИнформационныхБаз.ГруппыПоИБ(ИнформационнаяБаза).Найти(Справочники.ГруппыИнформационныхБаз.Рестораны) <> Неопределено;
//}