
////////////////////////////////////////////////////////////////////////////////
// ОБЩИЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Управляет доступностью элементов формы.
//
Процедура УправлениеДоступностью()
	
	Если ПланыОбмена.Основной.ЭтотУзел() = Ссылка Тогда
		ЭлементыФормы.Центр.Доступность = Ложь;
	КонецЕсли;
	
	Если ПланыОбмена.ГлавныйУзел() = Неопределено И ПланыОбмена.Основной.ЭтотУзел() <> Ссылка Тогда
		ЭлементыФормы.ОсновныеДействияФормы.Кнопки.СоздатьНачальныйОбраз.Доступность = Истина;
	Иначе
		ЭлементыФормы.ОсновныеДействияФормы.Кнопки.СоздатьНачальныйОбраз.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Проверяет правильность выбора информационной базы
//
//	Параметры:
//		ВыбраннаяИБ (Тип: СправочникСсылка.ИнформационныеБазы) - информационная база
//
//	Возвращаемое значение:
//		Истина - информационная база выбрана верно, ложь - в противном случае
//
Функция ПроверкаВыбраннойИнформационнойБазы(ВыбраннаяИБ)
	
	Успех = Истина;
	
	Если ПланыОбмена.ГлавныйУзел() = Неопределено И ПланыОбмена.Основной.ЭтотУзел() = Ссылка Тогда
		
		Если ВыбраннаяИБ <> Справочники.ИнформационныеБазы.Центр Тогда
			ВыбраннаяИБ = Справочники.ИнформационныеБазы.Центр;
			Предупреждение("Для главного узла должна быть установлена информационная база " + Справочники.ИнформационныеБазы.Центр.Наименование + ".");
			Успех = Ложь;
		КонецЕсли;
		
	Иначе
	
		Если ВыбраннаяИБ.Предопределенный Тогда
			
				Предупреждение("Информационная база " + Справочники.ИнформационныеБазы.Центр.Наименование + " может быть выбрана только для главного узла.");
				Успех = Ложь;
			
		Иначе	
		
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("ИнформационнаяБаза", ВыбраннаяИБ);
			Запрос.УстановитьПараметр("Ссылка"            , Ссылка);
			Запрос.Текст =
			"ВЫБРАТЬ
			|	Основной.Ссылка
			|ИЗ
			|	ПланОбмена.Основной КАК Основной
			|ГДЕ
			|	Основной.ИнформационнаяБаза = &ИнформационнаяБаза
			|	И Основной.Ссылка <> &Ссылка";
			
			Выборка = Запрос.Выполнить().Выбрать();
			
			Если Выборка.Следующий() Тогда
				Предупреждение("Информационная база """ + ВыбраннаяИБ + """ привязана к узлу """ + Выборка.Ссылка +""". Выберите другую.");
				Успех = Ложь;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Успех;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Обработчик события ПередОткрытием формы.
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		ЭтаФорма.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	//Попытка
	//	УстановитьМонопольныйРежим(Истина);
	//Исключение
	//	
	//	Предупреждение("Добавление и запись узла возможна только в монопольном режиме.");
	//	Если ЭтоНовый() Тогда
	//		Отказ = Истина;
	//	Иначе
	//		ТолькоПросмотр = Истина;
	//	КонецЕсли;
	//	
	//	Возврат;
	//КонецПопытки;
	
КонецПроцедуры

// Обработчик события ПриОткрытии формы.
//
Процедура ПриОткрытии()

	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		ТолькоПросмотр = Истина;
	ИначеЕсли ПланыОбмена.Основной.ЭтотУзел() = Ссылка И Не Центр Тогда
		Центр = Истина;
	КонецЕсли;
	                         	
	УправлениеДоступностью();
	
КонецПроцедуры

// Обработчик события ОбновлениеОтображения.
//
Процедура ОбновлениеОтображения()
	
	Если ЗначениеЗаполнено(ИнформационнаяБаза) И Константы.ИБВедениеСправочников.Получить() = ИнформационнаяБаза Тогда
		ЭлементыФормы.НадписьВедениеСправочников.Видимость  = Истина;
		ЭлементыФормы.НадписьВедениеСправочников.Заголовок  = "Ведение справочников";
	Иначе
		ЭлементыФормы.НадписьВедениеСправочников.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Обработчик нажатия кнопки ОсновныеДействияФормы.СоздатьНачальныйОбраз.
//
Процедура ОсновныеДействияФормыСоздатьНачальныйОбраз(Кнопка)
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ДиалогВыбораФайла.Заголовок = "Выберите папку для размещения информационной базы:";
		
	Если ДиалогВыбораФайла.Выбрать() Тогда
		Каталог = ДиалогВыбораФайла.Каталог;
	Иначе
		Возврат;
	КонецЕсли;
	
	Попытка
		ПланыОбмена.СоздатьНачальныйОбраз(ЭтотОбъект, "File = """ + Каталог + """");
		Предупреждение("Создание начального образа завершено!");
		ЭтотОбъект.Прочитать();
	Исключение
		Сообщить(ОписаниеОшибки());
	КонецПопытки;
	
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

// Обработчик события ПриИзменении флажка Центр.
//
Процедура ЦентрПриИзменении(Элемент)
	
		
КонецПроцедуры

Процедура ИнформационнаяБазаНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИнформационныеБазы.Ссылка
	|ИЗ
	|	Справочник.ИнформационныеБазы КАК ИнформационныеБазы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланОбмена.Основной КАК Основной
	|		ПО ИнформационныеБазы.Ссылка = Основной.ИнформационнаяБаза
	|ГДЕ
	|	(Основной.Ссылка ЕСТЬ NULL 
	|			ИЛИ Основной.Ссылка = &Ссылка)
	|	И НЕ ИнформационныеБазы.Предопределенный";
	
	ЭлементыФормы.ИнформационнаяБаза.СписокВыбора.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

Процедура ИнформационнаяБазаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = ПроверкаВыбраннойИнформационнойБазы(ВыбранноеЗначение);	
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Отказ = НЕ ПроверкаВыбраннойИнформационнойБазы(ИнформационнаяБаза);	
	
КонецПроцедуры

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Попытка
		УстановитьМонопольныйРежим(Ложь);
	Исключение
	КонецПопытки;
	
КонецПроцедуры
