Перем ЗаписиРегистра;
Перем КонстантаВалюта;

//////////////////////////////////////////////////////////////////////////////// 
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Обработчик события "ПередОткрытием" Формы
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если ЭтоНовый() Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры //ПередОткрытием()

// Обработчик события "ПриОткрытии" Формы.
//
Процедура ПриОткрытии()
	
	ЗаполнитьДанныеПоЗаказу();
	УправлениеВидимостью();
	УправлениеДоступностью();
	ИнтерфейсАдмина.ЗаполнитьПодменюВыбораПечатныхФорм(ЭтотОбъект, ЭлементыФормы.ОсновныеДействияФормы.Кнопки.ПодменюПечати);
	
	Попытка
		ЭлементыФормы.ПанельТаблиц.ТекущаяСтраница = ЭлементыФормы.ПанельТаблиц.Страницы[ВосстановитьЗначение("ТекущаяСтраницаЗаказ")];
	Исключение
	КонецПопытки;
	
КонецПроцедуры

Процедура ПриЗакрытии()
	
	СохранитьЗначение("ТекущаяСтраницаЗаказ", ЭлементыФормы.ПанельТаблиц.ТекущаяСтраница.Имя);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ШАПКИ

// Обработчик нажатия гиперссылки Официант.
//
Процедура ОфициантНажатие(Элемент)
	ОткрытьЗначение(Автор);
КонецПроцедуры

// Обработчик нажатия гиперссылки МестоРеализации.
//
Процедура МестоРеализацииНажатие(Элемент)
	ОткрытьЗначение(МестоРеализации);
КонецПроцедуры

// Обработчик нажатия гиперссылки ПосадочноеМесто.
//
Процедура ПосадочноеМестоНажатие(Элемент)
	ОткрытьЗначение(ПосадочноеМесто);
КонецПроцедуры

// Обработчик нажатия гиперссылки Кассир.
//
Процедура КассирНажатие(Элемент)
	ОткрытьЗначение(Кассир);
КонецПроцедуры

// Обработчик нажатия гиперссылки Клиент.
//
Процедура КлиентНажатие(Элемент)
	ОткрытьЗначение(Клиент);
КонецПроцедуры

///////////////////////////////////////////////////////////////////////
// ОБЩИЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Заполняет данные о заказе
Процедура ЗаполнитьДанныеПоЗаказу()
	
	// Данные по товарам
	Для Каждого СтрокаТЧ Из Товары Цикл  
		СтрокаТовара = ДеревоТоваров.Строки.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТовара, СтрокаТЧ);
		СуммаСкидки = (СтрокаТовара.СуммаРеализации-СтрокаТовара.Сумма);
		Если СуммаСкидки < 0 Тогда
			СуммаСкидки = -СуммаСкидки;
		КонецЕсли;
		СтрокаТовара.СуммаСкидки = СуммаСкидки;
		Если глВерсия = 1 Тогда
			Специфики = Неопределено;
		КонецЕсли;
		Если глВерсия > 1 Тогда
			МассивСпецифик = Специфики.НайтиСтроки(Новый Структура("НомерСтрокиТовара", СтрокаТЧ.НомерСтроки));
			Для Каждого СтрокаСпецифики Из МассивСпецифик Цикл 
				НоваяСтрока 		= СтрокаТовара.Строки.Добавить();
			//	НоваяСтрока.Товар 	= " - " + СтрокаСпецифики.Специфика.Наименование;
			    НоваяСтрока.Товар = СтрокаСпецифики.Специфика;
				//НоваяСтрока.
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;			
	
	// Данные о протоколе расчетов
	Отбор 			= Новый Структура("Заказ", Ссылка);
	ЗаписиРегистра 	= РегистрыСведений.ЗаказДопИнф.Получить(Отбор);
	ДатаОткрытия 	= ЗаписиРегистра.ДатаОткрытия;
	ДатаЗакрытия 	= ЗаписиРегистра.ДатаЗакрытия;
	Запрос  = Новый Запрос;
	Если ЗначениеЗаполнено(ЗаписиРегистра.ПротоколРасчетов) Тогда
		Кассир 		= ЗаписиРегистра.ПротоколРасчетов.Автор;
		Протокол 	= ЗаписиРегистра.ПротоколРасчетов.Протокол.Выгрузить().Скопировать();
		Оплата 	 	= ПолучитьТаблицуОплаты(Протокол);
		
		Для Каждого СтрокаОплаты Из Оплата Цикл
			ТипОплаты = СтрокаОплаты.Тип;
			Если НЕ ЗначениеЗаполнено(ТипОплаты) Тогда
				Продолжить;
			КонецЕсли;
			СуммаОплаты = Формат(СтрокаОплаты.Сумма, "ЧЦ=12; ЧДЦ=2;")+ " " + КонстантаВалюта;
			Если ТипОплаты = Перечисления.ТипыОплаты.Нал Тогда
				Наличные = СуммаОплаты;
			ИначеЕсли ТипОплаты = Перечисления.ТипыОплаты.Безнал Тогда
				Безнал = СуммаОплаты;
			ИначеЕсли ТипОплаты = Перечисления.ТипыОплаты.Карта Тогда
				БанкКарта = СуммаОплаты;
			ИначеЕсли ТипОплаты = Перечисления.ТипыОплаты.Неплательщик Тогда
				Неплательщик = СуммаОплаты;				
			КонецЕсли;				
		КонецЦикла;
		
	КонецЕсли;
	
	// Найдем удаления
	Запрос.Текст = "ВЫБРАТЬ
	|	Удаление.Товар,
	|	Удаление.Количество,
	|	Удаление.Причина,
	|	Удаление.Сумма,
	|	Удаление.Автор
	|ИЗ
	|	Документ.Удаление КАК Удаление
	|ГДЕ
	|	Удаление.Заказ = &Заказ
	|	И Удаление.ПометкаУдаления = ЛОЖЬ";
	Запрос.УстановитьПараметр("Заказ", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	НомерСтроки = 1;
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Удаления.Добавить();
		НоваяСтрока.НомерСтроки = НомерСтроки;
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	
	// Найдем возвраты
	Запрос.Текст = "ВЫБРАТЬ
	|	ВозвратТовары.Товар,
	|	ВозвратТовары.Количество,
	|	ВозвратТовары.Сумма КАК Сумма,
	|	ВозвратТовары.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.Возврат.Товары КАК ВозвратТовары
	|ГДЕ
	|	ВозвратТовары.Ссылка.Заказ = &Заказ
	|	И ВозвратТовары.Ссылка.ПометкаУдаления = ЛОЖЬ
	|ИТОГИ
	|	СУММА(Сумма)
	|ПО
	|	Ссылка";
	Запрос.УстановитьПараметр("Заказ", Ссылка);
	ВыборкаСсылок = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаСсылок.Следующий() Цикл
		// Строка ссылки
		НоваяСтрокаСсылка 		 = Возвраты.Строки.Добавить();
		НоваяСтрокаСсылка.Ссылка = ВыборкаСсылок.Ссылка;
		НоваяСтрокаСсылка.Сумма  = ВыборкаСсылок.Сумма;
		ВыборкаТоваров = ВыборкаСсылок.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаТоваров.Следующий() Цикл
			// Строка товаров
			НоваяСтрокаТовара 				= НоваяСтрокаСсылка.Строки.Добавить();
			НоваяСтрокаТовара.Ссылка 		= ВыборкаТоваров.Товар;
			НоваяСтрокаТовара.Количество 	= ВыборкаТоваров.Количество;
			НоваяСтрокаТовара.Сумма 		= ВыборкаТоваров.Сумма;			
		КонецЦикла;
	КонецЦикла;
	
	// Итоги
	СтрФормата = "ЧЦ=12; ЧДЦ=2;";
	ЭлементыФормы.Сумма.Значение 			= Строка(Формат(ИтоговаяСумма, СтрФормата)) + " " + КонстантаВалюта;
	ЭлементыФормы.СуммаСкидок.Значение 		= Строка(Формат(Скидки.Итог("Сумма"), СтрФормата)) + " " + КонстантаВалюта;
	ЭлементыФормы.СуммаУдалений.Значение 	= Строка(Формат(Удаления.Итог("Сумма"), СтрФормата)) + " " + КонстантаВалюта;
	ЭлементыФормы.СуммаВозвратов.Значение 	= Строка(Формат(Возвраты.Строки.Итог("Сумма"), СтрФормата)) + " " + КонстантаВалюта;
	
	// Текстовые поля
	ЭлементыФормы.ПосадочноеМесто.Значение 	= НаимПосадочногоМеста(ПосадочноеМесто);
	ЭлементыФормы.НадписьЗаголовок.Значение = "  Заказ № " + Номер;
	ЭлементыФормы.НадписьПроДаты.Значение 	= "Открыт: " + ДатаОткрытия + ?(ЗначениеЗаполнено(ДатаЗакрытия), "  Закрыт: " + ДатаЗакрытия, "");
	
КонецПроцедуры //ЗаполнитьДанныеПоЗаказу()

// Управляет видимостью формы.
//
Процедура УправлениеВидимостью()
	
	ТоварыКолонки = ЭлементыФормы.ДеревоТоваров.Колонки;
	ПанельТаблиц  = ЭлементыФормы.ПанельТаблиц; 
	
	ЕстьУдаления 	= Удаления.Количество() > 0;
	ЕстьСкидки 		= Скидки.Количество() > 0;
	ЕстьВозвраты	= Возвраты.Строки.Количество() > 0;
	ЗаказЗакрыт		= ЗаписиРегистра.Статус = Перечисления.СтатусыЗаказа.Закрыт;
	Разница 		= Товары.Итог("СуммаРеализации") - Товары.Итог("Сумма");
	ШапкаКолонкиСкидки = "СуммаСкидки";
	Если Разница > 0 Тогда
		ШапкаКолонкиСкидки = "Наценка";
		СинийЦвет = Новый Цвет(0,0,255);
		ТоварыКолонки.СуммаСкидки.ЦветТекстаПоля 		  = СинийЦвет;
		ЭлементыФормы.Скидки.Колонки.Сумма.ЦветТекстаПоля = СинийЦвет;
	ИначеЕсли Разница < 0 Тогда
		ШапкаКолонкиСкидки = "Скидка";
		ТоварыКолонки.СуммаСкидки.ЦветТекстаПоля = ЦветаСтиля.ЦветОсобогоТекста;
		ЭлементыФормы.Скидки.Колонки.Сумма.ЦветТекстаПоля = ЦветаСтиля.ЦветОсобогоТекста;
	КонецЕсли;
	
	ПанельТаблиц.Страницы.Скидки.Видимость				= ЕстьСкидки;
	ПанельТаблиц.Страницы.ПротоколРасчетов.Видимость 	= ЗаказЗакрыт;
	ПанельТаблиц.Страницы.Возвраты.Видимость 			= ЕстьВозвраты;
	ПанельТаблиц.Страницы.Удаления.Видимость 			= ЕстьУдаления;	
	
	ТоварыКолонки.ЦенаРеализации.Видимость 		= ЕстьСкидки;
	ТоварыКолонки.СуммаРеализации.Видимость 	= ЕстьСкидки;
	ТоварыКолонки.СуммаСкидки.Видимость 		= ЕстьСкидки;
	ТоварыКолонки.СуммаСкидки.ТекстШапки		= ШапкаКолонкиСкидки;
	ТоварыКолонки.КоличествоУдалено.Видимость 	= ЕстьУдаления;
	
	Если глВерсия = 1 Тогда
		Специфики = Неопределено;
	КонецЕсли;
	Если глВерсия > 1 Тогда
		Если Специфики.Количество() > 0 Тогда
			ЭлементыФормы.ДеревоТоваров.Колонки.Товар.ОтображатьИерархию = Истина;
		КонецЕсли;
	КонецЕсли;			
	
КонецПроцедуры

// Управляет доступностью формы
//
Процедура УправлениеДоступностью()
	
	ТипТП = Тип("ТабличноеПоле");
	Для Каждого Эл Из ЭлементыФормы Цикл
		Если ТипЗнч(Эл) = ТипТП Тогда
			Для Каждого Колонка Из Эл.Колонки Цикл
				Если Колонка.ЭлементУправления = Неопределено Тогда
					Продолжить;
				КонецЕсли;								
				Колонка.ЭлементУправления.ТолькоПросмотр 		= Истина;
				Колонка.ЭлементУправления.РедактированиеТекста 	= Ложь;
			КонецЦикла;			
		КонецЕсли;			
	КонецЦикла;
	
КонецПроцедуры //УправлениеДоступностью()

// Процедура вызывается при выборе пункта подменю "Печать" командной панели
// формы. Процедура отрабатывает выбор печатной формы.
// Подключение данной процедуры-обработчика выполняется из кода конфигурации
//
Процедура ДействияФормыДействиеВыбратьПечатнуюФормы(Кнопка) 	
	Если Кнопка <> Неопределено Тогда // найти новое значение вида операции
		СписокМакетов = ПолучитьСписокПечатныхФорм();
		СтрокаМакетаВСписке = СписокМакетов.НайтиПоЗначению(Кнопка.Имя);
		Если СтрокаМакетаВСписке <> Неопределено Тогда
			Печать(СтрокаМакетаВСписке.Значение);		
		Иначе
			СсылкаНаПечатнуюФорму = Справочники.ВнешниеОбработки.НайтиПоКоду(СтрЗаменить(Кнопка.Имя, "ВнешняяПечатнаяФорма_", ""));
			
			Если СсылкаНаПечатнуюФорму <> Неопределено Тогда
				Печать(СсылкаНаПечатнуюФорму);
			КонецЕсли;
			
		КонецЕсли;		
		
	КонецЕсли;
	
КонецПроцедуры // ДействияФормыДействиеВыбратьПечатнуюФормы()

Процедура ДеревоТоваровПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	Если глВерсия>1 И ТипЗнч(ДанныеСтроки.Товар) = Тип("СправочникСсылка.Специфики") Тогда
		 ОформлениеСтроки.Ячейки.Товар.Текст = " - "+ДанныеСтроки.Товар;
	КонецЕсли;
КонецПроцедуры


КонстантаВалюта = Константы.ОсновнаяВалюта.Получить();