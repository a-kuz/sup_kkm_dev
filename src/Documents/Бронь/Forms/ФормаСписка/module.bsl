
//Процедура-обработчик события ПриВыводеСтроки списка.
//
Процедура ДокументСписокПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	ЕстьБезМеста = Ложь;
	// посадочные места
	Текст = "";
	Док = ДанныеСтроки.Ссылка;
	КолВоСтрок = Док.ПосадочныеМеста.Количество();
	Для Каждого СтрокаТЧ Из Док.ПосадочныеМеста Цикл
		Если ЗначениеЗаполнено(СтрокаТЧ.ПосадочноеМесто) Тогда
			Текст = Текст + СтрокаТЧ.ПосадочноеМесто;//Текст = Текст + НаимПосадочногоМеста(СтрокаТЧ.ПосадочноеМесто);
		Иначе
			Текст = Текст + "Без места";
			ЕстьБезМеста = Истина;
		КонецЕсли; 
		Если СтрокаТЧ.НомерСтроки <> КолВоСтрок Тогда
			Текст = Текст + ", ";
		КонецЕсли;
	КонецЦикла;
	ОформлениеСтроки.Ячейки.ПосадочныеМеста.УстановитьТекст(Текст);
	
	// время
	СтрВремя = Формат(Док.ВремяНачала, "ДФ=ЧЧ:мм") + " - " + Формат(Док.ВремяКонца, "ДФ=ЧЧ:мм");
	ОформлениеСтроки.Ячейки.ВремяБрони.УстановитьТекст(СтрВремя);
	
	// дата
	Если Док.ВремяКонца - Док.ВремяНачала < 60*60*24 Тогда
		СтрДата = Формат(Док.ВремяНачала, "ДЛФ=Д");
	Иначе
		СтрДата = Формат(Док.ВремяНачала, "ДЛФ=Д") + " - " + Формат(Док.ВремяКонца, "ДЛФ=Д");
	КонецЕсли;
	ОформлениеСтроки.Ячейки.ДатаБрони.УстановитьТекст(СтрДата);
	
	// оформление цвета строки
	Если Док.ПометкаУдаления ИЛИ Док.Закрыто ИЛИ Док.ВремяКонца<ТекущаяДата() Тогда
		ОформлениеСтроки.ЦветТекста = WindowsЦвета.ТекстНедоступный;
		
	ИначеЕсли ЕстьБезМеста Тогда
		ОформлениеСтроки.ЦветТекста = ЦветаСтиля.ЦветОсобогоТекста;
		ОформлениеСтроки.Шрифт = Новый Шрифт(ОформлениеСтроки.Шрифт, , ,Истина);
		
	КонецЕсли; 
	
	Если Док.Интернет Тогда
		ОформлениеСтроки.ЦветФона = WebЦвета.БледноЛиловый;
	КонецЕсли;
	
КонецПроцедуры

//Процедура-обработчик события ПриАктивизацииСтроки списка.
//
Процедура ДокументСписокПриАктивизацииСтроки(Элемент)
	
	ИнтерфейсАдмина.СоздатьСписокПечатныхФорм(ЭтаФорма);
	
КонецПроцедуры //ДокументСписокПриАктивизацииСтроки()

// Процедура назначается динамически
// Процедура вызывается при выборе пункта подменю "Печать" командной панели
// формы. Процедура отрабатывает выбор печатной формы.
// Подключение данной процедуры-обработчика выполняется из кода конфигурации
//
Процедура ДействияФормыДействиеВыбратьПечатнуюФормы(Кнопка) 	
	ИнтерфейсАдмина.ВыбратьПечатнуюФорму(ЭтаФорма, Кнопка);
	
КонецПроцедуры //ДействияФормыДействиеВыбратьПечатнуюФормы()
