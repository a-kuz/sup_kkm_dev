Перем Расписание,ПанельВерх,ПанельВысота,МассивРасписаниеДня;

Процедура ВидимостьДоступность()
	ЭлементыФормы.ПанельТЧ.Страницы.ЦелевыеТовары.Видимость = ТипАкции = Справочники.ТипыАкций.НаборТоваров;		
	ЭлементыФормы.ПороговаяСумма.Видимость = ТипАкции = Справочники.ТипыАкций.СуммаЗаказа;
	ЭлементыФормы.фЦвет.Видимость = ТипАкции = Справочники.ТипыАкций.НаборТоваров;
	ЭлементыФормы.НадписьфЦвет.Видимость = ТипАкции = Справочники.ТипыАкций.НаборТоваров;
	ЭлементыФормы.НадписьПороговаяСумма.Видимость = ТипАкции = Справочники.ТипыАкций.СуммаЗаказа;
	ЭлементыФормы.ЛимитПодарков.Видимость = ТипАкции = Справочники.ТипыАкций.СуммаЗаказа;
	ЭлементыФормы.НадписьЛимитПодарков.Видимость = ТипАкции = Справочники.ТипыАкций.СуммаЗаказа;
	Если ТипАкции = Справочники.ТипыАкций.НаборТоваров Тогда
		Низ = ЭлементыФормы.ПанельТЧ.Верх +ЭлементыФормы.ПанельТЧ.Высота;
		
		ЭлементыФормы.НадписьРасписание.Верх = ЭлементыФормы.ПороговаяСумма.Верх; 
		//ЭлементыФормы.ПанельТЧ.Высота = ПанельВысота + (ЭлементыФормы.ПанельТЧ.Верх-ЭлементыФормы.ПороговаяСумма.Верх); 
	Иначе
		ЭлементыФормы.НадписьРасписание.Верх = ПанельВерх;
		//ЭлементыФормы.ПанельТЧ.Высота = ;
	КонецЕсли; 
	ЭтаФорма.Обновить();
КонецПроцедуры

Процедура ОбновитьНадписьРасписание()
	СтрокаРасписание = Строка(Расписание);
	Если СтрокаРасписание = "каждый  день; один раз в день" Тогда
		СтрокаРасписание = "Нажмите, чтобы задать расписание";
	Иначе
		СтрокаРасписание = СтрЗаменить(СтрокаРасписание,"; один раз в день","");
		СтрокаРасписание = СтрЗаменить(СтрокаРасписание,"один раз в день","");
	КонецЕсли;
	СтрокаРасписание = СтрЗаменить(СтрокаРасписание,":00:00",":00");
	ЭлементыФормы.НадписьРасписание.Заголовок = СтрокаРасписание;
КонецПроцедуры

Процедура ПриИзмененииПериода()
	Расписание.ДатаНачала = ДатаС;
	Расписание.ДатаКонца = ДатаПо;
	ОбновитьНадписьРасписание();
КонецПроцедуры

Процедура ПриОткрытии()
	ПанельВысота = ЭлементыФормы.ПанельТЧ.Высота;
	ПанельВерх = ЭлементыФормы.НадписьРасписание.Верх;
	ВидимостьДоступность();
	ОбновитьНадписьРасписание();
	Если Проведен Тогда
		
		ЭтаФорма.ТолькоПросмотр = Истина;
		ЭлементыФормы.фрмПремиальныеТовары.ТолькоПросмотр = Истина;
		ЭлементыФормы.КПРасписание.Доступность = Ложь;
		ЭлементыФормы.ВремяНачала.ТолькоПросмотр = Истина;
		ЭлементыФормы.ВремяКонца.ТолькоПросмотр = Истина;
		ЭлементыФормы.ДетальноеРасписаниеДня.ТолькоПросмотр = Истина;
		ЭлементыФормы.ЦелевыеТовары.ТолькоПросмотр = Истина;
		
	КонецЕсли; 
	ОформитьПолеЦвета();
КонецПроцедуры

Процедура ТипАкцииПриИзменении(Элемент)
	ВидимостьДоступность();
КонецПроцедуры

Процедура НадписьРасписаниеНажатие(Элемент)
	Если Найти(Строка(ПараметрыСеанса.ТекущийПользователь),"Кузнецов") Тогда
		Расписание.ДетальныеРасписанияДня = МассивРасписаниеДня;
		Д = Новый ДиалогРасписанияРегламентногоЗадания(Расписание);
		Д.ОткрытьМодально();
		Расписание = Д.Расписание;
		ДатаС = Расписание.ДатаНачала;
		ДатаПо = Расписание.ДатаКонца;
		ОбновитьНадписьРасписание();
	КонецЕсли;
	ЭлементыФормы.ПанельТЧ.ТекущаяСтраница = ЭлементыФормы.ПанельТЧ.Страницы.ПериодДействия;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СвернутьМассивРасписаний();
	ЗаполнитьТЧПремиальныеТовары();
	Расписание.ДетальныеРасписанияДня = МассивРасписаниеДня;
	СериализованноеРасписание = ЗначениеВСтрокуВнутр(Расписание);
	
	
	ЦветЦелевыхТоваров = Новый ХранилищеЗначения(фЦвет);
КонецПроцедуры

Процедура ДатаСПриИзменении(Элемент)
	ПриИзмененииПериода();
КонецПроцедуры

Процедура ДатаПоПриИзменении(Элемент)
	ПриИзмененииПериода();
КонецПроцедуры

Процедура КПРасписаниеУдалить(Кнопка)
	Индекс = ДетальноеРасписаниеДня.Индекс(ЭлементыФормы.ДетальноеРасписаниеДня.ТекущаяСтрока);
	МассивРасписаниеДня.Удалить(Индекс);
	ЗаполнитьСЗРасписание();
КонецПроцедуры

Процедура КПРасписаниеДобавить(Кнопка)
	РасписаниеДня = Новый РасписаниеРегламентногоЗадания;
	МассивРасписаниеДня.Добавить(РасписаниеДня);
	ЗаполнитьСЗРасписание();
	ЭлементыФормы.ДетальноеРасписаниеДня.ТекущаяСтрока = ДетальноеРасписаниеДня.Получить(ДетальноеРасписаниеДня.Количество()-1);
КонецПроцедуры

Процедура ЗаполнитьТЧПремиальныеТовары()
	ПремиальныеТовары.Очистить();
	Для каждого Т Из фрмПремиальныеТовары Цикл
		Для каждого Ст Из Т.Станции Цикл
					
			Нов = ПремиальныеТовары.Добавить();
			ЗаполнитьЗначенияСвойств(Нов,Т);
			Нов.Станция = Ст.Значение;
		
		КонецЦикла; 
	КонецЦикла; 	
КонецПроцедуры

Процедура СвернутьМассивРасписаний()
	ТЗ = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("ВремяНачала");
	ТЗ.Колонки.Добавить("ВремяКонца");
	Для каждого Т Из МассивРасписаниеДня Цикл
	
		Если Т.ВремяНачала = Т.ВремяКонца Тогда
		
			Продолжить;	
		
		КонецЕсли; 
		ЗаполнитьЗначенияСвойств(ТЗ.Добавить(),Т);
	КонецЦикла; 
	
	ТЗ.Сортировать("ВремяНачала");
	
	М = Новый Массив;
	Для каждого Т Из ТЗ Цикл
		
		Инд = ТЗ.Индекс(Т);
		Если Инд = ТЗ.Количество()-1 Тогда
		
			Прервать;
		
		КонецЕсли; 
		
		
		Если Т.ВремяКонца >= ТЗ[Инд+1].ВремяНачала Тогда
			
			ТЗ[Инд+1].ВремяНачала = Мин(Т.ВремяНачала,ТЗ[Инд+1].ВремяНачала);
			ТЗ[Инд+1].ВремяКонца = Макс(Т.ВремяКонца,ТЗ[Инд+1].ВремяКонца);
			
			М.Добавить(Т);
		КонецЕсли; 
		
	
	КонецЦикла; 
	Для каждого Т Из М Цикл
	
		ТЗ.Удалить(Т);
	
	КонецЦикла; 
	
	МассивРасписаниеДня.Очистить();
	Для каждого Т Из ТЗ Цикл
	
		Р = Новый РасписаниеРегламентногоЗадания;
		ЗаполнитьЗначенияСвойств(Р,Т);
		МассивРасписаниеДня.Добавить(Р);
	
	КонецЦикла; 
	Если Не МассивРасписаниеДня.Количество() Тогда
	
		МассивРасписаниеДня.Добавить(Новый РасписаниеРегламентногоЗадания);
	
	КонецЕсли; 
	ЗаполнитьСЗРасписание();
КонецПроцедуры

Процедура ЗаполнитьСЗРасписание()
	Если Неопределено = ЭлементыФормы.ДетальноеРасписаниеДня.ТекущаяСтрока Тогда
		Индекс = 0;
	Иначе
		Индекс = ДетальноеРасписаниеДня.Индекс(ЭлементыФормы.ДетальноеРасписаниеДня.ТекущаяСтрока);
	КонецЕсли; 
	
	
	ДетальноеРасписаниеДня.Очистить();
	Для каждого ЭлРасписания Из МассивРасписаниеДня Цикл
		Представление = "";
		Если ЗначениеЗаполнено(ЭлРасписания.ВремяНачала) Тогда
		
			Представление = "с " + Формат(ЭлРасписания.ВремяНачала,"ДФ=HH:mm") + " ";
		
		КонецЕсли; 
		
		Если ЗначениеЗаполнено(ЭлРасписания.ВремяКонца) Тогда
		
			Представление = Представление + "по " + Формат(ЭлРасписания.ВремяКонца,"ДФ=HH:mm");
		
		КонецЕсли; 

		Если Представление = "" Тогда
		
			Представление = "весь день";
		
		КонецЕсли;
		
		ДетальноеРасписаниеДня.Добавить(ЭлРасписания,Представление);
	
	КонецЦикла; 
	ОбновитьНадписьРасписание();
	Если ДетальноеРасписаниеДня.Количество() >= Индекс И ДетальноеРасписаниеДня.Количество()*Индекс Тогда
	
		Попытка
		
			ЭлементыФормы.ДетальноеРасписаниеДня.ТекущаяСтрока = ДетальноеРасписаниеДня.Получить(Индекс);
		
		Исключение
		
		КонецПопытки;
	
	КонецЕсли; 
КонецПроцедуры

Процедура ЗаполнитьТППремиальныеТовары()
	фрмПремиальныеТовары = ПремиальныеТовары.Выгрузить();
	
	фрмПремиальныеТовары.Свернуть("Товар,Цена,Количество");
	фрмПремиальныеТовары.Колонки.Добавить("НомерСтроки");
	фрмПремиальныеТовары.Колонки.Добавить("Станции",Новый ОписаниеТипов("СписокЗначений"));
	Для каждого Т Из фрмПремиальныеТовары Цикл
	
		Т.НомерСтроки = фрмПремиальныеТовары.Индекс(Т)+1;
		Т.Станции = Новый СписокЗначений;
		МС = ПремиальныеТовары.НайтиСтроки(Новый Структура("Товар", Т.Товар));
		Для каждого Ст Из МС Цикл
		   	Если Т.Станции.НайтиПоЗначению(Ст.Станция) = Неопределено Тогда
				Т.Станции.Добавить(Ст.Станция);	
			КонецЕсли;
		КонецЦикла; 
		
	
	КонецЦикла; 
КонецПроцедуры

Процедура ДеньНеделиПриИзменении(Элемент)
	М = Новый Массив;
	Для Инд=1 По 7 Цикл
		
		Если ЭтаФорма["ДеньНедели"+Инд] Тогда
			М.Добавить(Инд);
		КонецЕсли;
		
	КонецЦикла; 
	Расписание.ДниНедели = М;
	ОбновитьНадписьРасписание();
КонецПроцедуры

Процедура ВремяПриИзменении(Элемент)
	Если ДетальноеРасписаниеДня.Количество() = 0 Тогда
		КПРасписаниеДобавить("");
	КонецЕсли;
	
	Индекс = ДетальноеРасписаниеДня.Индекс(ЭлементыФормы.ДетальноеРасписаниеДня.ТекущаяСтрока);
	Эл=МассивРасписаниеДня[Индекс];
	Эл.ВремяНачала = ВремяНачала;
	Эл.ВремяКонца = ВремяКонца;
	Расписание.ДетальныеРасписанияДня = МассивРасписаниеДня;
	ЗаполнитьСЗРасписание();
КонецПроцедуры

Процедура ДетальноеРасписаниеДняПриАктивизацииСтроки(Элемент)
	Если ЭлементыФормы.ДетальноеРасписаниеДня.ТекущаяСтрока = Неопределено Тогда
	
		Возврат;
	
	КонецЕсли; 
	Индекс = ДетальноеРасписаниеДня.Индекс(ЭлементыФормы.ДетальноеРасписаниеДня.ТекущаяСтрока);
	Эл=МассивРасписаниеДня[Индекс];
	ВремяНачала = Эл.ВремяНачала;
	ВремяКонца = Эл.ВремяКонца;
КонецПроцедуры

Процедура ПремиальныеТоварыПередНачаломДобавления(Элемент, Отказ, Копирование)
	
КонецПроцедуры

Процедура фрмПремиальныеТоварыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда
		Элемент.ТекущаяСтрока.Количество = 1;
		Элемент.ТекущаяСтрока.Цена = 0;
	КонецЕсли;
КонецПроцедуры

Процедура ЦелевыеТоварыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда
		Элемент.ТекущаяСтрока.Количество = 1;
	КонецЕсли;
КонецПроцедуры

Процедура ВидАкцииПриИзменении(Элемент)
	ТипАкции = ВидАкции.Тип;
	ВидимостьДоступность();
КонецПроцедуры

Процедура ОформитьПолеЦвета()
	ЭлементыФормы.фЦвет.ЦветФона = фЦвет;
	ЭлементыФормы.фЦвет.ЦветФонаПоля = фЦвет;
    ЭлементыФормы.фЦвет.ЦветРамки = фЦвет;
КонецПроцедуры

Процедура фЦветПриИзменении(Элемент)
	ОформитьПолеЦвета();
КонецПроцедуры

Если ЗначениеЗаполнено(СериализованноеРасписание) Тогда
	Расписание = ЗначениеИзСтрокиВнутр(СериализованноеРасписание);
	МассивРасписаниеДня = Расписание.ДетальныеРасписанияДня;
	ПроставитьВсеДни = Расписание.ДниНедели.Количество() = 0;
	Для Инд=1 По 7 Цикл
		
		ЭтаФорма["ДеньНедели"+Инд] = Расписание.ДниНедели.Найти(Инд)<>Неопределено ИЛИ ПроставитьВсеДни;
		ЭлементыФормы["ДеньНедели"+Инд].Доступность = Не Проведен;
	КонецЦикла; 
Иначе
	Расписание = Новый РасписаниеРегламентногоЗадания;
	Расписание.ПериодПовтораДней = 1;
	МассивРасписаниеДня = Новый Массив;
	РасписаниеДня = Новый РасписаниеРегламентногоЗадания;
	МассивРасписаниеДня.Добавить(РасписаниеДня);
	Расписание.ДетальныеРасписанияДня = МассивРасписаниеДня;
	Для Инд=1 По 7 Цикл
		
		ЭтаФорма["ДеньНедели"+Инд] = Истина;
		
	КонецЦикла; 
КонецЕсли; 

ЗаполнитьСЗРасписание();

ЗаполнитьТППремиальныеТовары();

Цвет = ЦветЦелевыхТоваров.Получить();
Если Цвет <> Неопределено Тогда

	фЦвет = Цвет;
ИНаче
	фЦвет = ЦветаСтиля.ЦветФона2;

КонецЕсли; 