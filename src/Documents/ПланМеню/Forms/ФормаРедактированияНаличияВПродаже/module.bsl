Перем МассивСтрок Экспорт;

// Процедура обновляет информацию о наличии в продаже для массива строк
//
Процедура ОбновитьНаличиеВПродаже()

	Если НЕ ТипЗнч(МассивСтрок) = Тип("Массив") Тогда
		Возврат;
	КонецЕсли;
	
	Если МассивСтрок.Количество() >= 1 Тогда
		
		ТекущаяСтрока = МассивСтрок[0];
		
		СписокКогдаВПродаже.Очистить();
		СписокНедель.Очистить();
		ИмяТЧ = Метаданные.НайтиПоТипу(ТипЗнч(ТекущаяСтрока)).Имя;
		
		// наличие в продаже
		ИнтерфейсАдмина.ЗаполнитьСписокДнейНедели(СписокКогдаВПродаже, ТекущаяСтрока.КогдаВПродаже);
		
		ФлагВремя1		= ЗначениеЗаполнено(ТекущаяСтрока.КогдаВПродажеВремя1);
		ФлагВремя2		= ЗначениеЗаполнено(ТекущаяСтрока.КогдаВПродажеВремя2);
		
		КогдаВПродажеВремя1 = ТекущаяСтрока.КогдаВПродажеВремя1;
		КогдаВПродажеВремя2 = ТекущаяСтрока.КогдаВПродажеВремя2;
		
		Если ИмяТЧ = "Товары" Тогда
			СтрокиПоНеделям = ДокументОбъект.КогдаВПродажеПоНеделям.НайтиСтроки(Новый Структура("ИдентификаторТовара", ТекущаяСтрока.ИдентификаторТовара));
			мКогдаВПродажеПоНеделям = ДокументОбъект.КогдаВПродажеПоНеделям.Выгрузить().Скопировать(СтрокиПоНеделям);
			
			ФлагНаличиеПоНеделям = мКогдаВПродажеПоНеделям.Количество() <> 0;
			
			ДатаПН = НачалоНедели(ТекущаяДата());
			Для н=1 По 10 Цикл
				Пометка = мКогдаВПродажеПоНеделям.Найти(ДатаПН, "ДатаПонедельника") <> Неопределено;
				СписокНедель.Добавить(ДатаПН, "Неделя № "+НеделяГода(ДатаПН)+", "+Формат(ДатаПН,"ДЛФ=Д")+" - "+Формат(КонецНедели(ДатаПН),"ДЛФ=Д"), Пометка);
				ДатаПН = КонецНедели(ДатаПН) + 1;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

Процедура УправлениеВидимостью()
	
	Если МассивСтрок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока = МассивСтрок[0];
	ИмяТЧ = Метаданные.НайтиПоТипу(ТипЗнч(ТекущаяСтрока)).Имя;
	
	Если ИмяТЧ = "Товары" Тогда
		ЭлементыФормы.ПанельНаличиеПоНеделям.Свертка = РежимСверткиЭлементаУправления.Нет;
	Иначе
		ЭлементыФормы.ПанельНаличиеПоНеделям.Свертка = РежимСверткиЭлементаУправления.Верх;
	КонецЕсли;
	
	ЭлементыФормы.ИмяТабличнойЧасти.Заголовок = ИмяТЧ;
	Если МассивСтрок.Количество() = 1 Тогда
		ЭлементыФормы.НомерСтроки.Заголовок = ТекущаяСтрока.НомерСтроки;
		ИнформацияОТоваре = ТекущаяСтрока.Номенклатура.Наименование;
	ИначеЕсли МассивСтрок.Количество() > 1 Тогда
		ЭлементыФормы.НомерСтроки.Заголовок = "Несколько строк";
		ИнформацияОТоваре = "Несколько строк";
	КонецЕсли;
	ЭлементыФормы.Номенклатура.Заголовок = ИнформацияОТоваре;
	
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если НЕ ТипЗнч(КлючУникальности) = Тип("ДокументОбъект.ПланМеню") Тогда
		Предупреждение("Эта обработка предназначена для вызова из документа ""План-меню""");
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновлениеОтображения()
	
	ЭлементыФормы.СписокНедель.Доступность = ФлагНаличиеПоНеделям;
	ЭлементыФормы.КогдаВПродажеВремя1.Доступность = ФлагВремя1;
	ЭлементыФормы.КогдаВПродажеВремя2.Доступность = ФлагВремя2;
	
КонецПроцедуры

// Обработчик события ОбработкаОповещения формы.
//
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "АктивизацияСтрокиТабличнойЧастиДокументаПланМеню" И ДокументОбъект = Источник Тогда
		
		//МассивСтрок = Новый Массив;
		//МассивСтрок.Добавить(Параметр);
		МассивСтрок = Параметр;
		УправлениеВидимостью();
		ОбновитьНаличиеВПродаже();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура КогдаВПродажеВремя1НачалоВыбора(Элемент, СтандартнаяОбработка)
	
	ИнтерфейсАдмина.ВыбратьВремяИзСписка(ЭтаФорма, Элемент);
	СохранитьИзменения();
	
КонецПроцедуры

Процедура КоманднаяПанельКогдаВПродажеУстановитьФлажки(Кнопка)
	
	СписокКогдаВПродаже.ЗаполнитьПометки(Истина);
	СохранитьИзменения();
	
КонецПроцедуры

Процедура КоманднаяПанельКогдаВПродажеСнятьФлажки(Кнопка)
	
	СписокКогдаВПродаже.ЗаполнитьПометки(Ложь);
	СохранитьИзменения();
	
КонецПроцедуры

Процедура СохранитьИзменения()
	
	Для каждого ТекущаяСтрока Из МассивСтрок Цикл
		// наличие в продаже
		ТекущаяСтрока.КогдаВПродаже			= ИнтерфейсАдмина.СписокПометокВЧисло(СписокКогдаВПродаже);
		ТекущаяСтрока.КогдаВПродажеВремя1	= ?(ФлагВремя1, КогдаВПродажеВремя1, Неопределено);
		ТекущаяСтрока.КогдаВПродажеВремя2	= ?(ФлагВремя2, КогдаВПродажеВремя2, Неопределено);
		
		СтрокиПоНеделям = ДокументОбъект.КогдаВПродажеПоНеделям.НайтиСтроки(Новый Структура("ИдентификаторТовара", ТекущаяСтрока.ИдентификаторТовара));
		Для каждого СтрокаКУдалению Из СтрокиПоНеделям Цикл
			ДокументОбъект.КогдаВПродажеПоНеделям.Удалить(СтрокаКУдалению);
		КонецЦикла;
		
		Если ФлагНаличиеПоНеделям Тогда
			Для каждого Неделя Из СписокНедель Цикл
				Если Неделя.Пометка Тогда
					НоваяСтрока = ДокументОбъект.КогдаВПродажеПоНеделям.Добавить();
					НоваяСтрока.ДатаПонедельника	= Неделя.Значение;
					НоваяСтрока.ИдентификаторТовара	= ТекущаяСтрока.ИдентификаторТовара;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры


Процедура СписокКогдаВПродажеПриИзмененииФлажка(Элемент)
	
	СохранитьИзменения();
	
КонецПроцедуры

Процедура ФлагНаличиеПоНеделямПриИзменении(Элемент)
	
	СохранитьИзменения();
	мФлагНаличиеПоНеделям = ФлагНаличиеПоНеделям;
	ОбновитьНаличиеВПродаже();
	ФлагНаличиеПоНеделям = мФлагНаличиеПоНеделям;
	
КонецПроцедуры


Процедура СписокНедельПриИзмененииФлажка(Элемент)
	
	СохранитьИзменения();
	
КонецПроцедуры

