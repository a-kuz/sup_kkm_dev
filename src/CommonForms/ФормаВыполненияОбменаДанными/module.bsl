
// процедура по настройке обмена данными устанавливает параметры
Процедура УстановитьПараметрыОбменаПоНастройке()
	
	ДанныеОбОбмене = "";
	
	НаличиеНастройки = ОпределитьНаличиеНастройки();
	
	Если Не НаличиеНастройки Тогда
		Возврат;
	КонецЕсли;
		
	// настройка имеется
	ОбъектНастройки = НастройкаОбменаДанными.ПолучитьОбъект();
		
	СтрокаОперации = "Выполняемые действия: ";
		
	Если ОбъектНастройки.ПроизводитьПриемСообщений 
		И ОбъектНастройки.ПроизводитьОтправкуСообщений Тогда
			
		СтрокаОперации = СтрокаОперации + "Загрузка и выгрузка данных";
			
	ИначеЕсли ОбъектНастройки.ПроизводитьПриемСообщений Тогда
			
		СтрокаОперации = СтрокаОперации + "Загрузка данных";
			
	ИначеЕсли ОбъектНастройки.ПроизводитьОтправкуСообщений Тогда
			
		СтрокаОперации = СтрокаОперации + "Выгрузка данных";
			
	КонецЕсли;
		
	ДанныеОбОбмене = ДанныеОбОбмене + СтрокаОперации + Символы.ПС;
	
	СтрокаТипаОбмена = "Тип обмена: ";
	
	// в ОбъектНастройки.ТипНастройки от типа обмена устанавливаем нужную настройку
	Если ОбъектНастройки.ТипНастройки = Перечисления.ТипыАвтоматическогоОбменаДанными.ОбменЧерезФайловыйРесурс Тогда
		
		СтрокаТипаОбмена = СтрокаТипаОбмена + " Обмен через файловый ресурс";
		СтрокаПараметровОбмена = "Каталог обмена: " + ?(НЕ ЗначениеЗаполнено(ОбъектНастройки.КаталогОбменаИнформацией), "не задан", ОбъектНастройки.КаталогОбменаИнформацией);
		
	ИначеЕсли ОбъектНастройки.ТипНастройки = Перечисления.ТипыАвтоматическогоОбменаДанными.ОбменЧерезFTPРесурс Тогда
		
		СтрокаТипаОбмена = СтрокаТипаОбмена + " Обмен через FTP ресурс";
		СтрокаПараметровОбмена = "FTP каталог обмена: " + ?(НЕ ЗначениеЗаполнено(ОбъектНастройки.FTPАдресОбмена), "не задан", ОбъектНастройки.FTPАдресОбмена);
		
	ИначеЕсли ОбъектНастройки.ТипНастройки = Перечисления.ТипыАвтоматическогоОбменаДанными.ОбменЧерезПочту Тогда
		
		СтрокаТипаОбмена = СтрокаТипаОбмена + " Обмен через E-Mail";
		СтрокаПараметровОбмена = "Учетная запись приема и отправки сообщений: " + ?(НЕ ЗначениеЗаполнено(ОбъектНастройки.ПочтовыйАдресПолучателя), "не задана", ОбъектНастройки.ПочтовыйАдресПолучателя);
		
	КонецЕсли;
	
	ДанныеОбОбмене = ДанныеОбОбмене + СтрокаТипаОбмена + Символы.ПС;
	ДанныеОбОбмене = ДанныеОбОбмене + СтрокаПараметровОбмена + Символы.ПС + Символы.ПС;
	
	// комментарий к настройке надо вывести
	ДанныеОбОбмене = ДанныеОбОбмене + "Комментарий к настройке обмена: " + ОбъектНастройки.Комментарий + Символы.ПС + Символы.ПС;
	
	ДанныеОПоследнемОбмене = ПеречитатьПараметрыНастройки();
	
	ДанныеОбОбмене = ДанныеОбОбмене + ДанныеОПоследнемОбмене;
	
КонецПроцедуры

//Функция определяет наличие настройки обмена
Функция ОпределитьНаличиеНастройки()
	
	Если НЕ ЗначениеЗаполнено(НастройкаОбменаДанными) Тогда
		Возврат Ложь;
	КонецЕсли;	
			
	ОбъектНастройки = НастройкаОбменаДанными.ПолучитьОбъект();
		
	Если ОбъектНастройки = Неопределено Тогда
		Возврат Ложь;
	Конецесли;
	
	Возврат Истина;
	
КонецФункции

// нажатие на кнопку выполнить
Процедура ОсновныеДействияФормыВыполнить(Кнопка)
	
	Если НЕ ОпределитьНаличиеНастройки() Тогда
		Предупреждение("Не выбрана настройка для проведения обмена данными");
		Возврат;
	КонецЕсли;
	
	МассивОбменов = Новый Массив();
	МассивОбменов.Добавить(НастройкаОбменаДанными);

	ПроцедурыОбменаДанными.ПроизвестиСписокОбменовДанными(МассивОбменов, Истина, глОбработкаАвтоОбменДанными);
		
	УстановитьПараметрыОбменаПоНастройке();
	
КонецПроцедуры

// при открытии формы
Процедура ПриОткрытии()
	
	НастройкаОбменаДанными = ВосстановитьЗначение("ОбщиеФормы.ФормаЗапускаОбменаДанными.НастройкаОбменаДанными");
	
	НаличиеНастройки = ОпределитьНаличиеНастройки();
	
	Если НаличиеНастройки Тогда
		
		УстановитьПараметрыОбменаПоНастройке();
		Возврат;
		
	КонецЕсли;
	
	НастройкаОбменаДанными = Справочники.НастройкиОбменаДанными.ПустаяСсылка();
	
	// сначала ищем первую настройку у которой пользователь помечен как ответственный за обмен
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ Разрешенные первые 1
				   |	НастройкиОбменаДанными.Ссылка
				   |ИЗ
				   |	Справочник.НастройкиОбменаДанными КАК НастройкиОбменаДанными
				   | ГДЕ 
				   |	(НастройкиОбменаДанными.Ответственный = &ТекущийПользователь)";
					   
	Запрос.УстановитьПараметр("ТекущийПользователь", ПараметрыСеанса.ТекущийПользователь);			   
		
	Таблица = Запрос.Выполнить().Выгрузить();
		
	Если Таблица.Количество() <> 0 Тогда
			
		НастройкаОбменаДанными = Таблица[0].Ссылка;	
		УстановитьПараметрыОбменаПоНастройке();
		Возврат;
		
	КонецЕсли;
	
	// ищем просто первую попавшуюся настройку
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ Разрешенные первые 1
				   |	НастройкиОбменаДанными.Ссылка
				   |ИЗ
				   |	Справочник.НастройкиОбменаДанными КАК НастройкиОбменаДанными
				   |";
					   
	Таблица = Запрос.Выполнить().Выгрузить();
		
	Если Таблица.Количество() <> 0 Тогда
			
		НастройкаОбменаДанными = Таблица[0].Ссылка;
		УстановитьПараметрыОбменаПоНастройке();
		Возврат;
			
	КонецЕсли;

	// в общем ни одна настройка не доступна для выбора
	ПредложитьСоздатьНовуюНастройкуОбмена();
	
КонецПроцедуры

//процедура спрашивает пользователя о необходимости создания новой настройки обмена
Процедура ПредложитьСоздатьНовуюНастройкуОбмена()
	
	// в общем ни одна настройка не доступна для выбора
	ОтветПользователя = Вопрос("Нет ни одной настройки обмена данными доступной для выполнения. Хотите создать новую настройку обмена?",
		РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да, "Настройки обмена данными");
		
	Если ОтветПользователя <> КодВозвратаДиалога.Да Тогда
		Возврат;	
	КонецЕсли;
	
	СоздатьИВыбратьНастройкуОбменаДанными();
	
КонецПроцедуры

// процедура создает новую настройку для обмена и помещает ее в поле выбора
Процедура СоздатьИВыбратьНастройкуОбменаДанными()
	
	НоваяНастройкаОбмена = Справочники.НастройкиОбменаДанными.СоздатьЭлемент();
		
	ФормаЭлемента = НоваяНастройкаОбмена.ПолучитьФорму("ФормаЭлемента", ЭтаФорма);
	ФормаЭлемента.ОткрытьМодально();
		
	Если НЕ ЗначениеЗаполнено(НоваяНастройкаОбмена.Ссылка) Тогда
		Возврат;
	Иначе
		НастройкаОбменаДанными = НоваяНастройкаОбмена.Ссылка;
		УстановитьПараметрыОбменаПоНастройке();
	КонецЕсли;
	
КонецПроцедуры

// выбор настроек обмена из списка
Процедура НастройкаОбменаДаннымиНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	// выпадающий список
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ Разрешенные
				   |	НастройкиОбменаДанными.Ссылка
				   |ИЗ
				   |	Справочник.НастройкиОбменаДанными КАК НастройкиОбменаДанными
				   |";
				   
	Таблица = Запрос.Выполнить().Выгрузить();
	
	Если Таблица.Количество() = 0 Тогда
		
		ПредложитьСоздатьНовуюНастройкуОбмена();
		
	Иначе	
		
		// выбираем настройку обмена из списка
		СписокВозврата = Новый СписокЗначений;
		
		Для Каждого СтрокаТаблицы Из Таблица Цикл
			
			СписокВозврата.Добавить(СтрокаТаблицы.Ссылка);
			
		КонецЦикла;
		
		СписокВозврата.СортироватьПоПредставлению();
		
		СписокВозврата.Добавить("Добавить новую настройку обмена");
		
		
		НачальноеЗначение = СписокВозврата.НайтиПоЗначению(Элемент.Значение);
		ВыбранныйЭлемент = ЭтаФорма.ВыбратьИзСписка(СписокВозврата, Элемент, НачальноеЗначение);
		
		// ничего не выбрали
		Если ВыбранныйЭлемент = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Если ВыбранныйЭлемент.Значение = "Добавить новую настройку обмена" Тогда 
			
			СоздатьИВыбратьНастройкуОбменаДанными();
			
		Иначе
			
			НастройкаОбменаДанными = ВыбранныйЭлемент.Значение;
			УстановитьПараметрыОбменаПоНастройке();
			
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

// нажатие на кнопку настройки обмена данными
Процедура ОсновныеДействияФормыНастройкиОбмена(Кнопка)
	
	ФормаСписка = Справочники.НастройкиОбменаДанными.ПолучитьФормуСписка("ФормаСписка", ЭтаФорма); 
	ФормаСписка.Открыть();
	
КонецПроцедуры

Процедура ОсновныеДействияФормыПрименитьИзмененияВКонфигурации(Кнопка)
			
	Если НЕ ОпределитьНаличиеНастройки() Тогда
		Предупреждение("Не выбрана настройка для проведения изменений в конфигурации");
		Возврат;
	Иначе
		ПроцедурыОбменаДанными.ОбновитьКонфигурациюБазыДанных(НастройкаОбменаДанными);
	КонецЕсли;
			
КонецПроцедуры

// Функция перечитывает данные параметров настройки
Функция ПеречитатьПараметрыНастройки()
	
	ДанныеОПоследнемОбмене = "Информация о последнем обмене:" + Символы.ПС;
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
				   |	ПараметрыОбменаДанными.ДатаПоследнейЗагрузки,
				   |	ПараметрыОбменаДанными.ДатаПоследнейВыгрузки,
				   |	ПараметрыОбменаДанными.РезультатПоследнейЗагрузки,
				   |	ПараметрыОбменаДанными.РезультатПоследнейВыгрузки
				   |ИЗ
				   |	РегистрСведений.ПараметрыОбменаДанными КАК ПараметрыОбменаДанными
				   |	ГДЕ (ПараметрыОбменаДанными.НастройкаОбменаДанными = &Ссылка)";
				   
	Запрос.УстановитьПараметр("Ссылка", НастройкаОбменаДанными); 
	
	Выборка = Запрос.Выполнить().Выбрать();

	Если Выборка.Следующий() Тогда
		
		ДатаПоследнейЗагрузки = Выборка.ДатаПоследнейЗагрузки;
		ДатаПоследнейВыгрузки = Выборка.ДатаПоследнейВыгрузки;
		РезультатПоследнейЗагрузки = Выборка.РезультатПоследнейЗагрузки;
		РезультатПоследнейВыгрузки = Выборка.РезультатПоследнейВыгрузки;
			
	Иначе
	
		ДатаПоследнейЗагрузки = Неопределено;
		ДатаПоследнейВыгрузки = Неопределено;
		РезультатПоследнейЗагрузки = Ложь;
		РезультатПоследнейВыгрузки = Ложь;
		
	КонецЕсли;
	
	СтрокаПоследнейЗагрузки = "  Загрузка данных: ";
	СтрокаПоследнейВыгрузки = "  Выгрузка данных: ";
	
	Если НастройкаОбменаДанными.ПроизводитьПриемСообщений Тогда
		Если НЕ ЗначениеЗаполнено(ДатаПоследнейЗагрузки) Тогда
			СтрокаПоследнейЗагрузки = СтрокаПоследнейЗагрузки + "не выполнялась";
		Иначе
			СтрокаПоследнейЗагрузки = СтрокаПоследнейЗагрузки + Строка(ДатаПоследнейЗагрузки);
			Если РезультатПоследнейЗагрузки = Истина Тогда 
				СтрокаПоследнейЗагрузки = СтрокаПоследнейЗагрузки + " (успешно)";
			Иначе
				СтрокаПоследнейЗагрузки = СтрокаПоследнейЗагрузки + " (с ошибками)";
			КонецЕсли;
		КонецЕсли;
	Иначе
		СтрокаПоследнейЗагрузки = СтрокаПоследнейЗагрузки + " не производится";
	КонецЕсли;
	
	Если НастройкаОбменаДанными.ПроизводитьОтправкуСообщений Тогда
		Если НЕ ЗначениеЗаполнено(ДатаПоследнейВыгрузки) Тогда
			СтрокаПоследнейВыгрузки = СтрокаПоследнейВыгрузки + "не выполнялась";
		Иначе
			СтрокаПоследнейВыгрузки = СтрокаПоследнейВыгрузки + Строка(ДатаПоследнейВыгрузки);
			
			Если РезультатПоследнейВыгрузки = Истина Тогда 
				СтрокаПоследнейВыгрузки = СтрокаПоследнейВыгрузки +  " (успешно)";
			Иначе
				СтрокаПоследнейВыгрузки = СтрокаПоследнейВыгрузки + " (с ошибками)";
			КонецЕсли;
		КонецЕсли;
	Иначе
		СтрокаПоследнейВыгрузки = СтрокаПоследнейВыгрузки + " не производится";
	КонецЕсли;
	
	ДанныеОПоследнемОбмене = ДанныеОПоследнемОбмене + СтрокаПоследнейЗагрузки + Символы.ПС;
	ДанныеОПоследнемОбмене = ДанныеОПоследнемОбмене + СтрокаПоследнейВыгрузки;
	
	Возврат ДанныеОПоследнемОбмене;
	
КонецФункции

// при изменении настройки обмена
Процедура НастройкаОбменаДаннымиПриИзменении(Элемент)
	
	УстановитьПараметрыОбменаПоНастройке();
		
КонецПроцедуры

// обновить данные
Процедура КоманднаяПанель1Обновить(Кнопка)
	
	УстановитьПараметрыОбменаПоНастройке();
	
КонецПроцедуры

// при закрытии формы выполнения обмена
Процедура ПриЗакрытии()
	
	СохранитьЗначение("ОбщиеФормы.ФормаЗапускаОбменаДанными.НастройкаОбменаДанными", НастройкаОбменаДанными);
	
КонецПроцедуры

