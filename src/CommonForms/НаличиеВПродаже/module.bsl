
Перем ОбъектЗаписи;

Функция ПолучитьРасшифровкуНаличия() Экспорт
	
	ПрочитатьНаличие();
	
	Если Товар.ВариантНаличияВПродаже = Перечисления.ВариантыНаличияВПродаже.ПростойПоИБ Тогда
		Возврат ?(ЕстьВПродаже, "Есть", "Нет");
	КонецЕсли;
	
	Расшифровка = "";
	
	Если КогдаВПродаже=0 Тогда
		Расшифровка = "Нет в продаже";
		
	ИначеЕсли КогдаВПродаже=127 И НЕ (ФлагНаличиеПоНеделям ИЛИ ФлагВремя1 ИЛИ ФлагВремя2) Тогда
		Расшифровка = "Всегда в продаже";
		
	Иначе
		
		//-------------------------------
		Если КогдаВПродаже=127 Тогда
			РасшифровкаПоДням = "";
		Иначе
			РасшифровкаПоДням = ?(СписокДниНедели[0].Пометка,"ПН,","") +
								?(СписокДниНедели[1].Пометка,"ВТ,","") +
								?(СписокДниНедели[2].Пометка,"СР,","") +
								?(СписокДниНедели[3].Пометка,"ЧТ,","") +
								?(СписокДниНедели[4].Пометка,"ПТ,","") +
								?(СписокДниНедели[5].Пометка,"СБ,","") +
								?(СписокДниНедели[6].Пометка,"ВС,","") ;
								
			РасшифровкаПоДням = Лев(РасшифровкаПоДням, СтрДлина(РасшифровкаПоДням)-1 ) + Символы.ПС;
		КонецЕсли;
		
		//-------------------------------
		РасшифровкаПоВремени="";
		Если ФлагВремя1 И ЗначениеЗаполнено(Время1) И ФлагВремя2 И ЗначениеЗаполнено(Время2) Тогда
			РасшифровкаПоВремени = "время с " + Формат(Время1,"ДФ=ЧЧ:мм") + " по " + Формат(Время2,"ДФ=ЧЧ:мм") + Символы.ПС;
		ИначеЕсли ФлагВремя1 И ЗначениеЗаполнено(Время1) Тогда
			РасшифровкаПоВремени = "после " + Формат(Время1,"ДФ=ЧЧ:мм") + Символы.ПС;
		ИначеЕсли ФлагВремя2 И ЗначениеЗаполнено(Время2) Тогда
			РасшифровкаПоВремени = "до " + Формат(Время2,"ДФ=ЧЧ:мм") + Символы.ПС;
		КонецЕсли;
		
		//-------------------------------
		РасшифровкаПоНеделям = "";
		Если ФлагНаличиеПоНеделям Тогда
			Для каждого Неделя Из СписокНедель Цикл
				Если Неделя.Пометка Тогда
					РасшифровкаПоНеделям = РасшифровкаПоНеделям + Неделя.Представление + Символы.ПС;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Расшифровка = РасшифровкаПоДням + РасшифровкаПоВремени + Символы.ПС + РасшифровкаПоНеделям;
		
	КонецЕсли;
	
	Возврат Расшифровка;
КонецФункции

Процедура ЗаполнитьНаличиеПоИБ(ТаблицаИБ) Экспорт
	
	ТаблицаИБ.Очистить();
	
	Запрос = Новый Запрос("ВЫБРАТЬ Ссылка ИЗ Справочник.ИнформационныеБазы
							|ГДЕ НЕ ПометкаУдаления");
							
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ИнформационнаяБаза = Выборка.Ссылка;
		СтрокаИБ = ТаблицаИБ.Добавить();
		СтрокаИБ.ИБ = ИнформационнаяБаза;
		СтрокаИБ.Значение = СтрЗаменить( ПолучитьРасшифровкуНаличия(), Символы.ПС, " ");
	КонецЦикла; 
	
КонецПроцедуры
 
Процедура ПрочитатьНаличие()

	Если Товар.ВариантНаличияВПродаже = Перечисления.ВариантыНаличияВПродаже.ПростойПоИБ ИЛИ
		 Товар.ВариантНаличияВПродаже = Перечисления.ВариантыНаличияВПродаже.СложныйПоИБ Тогда
		 
		ОбъектЗаписи = Товар.КогдаВПродажеПоИБ.Найти(ИнформационнаяБаза, "ИнформационнаяБаза");
	Иначе
		ОбъектЗаписи = Товар;
	КонецЕсли;
	
	Если Товар.ВариантНаличияВПродаже = Перечисления.ВариантыНаличияВПродаже.ПростойПоИБ Тогда
		ЕстьВПродаже = ОбъектЗаписи <> Неопределено И ОбъектЗаписи.ЕстьВПродаже;
		Возврат;
	КонецЕсли; 
	
	Если ОбъектЗаписи = Неопределено Тогда
		КогдаВПродаже	= 0;
		Время1			= Неопределено;
		Время2			= Неопределено;
	Иначе
		КогдаВПродаже	= ОбъектЗаписи.КогдаВПродаже;
		Время1			= ОбъектЗаписи.КогдаВПродажеВремя1;
		Время2			= ОбъектЗаписи.КогдаВПродажеВремя2;
	КонецЕсли; 
	
	ИнтерфейсАдмина.ЗаполнитьСписокДнейНедели(СписокДниНедели, КогдаВПродаже);
	ФлагВремя1	= ЗначениеЗаполнено(Время1);
	ФлагВремя2	= ЗначениеЗаполнено(Время2);
	
	СписокНедель.Очистить();
	ФлагНаличиеПоНеделям = Ложь;
	
	ДатаПН = НачалоНедели(ТекущаяДата());
	Для н=1 По 10 Цикл
		
		Если Товар.ВариантНаличияВПродаже = Перечисления.ВариантыНаличияВПродаже.СложныйПоИБ Тогда
			Пометка = Ложь;
			СтрокиНедели = Товар.КогдаВПродажеПоНеделям.НайтиСтроки( Новый Структура("ИнформационнаяБаза", ИнформационнаяБаза) );
			Для каждого Неделя Из СтрокиНедели Цикл
				Если Неделя.ДатаПонедельника = ДатаПН Тогда
					Пометка = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла; 
		Иначе
			Пометка = Товар.КогдаВПродажеПоНеделям.Найти(ДатаПН, "ДатаПонедельника") <> Неопределено;
		КонецЕсли; 
		
		СписокНедель.Добавить(ДатаПН, "Неделя № "+НеделяГода(ДатаПН)+", "+Формат(ДатаПН,"ДЛФ=Д")+" - "+Формат(КонецНедели(ДатаПН),"ДЛФ=Д"), Пометка );
		ДатаПН = КонецНедели(ДатаПН) + 1;
		ФлагНаличиеПоНеделям = ФлагНаличиеПоНеделям ИЛИ Пометка;
	КонецЦикла;

КонецПроцедуры
 
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	ПрочитатьНаличие();
	
	Если Товар.ВариантНаличияВПродаже = Перечисления.ВариантыНаличияВПродаже.ПростойПоИБ Тогда
		Отказ = Истина;
		// надо просто поменять признак наличия
		Если ОбъектЗаписи = Неопределено Тогда
			ОбъектЗаписи = Товар.КогдаВПродажеПоИБ.Добавить();
			ОбъектЗаписи.ИнформационнаяБаза = ИнформационнаяБаза;
		КонецЕсли; 
		
		ОбъектЗаписи.ЕстьВПродаже = НЕ ЕстьВПродаже;
	КонецЕсли; 
	
	ТолькоПросмотр = ВладелецФормы.ТолькоПросмотр;
	Если ТолькоПросмотр Тогда
		ЭлементыФормы.ОсновныеДействияФормы.Кнопки.ОК.Доступность = Ложь;
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбновлениеОтображения()
	
	ЭлементыФормы.СписокНедель.Доступность = ФлагНаличиеПоНеделям;
	ЭлементыФормы.Время1.Доступность = ФлагВремя1;
	ЭлементыФормы.Время2.Доступность = ФлагВремя2;
		
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура ОсновныеДействияФормыОК(Кнопка)
	
	Если Товар.ВариантНаличияВПродаже = Перечисления.ВариантыНаличияВПродаже.СложныйПоИБ Тогда
		Если ОбъектЗаписи = Неопределено Тогда
			ОбъектЗаписи = Товар.КогдаВПродажеПоИБ.Добавить();
			ОбъектЗаписи.ИнформационнаяБаза = ИнформационнаяБаза;
		КонецЕсли;
		
		СтрокиНедели = Товар.КогдаВПродажеПоНеделям.НайтиСтроки( Новый Структура("ИнформационнаяБаза", ИнформационнаяБаза) );
		Для каждого Неделя Из СтрокиНедели Цикл
			Товар.КогдаВПродажеПоНеделям.Удалить(Неделя);
		КонецЦикла; 
		
	Иначе
		Товар.КогдаВПродажеПоНеделям.Очистить();
		ИнформационнаяБаза = Неопределено;
	КонецЕсли;
	
	ОбъектЗаписи.КогдаВПродаже = ИнтерфейсАдмина.СписокПометокВЧисло(СписокДниНедели);
	ОбъектЗаписи.КогдаВПродажеВремя1	= ?(ФлагВремя1, Время1, Неопределено);
	ОбъектЗаписи.КогдаВПродажеВремя2	= ?(ФлагВремя2, Время2, Неопределено);
	
	Если ФлагНаличиеПоНеделям Тогда
		Для каждого Неделя Из СписокНедель Цикл
			Если Неделя.Пометка Тогда
				СтрокаТЧ = Товар.КогдаВПродажеПоНеделям.Добавить();
				СтрокаТЧ.ИнформационнаяБаза = ИнформационнаяБаза;
				СтрокаТЧ.ДатаПонедельника = Неделя.Значение;
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли; 
	
	Закрыть();
КонецПроцедуры

Процедура КоманднаяПанельДниНеделиУстановитьФлажки(Кнопка)
	
	СписокДниНедели.ЗаполнитьПометки(Истина);
	
КонецПроцедуры

Процедура КоманднаяПанельДниНеделиСнятьФлажки(Кнопка)
	
	СписокДниНедели.ЗаполнитьПометки(Ложь);
	
КонецПроцедуры

Процедура КогдаВПродажеВремя1НачалоВыбора(Элемент, СтандартнаяОбработка)
	
	ИнтерфейсАдмина.ВыбратьВремяИзСписка(ЭтаФорма, Элемент);
	
КонецПроцедуры

Процедура КогдаВПродажеВремя2НачалоВыбора(Элемент, СтандартнаяОбработка)
	
	ИнтерфейсАдмина.ВыбратьВремяИзСписка(ЭтаФорма, Элемент, Истина);
	
КонецПроцедуры
