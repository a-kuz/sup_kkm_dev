
Перем ПрофильВхода Экспорт;

// Добавление пользователя информационной базы
//
Процедура ДобавитьПользователя()
	
	Перем ИмяНовогоПользователя;
	
	Если НЕ ВвестиСтроку(ИмяНовогоПользователя, "Введите имя пользователя") Тогда
		Возврат;
	КонецЕсли; 
	
	Пользователь = ПользователиИнформационнойБазы.СоздатьПользователя();
	Пользователь.Имя				= ИмяНовогоПользователя;
	Пользователь.ПолноеИмя			= ИмяНовогоПользователя;
	Пользователь.ОсновнойИнтерфейс	= Метаданные.Интерфейсы.РабочееМесто;
	Пользователь.Роли.Добавить(Метаданные.Роли.РабочееМесто);
	Пользователь.Язык				= Метаданные.Языки.Русский;
	Попытка
		Пользователь.Записать();
	Исключение
		Предупреждение(ОписаниеОшибки());
		Возврат;
	КонецПопытки;
	
	ЭлементыФормы.ИмяПользователя.СписокВыбора.Вставить(0, Пользователь.Имя );
	ИмяПользователя = ИмяНовогоПользователя;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	Если Лев(ПрофильВхода,2) = "\\" Тогда
		Стр = Сред(ПрофильВхода,3);
		Инд = Найти(Стр, "\");
		Если Инд = 0 Тогда
			РабочаяСтанция = Стр;
		Иначе
			РабочаяСтанция = Лев(Стр, Инд-1);
			ИмяПользователя = Сред(Стр, Инд+1);
		КонецЕсли; 
	КонецЕсли;
	
	Если ПравоДоступа("Администрирование", Метаданные) Тогда
		МассивПользователей = ПользователиИнформационнойБазы.ПолучитьПользователей();
		Для каждого Пользователь Из МассивПользователей Цикл
			ЭлементыФормы.ИмяПользователя.СписокВыбора.Добавить( Пользователь.Имя );
		КонецЦикла; 
		ЭлементыФормы.ИмяПользователя.СписокВыбора.Добавить( "ДобавитьПользователя", "Добавить нового..." );
		ЭлементыФормы.ИмяПользователя.КнопкаСпискаВыбора = Истина;
		
	Иначе
		ЭлементыФормы.ИмяПользователя.КнопкаСпискаВыбора = Ложь;
		
	КонецЕсли; 
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура РабочаяСтанцияНачалоВыбора(Элемент, СтандартнаяОбработка)
	

	
КонецПроцедуры

Процедура ОКНажатие(Элемент)
	
	Если НЕ( ЗначениеЗаполнено(РабочаяСтанция) И ЗначениеЗаполнено(ИмяПользователя) ) ИЛИ
		ИмяПользователя = "ДобавитьПользователя" Тогда
		
		Предупреждение("Укажите имя компьютера и имя пользователя!");
		Возврат;
	КонецЕсли;
	
	ПрофильВхода = "\\" + РабочаяСтанция + "\" + ИмяПользователя;
	Закрыть("ОК");
	
КонецПроцедуры

Процедура ОтменаНажатие(Элемент)
	
	Закрыть();
	
КонецПроцедуры

Процедура ИмяПользователяОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение = "ДобавитьПользователя" Тогда
		СтандартнаяОбработка = Ложь;
		ДобавитьПользователя();
	КонецЕсли; 
	
КонецПроцедуры


