
////////////////////////////////////////////////////////////////////////////////
// ДЛЯ ФОРМИРОВАНИЯ ОПЕРАТИВНОЙ СВОДКИ В БЭКе

Функция ПолучитьОперативнуюТаблицуРеализации(ТаблицаПараметров) Экспорт
	
	ТекстЗапроса = "
		// ТОВАРЫ
		// товары реализация
		|ВЫБРАТЬ
		|	Ссылка.МестоРеализации.Код			КАК МестоРеализации,
		|	ГруппаОплаты.Код 					КАК ГруппаОплаты,
		|	ВЫБОР КОГДА Товар.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|		ТОГДА Товар.Наименование 
		|		ИНАЧЕ Товар.Номенклатура.Наименование 
		|	КОНЕЦ								КАК Наименование,
 		|	Товар.Номенклатура.Код 				КАК Код,
		|	Количество * Товар.КоэфПересчета	КАК КоличествоПродано,
		|	СуммаРеализации						КАК СуммаПродано,
		|	0 									КАК КоличествоСписать,
		|	0 									КАК СуммаСписать,
		|	0 									КАК КоличествоВозврат,
		|	0 									КАК СуммаВозврат
		|ИЗ
		|	Документ.Заказ.Товары
		|ГДЕ
		|	НЕ Ссылка.ПометкаУдаления
		|	И Ссылка.МестоРеализации.Код = &КодМестаРеализации
		|	И Ссылка.Дата >= &ДатаС
		|	И Количество <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ

		// товары возвраты
		|ВЫБРАТЬ
		|	Ссылка.МестоРеализации.Код			КАК МестоРеализации,
		|	ГруппаОплаты.Код 					КАК ГруппаОплаты,
		|	ВЫБОР КОГДА Товар.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|		ТОГДА Товар.Наименование 
		|		ИНАЧЕ Товар.Номенклатура.Наименование 
		|	КОНЕЦ								КАК Наименование,
 		|	Товар.Номенклатура.Код 				КАК Код,
		|	0									КАК КоличествоПродано,
		|	0									КАК СуммаПродано,
		|	0 									КАК КоличествоСписать,
		|	0 									КАК СуммаСписать,
		|	Количество * Товар.КоэфПересчета	КАК КоличествоВозврат,
		|	Сумма								КАК СуммаВозврат
		|ИЗ
		|	Документ.Возврат.Товары
		|ГДЕ
		|	НЕ Ссылка.ПометкаУдаления
		|	И Ссылка.МестоРеализации.Код = &КодМестаРеализации
		|	И Ссылка.Причина.Списание
		|	И Ссылка.Дата >= &ДатаС
		|
		|ОБЪЕДИНИТЬ ВСЕ

		// товары удаления
		|ВЫБРАТЬ
		|	МестоРеализации.Код					КАК МестоРеализации,
		|	ГруппаОплаты.Код 					КАК ГруппаОплаты,
		|	ВЫБОР КОГДА Товар.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|		ТОГДА Товар.Наименование 
		|		ИНАЧЕ Товар.Номенклатура.Наименование 
		|	КОНЕЦ								КАК Наименование,
 		|	Товар.Номенклатура.Код 				КАК Код,
		|	0									КАК КоличествоПродано,
		|	0									КАК СуммаПродано,
		|	Количество * Товар.КоэфПересчета	КАК КоличествоСписать,
		|	Сумма								КАК СуммаСписать,
		|	0									КАК КоличествоВозврат,
		|	0									КАК СуммаВозврат
		|ИЗ
		|	Документ.Удаление
		|ГДЕ
		|	НЕ ПометкаУдаления
		|	И МестоРеализации.Код = &КодМестаРеализации
		|	И Причина.Списание
		|	И Дата >= &ДатаС
		|
		|ОБЪЕДИНИТЬ ВСЕ
		
		// СПЕЦИФИКА (вся специфика идет в списание)
		// специфика реализация
		|ВЫБРАТЬ
		|	Ссылка.МестоРеализации.Код			КАК МестоРеализации,
		|	ГруппаОплаты.Код 					КАК ГруппаОплаты,
		|	Специфика.Номенклатура.Наименование КАК Наименование,
 		|	Специфика.Номенклатура.Код 			КАК Код,
		|	0									КАК КоличествоПродано,
		|	0									КАК СуммаПродано,
		|	Количество * Специфика.КоэфПересчета КАК КоличествоСписать,
		|	0 									КАК СуммаСписать,
		|	0 									КАК КоличествоВозврат,
		|	0 									КАК СуммаВозврат
		|ИЗ
		|	Документ.Заказ.Специфики
		|ГДЕ
		|	НЕ Ссылка.ПометкаУдаления
		|	И Ссылка.МестоРеализации.Код = &КодМестаРеализации
		|	И Ссылка.Дата >= &ДатаС
		|	И Количество <> 0
		|	И Специфика.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Специфики.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ

		// специфика возвраты
		|ВЫБРАТЬ
		|	Ссылка.МестоРеализации.Код			КАК МестоРеализации,
		|	ГруппаОплаты.Код 					КАК ГруппаОплаты,
		|	Специфика.Номенклатура.Наименование КАК Наименование,
 		|	Специфика.Номенклатура.Код 			КАК Код,
		|	0									КАК КоличествоПродано,
		|	0									КАК СуммаПродано,
		|	-Количество * Специфика.КоэфПересчета КАК КоличествоСписать,
		|	0 									КАК СуммаСписать,
		|	0 									КАК КоличествоВозврат,
		|	0 									КАК СуммаВозврат
		|ИЗ
		|	Документ.Возврат.Специфики
		|ГДЕ
		|	НЕ Ссылка.ПометкаУдаления
		|	И Ссылка.МестоРеализации.Код = &КодМестаРеализации
		|	И Ссылка.Причина.Списание
		|	И Ссылка.Дата >= &ДатаС
		|
		|ОБЪЕДИНИТЬ ВСЕ

		// специфика удаления
		|ВЫБРАТЬ
		|	Ссылка.МестоРеализации.Код			КАК МестоРеализации,
		|	Ссылка.ГруппаОплаты.Код 			КАК ГруппаОплаты,
		|	Специфика.Номенклатура.Наименование	КАК Наименование,
 		|	Специфика.Номенклатура.Код 			КАК Код,
		|	0									КАК КоличествоПродано,
		|	0									КАК СуммаПродано,
		|	Ссылка.Количество * Специфика.КоэфПересчета КАК КоличествоСписать,
		|	0	 								КАК СуммаСписать,
		|	0									КАК КоличествоВозврат,
		|	0									КАК СуммаВозврат
		|ИЗ
		|	Документ.Удаление.Специфики
		|ГДЕ
		|	НЕ Ссылка.ПометкаУдаления
		|	И Ссылка.МестоРеализации.Код = &КодМестаРеализации
		|	И Ссылка.Причина.Списание
		|	И Ссылка.Дата >= &ДатаС
		|";
		
	ТаблицаРезультата = ВыполнитьЗапрос(ТекстЗапроса, ТаблицаПараметров);
	ТаблицаРезультата.Свернуть("МестоРеализации, ГруппаОплаты, Наименование, Код",
								"КоличествоПродано, СуммаПродано, КоличествоСписать, СуммаСписать, КоличествоВозврат, СуммаВозврат");
	
	Возврат ТаблицаРезультата;
КонецФункции

Функция ПолучитьОперативнуюТаблицуВыручки(ТаблицаПараметров) Экспорт
	
	ТекстЗапроса = "
		// выручка
		|ВЫБРАТЬ
		|	Ссылка.МестоРеализации.Код			КАК МестоРеализации,
		|	Ссылка.ГруппаОплаты.Код 			КАК ГруппаОплаты,
		|	ВариантОплаты.Наименование			КАК ВариантОплаты,
		|	ВариантОплаты.Тип					КАК ТипОплатыСсылка,
		|	СуммаФакт							КАК Сумма,
		|	0 									КАК СуммаВозврат
		|ИЗ
		|	Документ.ПротоколРасчетов.Протокол
		|ГДЕ
		|	НЕ Ссылка.ПометкаУдаления И НЕ Ссылка.Неучетный
		|	И ВариантОплаты <> ЗНАЧЕНИЕ(Справочник.ВариантыОплаты.ПустаяСсылка)
		|	И Ссылка.МестоРеализации.Код = &КодМестаРеализации
		|	И Ссылка.Дата >= &ДатаС
		|
		|ОБЪЕДИНИТЬ ВСЕ

		// возвраты
		|ВЫБРАТЬ
		|	Ссылка.МестоРеализации.Код			КАК МестоРеализации,
		|	ГруппаОплаты.Код 					КАК ГруппаОплаты,
		|	Ссылка.ВариантОплаты.Наименование	КАК ВариантОплаты,
		|	Ссылка.ВариантОплаты.Тип			КАК ТипОплатыСсылка,
		|	0									КАК Сумма,
		|	Сумма								КАК СуммаВозврат
		|ИЗ
		|	Документ.Возврат.Товары
		|ГДЕ
		|	НЕ Ссылка.ПометкаУдаления
		|	И Ссылка.МестоРеализации.Код = &КодМестаРеализации
		|	И Ссылка.Дата >= &ДатаС
		|";
		
	ТаблицаРезультата = ВыполнитьЗапрос(ТекстЗапроса, ТаблицаПараметров);
	ТаблицаРезультата.Свернуть("МестоРеализации, ГруппаОплаты, ВариантОплаты, ТипОплатыСсылка",
								"Сумма, СуммаВозврат");
								
	ТаблицаРезультата.Колонки.Добавить("ТипОплаты");
	Для каждого Строка Из ТаблицаРезультата Цикл
		Строка.ТипОплаты = Строка(Строка.ТипОплатыСсылка);
	КонецЦикла; 
	ТаблицаРезультата.Колонки.Удалить("ТипОплатыСсылка");
	
	Возврат ТаблицаРезультата;
КонецФункции

Функция ВыполнитьЗапрос(ТекстЗапроса, ТаблицаПараметров)

	Для каждого СтрокаПараметров Из ТаблицаПараметров Цикл
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("КодМестаРеализации"	,СокрЛП(СтрокаПараметров.КодМестаРеализации));
		Запрос.УстановитьПараметр("ДатаС"				,СтрокаПараметров.ДатаНачала);
		
		Результат = Запрос.Выполнить();
		
		Если ТаблицаПараметров.Индекс(СтрокаПараметров)=0 Тогда
			ТаблицаРезультата = Результат.Выгрузить();
		Иначе
			Выборка = Результат.Выбрать();
			Пока Выборка.Следующий() Цикл
				ЗаполнитьЗначенияСвойств(ТаблицаРезультата.Добавить(), Выборка);
			КонецЦикла; 
		КонецЕсли; 
		
	КонецЦикла;
	
	Возврат ТаблицаРезультата;
КонецФункции
