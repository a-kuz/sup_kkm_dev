#Если НЕ ТонкийКлиент Тогда

Функция СотрудникНаРаботе(Сотрудник, МестоРеализации=Неопределено, Время=Неопределено) Экспорт
	
	Если глВерсия = 1 Тогда
		Возврат Ложь;
	КонецЕсли; 
	
	Если МестоРеализации=Неопределено Тогда
		МестоРеализации = глПараметрыРМ.МестоРеализации;
	КонецЕсли; 
	
	Если Время=Неопределено Тогда
		СтруктураРесурсов = РегистрыСведений.ФактическийУчетРабочегоВремени.ПолучитьПоследнее(, Новый Структура("Сотрудник, МестоРеализации", Сотрудник, МестоРеализации));
	Иначе
		СтруктураРесурсов = РегистрыСведений.ФактическийУчетРабочегоВремени.ПолучитьПоследнее(Время, Новый Структура("Сотрудник, МестоРеализации", Сотрудник, МестоРеализации));
	КонецЕсли; 
	
	Возврат СтруктураРесурсов.НаРаботе;
КонецФункции

Функция ОтметитьПриходУход(НаРаботе=Неопределено, Сотрудник=Неопределено, МестоРеализации=Неопределено, Молча=Ложь) Экспорт

	Если глВерсия = 1 Тогда
		Возврат Ложь;
	КонецЕсли; 
	
	Если Сотрудник = Неопределено Тогда
		Сотрудник = ИнтерфейсРМ.ИдентификацияПользователя();
		Если НЕ ЗначениеЗаполнено(Сотрудник) Тогда
			Возврат Ложь;
		КонецЕсли; 
	КонецЕсли; 
	
	Если МестоРеализации = Неопределено Тогда
		МестоРеализации = глПараметрыРМ.МестоРеализации;
	КонецЕсли; 
	
	СейчасНаРаботе = СотрудникНаРаботе(Сотрудник, МестоРеализации);
	
	Если НаРаботе = Неопределено Тогда
		НаРаботе = НЕ СейчасНаРаботе;
	КонецЕсли; 
	
	Если НаРаботе = СейчасНаРаботе Тогда
		Если НЕ Молча Тогда
			Текст1 = Сотрудник.Наименование;
			Текст2 = ?(НаРаботе, "Начало рабочей смены сотрудника", "Окончание рабочей смены сотрудника")+" уже было отмечено!";
			ИнтерфейсРМ.ВопросПредупреждение("Ошибка", Текст1, Текст2, "", "ОК", "",,,5);
		КонецЕсли; 
		
	Иначе
		ТекДата = НачалоМинуты(ТекущаяДата());
		
		МенеджерЗаписи = РегистрыСведений.ФактическийУчетРабочегоВремени.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Период			= ТекДата;
		МенеджерЗаписи.Сотрудник		= Сотрудник;
		МенеджерЗаписи.МестоРеализации	= МестоРеализации;
		МенеджерЗаписи.Прочитать();
		
		// если в эту минуту уже есть отметка прихода/ухода
		Если МенеджерЗаписи.Выбран() Тогда
			Если НаРаботе Тогда
				// есть отметка ухода, поставить разные отметки в одно время нельзя,
				// поэтому отодвинем уход на минуту назад
				МенеджерЗаписи.Период	= ТекДата - 60;
				МенеджерЗаписи.Прочитать();
				Если МенеджерЗаписи.Выбран() Тогда
					Если НЕ Молча Тогда
						Текст1 = Сотрудник.Наименование;
						Текст2 = "Слишком частый приход/уход!";
						ИнтерфейсРМ.ВопросПредупреждение("Ошибка", Текст1, Текст2, "", "ОК", "",,,5);
					КонецЕсли;
					Возврат Ложь;
				КонецЕсли; 
				МенеджерЗаписи.Период			= ТекДата - 60;
				МенеджерЗаписи.Сотрудник		= Сотрудник;
				МенеджерЗаписи.МестоРеализации	= МестоРеализации;
				МенеджерЗаписи.НаРаботе			= Ложь;
				МенеджерЗаписи.Записать();
				
			Иначе
				// есть отметка прихода, просто удаляем ее
				МенеджерЗаписи.Удалить();
				Если НЕ Молча Тогда
					Текст1 = Сотрудник.Наименование;
					Текст2 = "Отметка прихода в "+Формат(ТекДата,"ДФ=ЧЧ:мм")+" удалена.";
					ИнтерфейсРМ.ВопросПредупреждение("Предупреждение", Текст1, Текст2, "", "ОК", "",,,5);
				КонецЕсли;
				Возврат Истина;
			КонецЕсли; 
		КонецЕсли; 
		
		МенеджерЗаписи = РегистрыСведений.ФактическийУчетРабочегоВремени.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Период			= ТекДата;
		МенеджерЗаписи.Сотрудник		= Сотрудник;
		МенеджерЗаписи.МестоРеализации	= МестоРеализации;
		МенеджерЗаписи.НаРаботе			= НаРаботе;
		МенеджерЗаписи.Записать();
		
		Если НЕ Молча Тогда
			Текст1 = Сотрудник.Наименование;
			Текст2 = "Отмечено "+?(НаРаботе, "начало рабочей смены сотрудника", "окончание рабочей смены сотрудника")+"
			|в "+Формат(ТекДата,"ДФ=ЧЧ:мм");
			ИнтерфейсРМ.ВопросПредупреждение("Предупреждение", Текст1, Текст2, "", "ОК", "",,,5);
		КонецЕсли;
		
	КонецЕсли;
	
	Если НаРаботе Тогда
		// если смена открыта, то автоматом добавляем в состав смены
		ТекСмена = ИнтерфейсРМ.ТекущаяСмена();
		Если ЗначениеЗаполнено(ТекСмена) И ТекСмена.СоставСмены.Найти(Сотрудник, "Сотрудник") = Неопределено Тогда
			
			ДокОткрытияСмены = ТекСмена.ПолучитьОбъект();
			ДокОткрытияСмены.СоставСмены.Добавить().Сотрудник = Сотрудник;
			ДокОткрытияСмены.Записать();
			
			Оповестить("ИзменениеСмены");
		КонецЕсли; 
	КонецЕсли; 
	
	Возврат Истина;
КонецФункции

Функция КтоНаРаботе(МестоРеализации=Неопределено) Экспорт
	
	Если глВерсия = 1 Тогда
		Возврат Новый Массив;
	КонецЕсли; 
	
	Если МестоРеализации=Неопределено Тогда
		МестоРеализации = глПараметрыРМ.МестоРеализации;
	КонецЕсли; 
	
	ЗапросФакт = Новый Запрос("
		|ВЫБРАТЬ
		|	ФактическийУчетРабочегоВремениСрезПоследних.Сотрудник
		|ИЗ
		|	РегистрСведений.ФактическийУчетРабочегоВремени.СрезПоследних(, ) КАК ФактическийУчетРабочегоВремениСрезПоследних
		|ГДЕ
		|	ФактическийУчетРабочегоВремениСрезПоследних.МестоРеализации = &МестоРеализации
		|	И ФактическийУчетРабочегоВремениСрезПоследних.НаРаботе
		|");
		
	ЗапросФакт.УстановитьПараметр("МестоРеализации", МестоРеализации);
	
	МассивСотрудников = ЗапросФакт.Выполнить().Выгрузить().ВыгрузитьКолонку("Сотрудник");
	
	Возврат МассивСотрудников;
КонецФункции

// Возвращает длительность интервала в пределах 24 часов в формате времени
//
Функция ДлительностьИнтервала(ВремяНачала,ВремяОкончания) Экспорт

	мВремяНачала = Дата(1,1,1,Час(ВремяНачала), Минута(ВремяНачала),0);
	мВремяОкончания = Дата(1,1,1,Час(ВремяОкончания), Минута(ВремяОкончания),0);
	
	Если мВремяОкончания < мВремяНачала Тогда
		мВремяОкончания = мВремяОкончания + 60*60*24;
	КонецЕсли;
	
	ВсегоМинут = Цел((мВремяОкончания - мВремяНачала) / 60);
	ДлительностьЧасов = Цел(ВсегоМинут / 60);
	ДлительностьМинут = ВсегоМинут - ДлительностьЧасов * 60;
	Длительность = Дата(1,1,1,ДлительностьЧасов, ДлительностьМинут,0);
	
	Возврат Длительность;
КонецФункции

#КонецЕсли