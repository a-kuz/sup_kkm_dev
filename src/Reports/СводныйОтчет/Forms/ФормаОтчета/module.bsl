Перем Список;

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура ДействияФормыНастройка(Кнопка)
	Форма =  ПолучитьФорму("ФормаНастройки", ЭтаФорма);
	Форма.ОткрытьМодально();
	
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	ФормированиеОтчетов.ПередОткрытиемОтчета(ЭтаФорма);
КонецПроцедуры

Процедура ПриОткрытии()
	Если Не ЗначениеЗаполнено(СписокРазделов) Тогда
		СписокРазделов = Список;
		Для Каждого Раздел Из СписокРазделов Цикл
			Раздел.Пометка = Истина;
		КонецЦикла;	
	Иначе
		Если СписокРазделов.Количество() = 4 Тогда
			СписокРазделов.Добавить(5,"Раздел5. Скидки/Наценки");	
		КонецЕсли;	
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДатаС) И Не ЗначениеЗаполнено(ДатаПо) И Не ЗначениеЗаполнено(МассивСмен) Тогда 
		ДатаС = НачалоДня(ТекущаяДата());
		ДатаПо = ТекущаяДата();
		Период = ПредставлениеПериода(ДатаС,ДатаПо);
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(МассивСмен) Тогда
		МестоРеализации = МассивСмен[0].Значение.МестоРеализации;
	Иначе
		МестоРеализации = Константы.ОсновноеМестоРеализации.Получить();
   	КонецЕсли;
	
	
КонецПроцедуры

Процедура ДействияФормыСформировать(Кнопка)
	СформироватьОтчет(ЭлементыФормы.Результат)
КонецПроцедуры

Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	Перем ВыполненноеДействие;
	Перем  Область;
	СтандартнаяОбработка=Ложь;
	
	ТаблицаЗаказов = Новый ТаблицаЗначений;
	Расшифровка.Свойство("Данные",ТаблицаЗаказов);
	Расшифровка.Свойство("Область",Область);
	
	Макет = ПолучитьМакет("Расшифровка");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьЗаголовок.Параметры.Период = Период; 		
	
	Форма = ПолучитьФорму("ФормаРасшифровки");
	Форма.Открыть();
	Таб = Форма.ЭлементыФормы.Результат;
	Таб.Очистить();
	КоличествоСтрок = 0;
	Таб.Вывести(ОбластьЗаголовок);
	
	 
	Если Лев(Область,8) = "Официант" Тогда 
		ВидРасшифровки = "Официант";
	ИначеЕсли Лев(Область,5) = "Место" Тогда
		ВидРасшифровки = "Место";
	ИначеЕсли Лев(Область,10) = "Начисление" Тогда 
		ВидРасшифровки = "Начисление";
	Иначе
		ВидРасшифровки = "Заказ";
	КонецЕсли;
	
	ОбластьШапка  = Макет.ПолучитьОбласть("Шапка"+ВидРасшифровки);
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка1"+ВидРасшифровки);
	ОбластьИтого  = Макет.ПолучитьОбласть("Итого"+ВидРасшифровки);
	
	Таб.Вывести(ОбластьШапка);
	н = 1;
	ИтогоСумма  = 0;
	ИтогоГостей = 0;
	
	Для Каждого Строка Из ТаблицаЗаказов Цикл
		Если ВидРасшифровки = "Место" Тогда
			Если ТипЗнч(Строка.КолвоМестВсего) = Тип("СправочникСсылка.ПосадочныеМеста") Тогда
				ОбластьСтрока.Параметры.Место = ""+н+".  "+НаимПосадочногоМеста(Строка.КолвоМестВсего);
				ОбластьСтрока.Параметры.РасшМесто = Строка.КолвоМестВсего;
				Таб.Вывести(ОбластьСтрока);
				н=н+1;
        	КонецЕсли;
		ИначеЕсли ВидРасшифровки = "Официант" Тогда
			Если ТипЗнч(Строка.Сотрудник) = Тип("СправочникСсылка.Сотрудники") Тогда 
				ОбластьСтрока.Параметры.Официант = ""+н+".  "+Строка.Сотрудник;
				ОбластьСтрока.Параметры.РасшОфициант = Строка.Сотрудник;
				Таб.Вывести(ОбластьСтрока);
				н=н+1;
			КонецЕсли;
		ИначеЕсли ВидРасшифровки = "Начисление" Тогда
			Если ТипЗнч(Строка.Начисление) = Тип("ДокументСсылка.НачислениеБезнал") Тогда 
				ОбластьСтрока.Параметры.Начисление = ""+н+".  "+Строка.Начисление;
				ОбластьСтрока.Параметры.Сумма = Строка.Начисление.Сумма;
				ОбластьСтрока.Параметры.РасшНачисление = Строка.Начисление;
				ИтогоСумма  = ИтогоСумма + Строка.Начисление.Сумма;
				Таб.Вывести(ОбластьСтрока);
				н=н+1;
			КонецЕсли;
		Иначе
			Если Область = "Рейтинг" ИЛИ Область = "Себестоимость" ИЛИ Область = "Скидки" Тогда
				Заказ = Строка.Заказ;
			ИначеЕсли Область = "КолОткрЗаСмену" Тогда
				Заказ = Строка.ОткрытоЗаказов;
			ИначеЕсли Область = "КолЗакрЗаСмену" Тогда
				Заказ = Строка.КолЗакрытых.Заказ;
			ИначеЕсли Область = "КолвоЗаказов" Тогда
				Заказ = Строка.ОткрытоСейчас;
			ИначеЕсли Область = "КолУдалений" Тогда
				Заказ = Строка.Удалений.Заказ;
			ИначеЕсли Область = "Возвраты" Тогда
				Заказ = Строка.КолВозвратов.Заказ;
			Иначе
				Заказ = Строка.Ссылка;
			КонецЕсли;
			
			Если ТипЗнч(Заказ) = Тип("ДокументСсылка.Заказ") Тогда
				ОбластьСтрока.Параметры.Заказ = Заказ;
				ОбластьСтрока.Параметры.Чел   = Заказ.КоличествоПосетителей;
				ОбластьСтрока.Параметры.Сумма = Заказ.ИтоговаяСумма;
				ОбластьСтрока.Параметры.СтрРасшифровки = Заказ;
				Таб.Вывести(ОбластьСтрока);
				н = н+1;
				ИтогоСумма  = ИтогоСумма + Заказ.ИтоговаяСумма;
				ИтогоГостей = ИтогоГостей + Заказ.КоличествоПосетителей; 
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если ВидРасшифровки = "Заказ" Тогда
		ОбластьИтого.Параметры.Чел = ИтогоГостей;
		ОбластьИтого.Параметры.Сумма = ИтогоСумма;
	ИначеЕсли ВидРасшифровки = "Начисление" Тогда
		ОбластьИтого.Параметры.Сумма = ИтогоСумма;
	КонецЕсли;
	
	Таб.Вывести(ОбластьИтого);
	
КонецПроцедуры


Процедура КнопкаПериодаНажатие(Элемент)
	ФормированиеОтчетов.КнопкаПериодНажатие(ЭтотОбъект);
КонецПроцедуры

Процедура КнопкаСменыНажатие(Элемент)
	ФормированиеОтчетов.КнопкаСменаНажатие(ЭтотОбъект);
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ТЕЛО МОДУЛЯ

Список = Новый СписокЗначений;
Список.Добавить(1,"Раздел1. Выручка");
Список.Добавить(2,"Раздел2. Удаления");
Список.Добавить(3,"Раздел3. Средние показатели");
Список.Добавить(4,"Раздел4. Расшифровка по статусу заказов");
Список.Добавить(5,"Раздел5. Скидки/Наценки");
