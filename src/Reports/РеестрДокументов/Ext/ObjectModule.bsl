#Если Клиент Тогда
	
	////////////////////////////////////////////////////////////////////////////////
	// ПЕРЕМЕННЫЕ МОДУЛЯ
	
	// описание типов переменных
	Перем мОписаниеТиповСтрока          Экспорт;    // тип строка
	Перем мОписаниеТиповБулево          Экспорт;    // тип булево
	Перем мОписаниеТиповТаблицаЗначений Экспорт;    // тип таблица значений
	Перем мОписаниеТиповЗначенийФильтра Экспорт;    // тип значения фильтров
	
	Перем мТаблицаФильтры          Экспорт; // Все возможные фильтры
	Перем мСписокВидовДокументов   Экспорт; // Список видов документов
	
	Перем мНастройка Экспорт; // Настройка - ссылка на справочник настроек
    	
	Перем мСписокОбязательныхОтборов Экспорт; // Список обязательных отборов
	
	////////////////////////////////////////////////////////////////////////////////
	// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ
	
	// Определяет пустую ссылку одного из типов значений
	// Параметры
	//  ОписаниеТиповЗначения - Описание типов
	//
	// Возвращаемое значение:
	//   Пустая ссылка (справочник, перечисление)
	//
	Функция ОпределитьПустоеЗначениеТипа(ОписаниеТиповЗначения) Экспорт
		
		Если ТипЗнч(ОписаниеТиповЗначения) = Тип("ОписаниеТипов") Тогда
			
			МассивТипов = ОписаниеТиповЗначения.Типы();
			Для каждого Тип Из МассивТипов Цикл
				
				Если Тип <> Тип("СписокЗначений") Тогда
					
					Возврат Новый(Тип);
					
				КонецЕсли; 
				
			КонецЦикла; 
			
		Иначе
			
			Возврат Неопределено;
			
		КонецЕсли; 
		
	КонецФункции //ОпределитьПустоеЗначениеТипа()
	
	// Заполняет таблицу фильтров в зависимости от выбранного объекта обработки
	//
	// Параметры: 
	//	ИмяОбласти - имя области макета "ПараметрыФильтров" откуда будут
	//               считаны возможные фильтры и их параметры
	//	ВосстановлениеНастройки - булево, признак открытия с восстановлением настройки
	//
	Процедура ЗаполнитьНачальныеНастройки(ВосстановлениеНастройки = Ложь) Экспорт
		Перем ТипОбъекта;
		Перем ВысотаТаб;
		Перем Область;
		Перем МассивТипов;
		Перем ИмяПоляЗапроса;
		Перем Индекс;
		Перем Массив;
		Перем Макет;
		
		мТаблицаФильтры.Очистить();
		МассивТипов = Новый Массив;
		
		Макет     = ПолучитьМакет("ПараметрыФильтров");
		Область   = Макет.ПолучитьОбласть("Документы");
		ВысотаТаб = Область.ВысотаТаблицы;
		
		МассивНазначений = Новый Массив;
		Запрос = Новый Запрос;
		Запрос.Текст = "";
		Индекс = -1;
		ИмяПоляЗапроса = "";
		
		Для К = 1 По ВысотаТаб Цикл
			
			СтрФильтры = мТаблицаФильтры.Добавить();
			СтрФильтры.ИмяПоля           = СокрЛП( Область.Область(К, 1).Текст);
			СтрФильтры.ОписаниеПоля      = СокрЛП( Область.Область(К, 2).Текст);
			СтрФильтры.ПредставлениеПоля = СокрЛП( Область.Область(К, 3).Текст);
			СтрФильтры.ВклПоУмолчанию    = Булево( СокрЛП( Область.Область(К, 5).Текст));
			СтрФильтры.ИмяПоляВладелец   = СокрЛП( Область.Область(К, 6).Текст);
			СтрФильтры.ИмяПоляЗапроса    = СокрЛП( Область.Область(К, 7).Текст);
			
			Массив = Новый Массив;
			Массив.Добавить( Тип( СокрЛП( Область.Область(К, 2).Текст)));
			МассивТипов.Добавить(Массив[0]);
			СтрФильтры.ОписаниеТипов = Новый ОписаниеТипов(Массив);
			
			// Фильтры по значениям свойств
			//
			Индекс = Индекс + 1;
			
		КонецЦикла;
		
		Состояние("");
		
		МассивТипов.Добавить(Тип("Строка"));
		КвалификаторСтроки = Новый КвалификаторыСтроки(100, ДопустимаяДлина.Переменная);
		
		МассивТипов.Добавить(Тип("Число"));
		КвалификаторЧисла  = Новый КвалификаторыЧисла(5, 2);
		
		мОписаниеТиповЗначенийФильтра = Новый ОписаниеТипов(МассивТипов, КвалификаторЧисла, КвалификаторСтроки);
		
		Если Не ВосстановлениеНастройки Тогда
			ФильтрыОтчета.Очистить();
		КонецЕсли;	
		
	КонецПроцедуры // ЗаполнитьНачальныеНастройки()
	
	// Создает список значений, содержащий виды документов существующих в системе,
	// Виды документов выбираются через метаданные
	//
	// Параметры: 
	//  Нет.
	//
	// Возвращаемое значение:
	//  Список значений содержащий виды документов. Значение - идентификатор вида документа,
	//  представление - его синоним
	//
	Функция СоздатьСписокВидовДокументов()
		Перем СписокВидов;
		Перем Выбор;
		
		СписокВидов = Новый СписокЗначений;
		
		Для Каждого К Из Метаданные.Документы Цикл
			Если К.Имя = "РегистрацияСобытия" Тогда
				Продолжить;
			КонецЕсли; 
			СписокВидов.Добавить(К.Имя, К.Синоним);
		КонецЦикла;
		
		Возврат СписокВидов;
		
	КонецФункции // СоздатьСписокВидовДокументов()
	
	////////////////////////////////////////////////////////////////////////////////
	// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
	// 
	
	мТаблицаФильтры	= Новый ТаблицаЗначений;
	
	МассивСтрока = Новый Массив;
	МассивСтрока.Добавить(Тип("Строка"));
	КвалификаторСтроки   = Новый КвалификаторыСтроки(100, ДопустимаяДлина.Переменная);
	мОписаниеТиповСтрока = Новый ОписаниеТипов(МассивСтрока, , КвалификаторСтроки);
	
	МассивЧисло  = Новый Массив;
	МассивЧисло.Добавить(Тип("Число"));
	КвалификаторЧисла    = Новый КвалификаторыЧисла(1,0);
	ОписаниеТиповЧисло   = Новый ОписаниеТипов(МассивЧисло, КвалификаторЧисла);
	
	МассивБулево = Новый Массив;
	МассивБулево.Добавить(Тип("Булево"));
	мОписаниеТиповБулево = Новый ОписаниеТипов(МассивБулево);
	
	МассивТаблицаЗначений = Новый Массив;
	МассивТаблицаЗначений.Добавить(Тип("ТаблицаЗначений"));
	мОписаниеТиповТаблицаЗначений = Новый ОписаниеТипов(МассивТаблицаЗначений);
	
	МассивОписаниеТипов = Новый Массив;
	МассивОписаниеТипов.Добавить(Тип("ОписаниеТипов"));
	ОписаниеТиповОписаниеТипов = Новый ОписаниеТипов(МассивОписаниеТипов);
	
	// Инициализация таблиц всех возможных показателей, группировок,  фильтров
	мТаблицаФильтры.Колонки.Добавить("ИмяПоля",           мОписаниеТиповСтрока);
	мТаблицаФильтры.Колонки.Добавить("ПредставлениеПоля", мОписаниеТиповСтрока);
	мТаблицаФильтры.Колонки.Добавить("ОписаниеПоля",      мОписаниеТиповСтрока);
	мТаблицаФильтры.Колонки.Добавить("ИмяПоляВладелец",   мОписаниеТиповСтрока);
	мТаблицаФильтры.Колонки.Добавить("ВклПоУмолчанию",    мОписаниеТиповБулево);
	мТаблицаФильтры.Колонки.Добавить("Пометка",           мОписаниеТиповБулево);
	мТаблицаФильтры.Колонки.Добавить("ОписаниеТипов",     ОписаниеТиповОписаниеТипов);
	мТаблицаФильтры.Колонки.Добавить("ИмяПоляЗапроса",    мОписаниеТиповСтрока);
	мТаблицаФильтры.Индексы.Добавить("ИмяПоля");
	
	// Список возможных видов документов
	мСписокВидовДокументов = СоздатьСписокВидовДокументов();
	
	мСписокОбязательныхОтборов = Новый СписокЗначений;
	
#КонецЕсли