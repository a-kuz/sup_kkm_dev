
Процедура КоманднаяПанель1ОткрытьМакет(Кнопка)
	Если ЭлементыФормы.ПодключенныеФормы.ТекущиеДанные=Неопределено Тогда
		ЭлементыФормы.КоманднаяПанель1.Кнопки.ОткрытьМакет.Доступность=Ложь;
	КонецЕсли;
	Форма=ПолучитьФорму("ФормаМакет",ЭлементыФормы.ПодключенныеФормы.ТекущаяСтрока,ЭлементыФормы.ПодключенныеФормы.ТекущаяСтрока);
	Форма.СтрокаСправочника=ЭлементыФормы.ПодключенныеФормы.ТекущиеДанные;
	Форма.ПФОбъект=ЭтотОбъект;
	Форма.Открыть();
КонецПроцедуры

Процедура КоманднаяПанель1ОткрытьПроцедуру(Кнопка)
	Если ЭлементыФормы.ПодключенныеФормы.ТекущиеДанные=Неопределено Тогда
		ЭлементыФормы.КоманднаяПанельПодключенныеФормы.Кнопки.ОткрытьПроцедуру.Доступность=Ложь;
	КонецЕсли;
	Форма=ПолучитьФорму("ФормаПроцедура",ЭлементыФормы.ПодключенныеФормы);
	Форма.СтрокаСправочника=ЭлементыФормы.ПодключенныеФормы.ТекущиеДанные;
	Форма.Открыть();
КонецПроцедуры

Функция ИмяФ()
	ИмяФ1=СтрЗаменить(НРег(Родитель.Наименование),","," ");
	ИмяФ1=СтрЗаменить(ИмяФ1,"  "," ");
	ИмяФ1=СтрЗаменить(СокрЛП(ИмяФ1)," ","_");
	ИмяФ2=СтрЗаменить(НРег(Наименование),","," ");
	ИмяФ2=СтрЗаменить(ИмяФ2,"."," ");
	ИмяФ2=СтрЗаменить(ИмяФ2,""""," ");
	ИмяФ2=СтрЗаменить(ИмяФ2,":"," ");
	ИмяФ2=СтрЗаменить(ИмяФ2,"-"," ");
	ИмяФ2=СтрЗаменить(ИмяФ2,"+"," ");
	ИмяФ2=СтрЗаменить(ИмяФ2,"  "," ");
	ИмяФ2=СтрЗаменить(СокрЛП(ИмяФ2)," ","_");
	Возврат ИмяФ1+"."+ИмяФ2;
КонецФункции


Процедура КоманднаяПанель1ЗагрузитьИзФайла(Кнопка)
	Если ПодключенныеФормы.Количество()>0 И ЭлементыФормы.ПодключенныеФормы.ТекущиеДанные=Неопределено Тогда
		Предупреждение("Нужно сделать активной строку, в которую будем загружать макет и процедуру из файла !");
		Возврат;
	КонецЕсли;

	ИмяФ=ИмяФ();
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Заголовок = "Выберите файл с печатной формой";
	Диалог.ПолноеИмяФайла = ИмяФ+".zip";
	Диалог.Фильтр = "ZIP архив (*.zip)|*.zip|";
	Если Диалог.Выбрать() Тогда
		ПФ=Новый ЧтениеZipФайла(Диалог.ПолноеИмяФайла);
		ЕстьМакет=Ложь;ЕстьПроц=Ложь;
		Для каждого ПФФайла Из ПФ.Элементы Цикл
			Если нрег(ПФФайла.Расширение)="mxl" Тогда
				ЕстьМакет=Истина;
				ИмяФайла1=КаталогВременныхФайлов()+ПФФайла.Имя;
				ПФ.Извлечь(ПФФайла,КаталогВременныхФайлов());
			КонецЕсли;
			Если нрег(ПФФайла.Расширение)="txt" Тогда
				ЕстьПроц=Истина;
				ИмяФайла2=КаталогВременныхФайлов()+ПФФайла.Имя;
				ПФ.Извлечь(ПФФайла,КаталогВременныхФайлов());
			КонецЕсли;
		КонецЦикла;
		
		Если ЕстьМакет ИЛИ ЕстьПроц Тогда
			Если ПодключенныеФормы.Количество()=0 Тогда
				Стр=ПодключенныеФормы.Добавить();
				ЭлементыФормы.ПодключенныеФормы.ТекущаяСтрока=Стр;
			Иначе
				Стр=ЭлементыФормы.ПодключенныеФормы.ТекущаяСтрока;
			КонецЕсли;
			Если ЕстьМакет Тогда
				Стр.Макет=Новый ХранилищеЗначения(Новый ДвоичныеДанные(ИмяФайла1), Новый СжатиеДанных());
			КонецЕсли;
			Если ЕстьПроц Тогда
				Текст=Новый ТекстовыйДокумент;  
				Текст.Прочитать(ИмяФайла2);
				Стр.Процедура=Текст.ПолучитьТекст();
			КонецЕсли;
		Иначе
			Сообщить("Архив пуст");
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура КоманднаяПанель1ЗаписатьВФайл(Кнопка)
	Если ЭлементыФормы.ПодключенныеФормы.ТекущиеДанные=Неопределено Тогда
		Предупреждение("Нет подключенных макетов и процедур, записывать нечего !");
	Иначе
		ЕстьМакет=Ложь;
		ЕстьПроц=Ложь;
		ИмяФ=ИмяФ();
		Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
		Диалог.Заголовок = "Укажите путь, куда сохранить печатную форму";
		Диалог.ПолноеИмяФайла = ИмяФ+".zip";
		Диалог.Фильтр = "ZIP архив (*.zip)|*.zip|";
		Если Диалог.Выбрать() Тогда
			ДвоичныеДанные=ЭлементыФормы.ПодключенныеФормы.ТекущиеДанные.Макет.Получить();
			Если ДвоичныеДанные<>Неопределено Тогда
				Попытка
					ИмяФайла1=КаталогВременныхФайлов()+ИмяФ+".mxl";
					ДвоичныеДанные.Записать(ИмяФайла1);
					ЕстьМакет=Истина;
				Исключение
					Сообщить("Не удалось сохранить файл """+ИмяФайла1+""" !", СтатусСообщения.Важное);
				КонецПопытки;
			КонецЕсли;
			
			Если НЕ ПустаяСтрока(ЭлементыФормы.ПодключенныеФормы.ТекущиеДанные.Процедура) Тогда
				ИмяФайла2=КаталогВременныхФайлов()+ИмяФ+".txt";
				Текст=Новый ТекстовыйДокумент;  
				Текст.УстановитьТекст(ЭлементыФормы.ПодключенныеФормы.ТекущиеДанные.Процедура);
				Текст.Записать(ИмяФайла2,КодировкаТекста.ANSI);
				ЕстьПроц=Истина;
			КонецЕсли;
			
			Если ЕстьМакет ИЛИ ЕстьПроц Тогда
				ПФ=Новый ЗаписьZipФайла(Диалог.ПолноеИмяФайла);
				Если ЕстьМакет Тогда
					ПФ.Добавить(ИмяФайла1);
				КонецЕсли;
				Если ЕстьПроц Тогда
					ПФ.Добавить(ИмяФайла2);
				КонецЕсли;
				ПФ.Записать();
				Сообщить("Файл: "+Диалог.ПолноеИмяФайла+" записан !");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	Отказ=ЭтоНовый() И Родитель.Пустая();
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если НЕ Предопределенный Тогда
		Если ПодключенныеФормы.Количество()=0 Тогда
			Отказ=Истина;
		Иначе
			Для каждого Стр Из ПодключенныеФормы Цикл
				Если НЕ ЗначениеЗаполнено(Стр.Макет.Получить()) ИЛИ
					НЕ ЗначениеЗаполнено(Стр.Процедура) Тогда
					Отказ=Истина;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		Если Отказ Тогда
			Предупреждение("Нужно обязательно указать макет и процедуру");
		КонецЕсли;
	Иначе
		Для каждого Стр Из ПодключенныеФормы Цикл
			Если НЕ ЗначениеЗаполнено(Стр.Макет.Получить()) И
				НЕ ЗначениеЗаполнено(Стр.Процедура) Тогда
				Отказ=Истина;
			КонецЕсли;
		КонецЦикла;
		Если Отказ Тогда
			Предупреждение("Нужно указать или макет или процедуру");
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ПодключенныеФормыПриАктивизацииСтроки(Элемент)
	Если ЭлементыФормы.ПодключенныеФормы.ТекущиеДанные=Неопределено Тогда
		ЭлементыФормы.КоманднаяПанель1.Кнопки.ОткрытьМакет.Доступность=Ложь;
		ЭлементыФормы.КоманднаяПанель1.Кнопки.ОткрытьПроцедуру.Доступность=Ложь;
	Иначе	
		ЭлементыФормы.КоманднаяПанель1.Кнопки.ОткрытьМакет.Доступность=Истина;
		ЭлементыФормы.КоманднаяПанель1.Кнопки.ОткрытьПроцедуру.Доступность=Истина;
	КонецЕсли;
КонецПроцедуры

Процедура ПодключенныеФормыПередНачаломИзменения(Элемент, Отказ)
	Если ЭлементыФормы.ПодключенныеФормы.ТекущаяКолонка.Имя="Макет" Тогда
		КоманднаяПанель1ОткрытьМакет(Элемент);
		Отказ=Истина;
	ИначеЕсли ЭлементыФормы.ПодключенныеФормы.ТекущаяКолонка.Имя="Процедура" Тогда
		КоманднаяПанель1ОткрытьПроцедуру(Элемент);
		Отказ=Истина;
	КонецЕсли;
КонецПроцедуры

Процедура ПодключенныеФормыПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	Если ЗначениеЗаполнено(ДанныеСтроки.Макет.Получить()) Тогда
		ОформлениеСтроки.Ячейки.Макет.УстановитьТекст("Есть");
	КонецЕсли;
	Если ЗначениеЗаполнено(ДанныеСтроки.Процедура) Тогда
		ОформлениеСтроки.Ячейки.Процедура.УстановитьТекст("Есть");
	КонецЕсли;
КонецПроцедуры
