Процедура СформироватьЗаголовокОкна()

	ПредставлениеОбъекта = "";
	Если ТипЗнч(Отбор.Объект.Значение) = Тип("СписокЗначений") Тогда
		Если Отбор.Объект.ВидСравнения = ВидСравнения.ВСписке ИЛИ Отбор.Объект.ВидСравнения = ВидСравнения.ВСпискеПоИерархии Тогда
			ПредставлениеОбъекта = "в списке ";
		ИначеЕсли Отбор.Объект.ВидСравнения = ВидСравнения.НеВСписке ИЛИ Отбор.Объект.ВидСравнения = ВидСравнения.НеВСпискеПоИерархии Тогда
			ПредставлениеОбъекта = "не в списке ";
		КонецЕсли;
		ПредставлениеСписка = "";
		Для каждого ЭлементСписка Из Отбор.Объект.Значение Цикл
			Если Не ПустаяСтрока(ПредставлениеСписка) Тогда
				ПредставлениеСписка = ПредставлениеСписка + ", ";
			КонецЕсли; 
			ПредставлениеСписка = ПредставлениеСписка + ?(НЕ ЗначениеЗаполнено(ЭлементСписка.Значение), "<...>", Строка(ЭлементСписка.Значение));
		КонецЦикла;
		ПредставлениеОбъекта = ПредставлениеОбъекта + ПредставлениеСписка;
	ИначеЕсли Справочники.ТипВсеСсылки().СодержитТип(ТипЗнч(Отбор.Объект.Значение)) Тогда
		Если НЕ ЗначениеЗаполнено(Отбор.Объект.Значение) Тогда
			ПредставлениеОбъекта = "не определен";
		Иначе
			Если Отбор.Объект.ВидСравнения = ВидСравнения.Равно Тогда
				ПредставлениеОбъекта = "";
			ИначеЕсли Отбор.Объект.ВидСравнения = ВидСравнения.НеРавно Тогда
				ПредставлениеОбъекта = "не равно ";
			КонецЕсли; 
			ПредставлениеОбъекта = ПредставлениеОбъекта + Строка(Отбор.Объект.Значение);
		КонецЕсли; 
	КонецЕсли; 
	
	ЭтаФорма.Заголовок = "Контактная информация. Объект: " + ПредставлениеОбъекта;

КонецПроцедуры

Процедура РегистрСведенийСписокВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)

	Если ЭтаФорма.РежимВыбора Тогда
		СтандартнаяОбработка = Ложь;
		ЭтаФорма.Закрыть(Элемент.ТекущиеДанные.Представление);
	КонецЕсли; 
	
КонецПроцедуры

Процедура РегистрСведенийСписокПослеУдаления(Элемент)
	
	ОповеститьОЗаписиНовогоОбъекта(Неопределено);
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	Отбор.Объект.Использование = Истина;
	
	
	УправлениеРИБ.ПриОткрытииСпискаСправочника(ЭтаФорма);
	
КонецПроцедуры

Процедура ОбновлениеОтображения()
	
	СформироватьЗаголовокОкна();
	
КонецПроцедуры













