
Процедура УправлениеФормой()
	
	Если ЭтоКоктейль ИЛИ НЕ ТолькоВКоктейле Тогда
		ЭлементыФормы.НадписьНаименование.Заголовок = "Привязка:";
		ЭлементыФормы.Наименование.Данные = "Товар";
		ЭлементыФормы.Наименование.КнопкаОчистки = Ложь;
		ЭлементыФормы.Наименование.КнопкаОткрытия = Истина;
		ЭлементыФормы.Наименование.АвтоОтметкаНезаполненного = Истина;
		ЭлементыФормы.Наименование.АвтоВыборНезаполненного = Истина;
		ЭлементыФормы.Наименование.РежимВыбораНезаполненного = РежимВыбораНезаполненного.ПриАктивизации;
		
	Иначе
		ЭлементыФормы.НадписьНаименование.Заголовок = "Наименование:";
		ЭлементыФормы.Наименование.Данные = "Наименование";
		ЭлементыФормы.Наименование.АвтоОтметкаНезаполненного = Ложь;
		
	КонецЕсли;
	
	Если ЭтоКоктейль Тогда
		ЭлементыФормы.Панель1.ТекущаяСтраница = ЭлементыФормы.Панель1.Страницы.Коктейль;
	Иначе
		ЭлементыФормы.Панель1.ТекущаяСтраница = ЭлементыФормы.Панель1.Страницы.Позиция;
	КонецЕсли;
	
	ЭлементыФормы.ТолькоВКоктейле.Видимость = НЕ ЭтоКоктейль;
	
КонецПроцедуры

Функция ЗаполнитьСписокДляКоктелей()
	
	Список = Новый СписокЗначений;
	
	Запрос = Новый Запрос("ВЫБРАТЬ Ссылка ИЗ Справочник.ПозицииНаливайки
						  |ГДЕ НЕ ПометкаУдаления И НЕ ЭтоКоктейль
						  |		И НЕ Ссылка = &ТекущаяСсылка
						  |УПОРЯДОЧИТЬ ПО Наименование
						  |");
						  
	Запрос.УстановитьПараметр("ТекущаяСсылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Список.Добавить(Выборка.Ссылка);
	КонецЦикла; 
	
	Возврат Список;
КонецФункции
 
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	//УправлениеРИБ.ПередОткрытиемЭлементаСправочника(ЭтотОбъект, ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	УправлениеФормой();
	
	ЭлементыФормы.СоставКоктейля.Колонки.Позиция.ЭлементУправления.СписокВыбора = ЗаполнитьСписокДляКоктелей();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ЭтоКоктейль ИЛИ НЕ ТолькоВКоктейле Тогда
		
		Если НЕ ЗначениеЗаполнено(Товар) Тогда
			Сообщить("Укажите привязку к меню!");
			Отказ = Истина;
		КонецЕсли; 
		
		Наименование = Товар.Наименование;
		
	Иначе
		Товар = Неопределено;
		
		Если НЕ ЗначениеЗаполнено(Наименование) Тогда
			Сообщить("Укажите наименование!");
			Отказ = Истина;
		КонецЕсли; 
		
	КонецЕсли;
	
	Если ЭтоКоктейль И СоставКоктейля.Количество()=0 Тогда
		Сообщить("Заполните состав коктейля!");
		Отказ = Истина;
	КонецЕсли; 
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура ТипПриИзменении(Элемент)
	
	УправлениеФормой();
	
КонецПроцедуры

Процедура ТолькоВКоктейлеПриИзменении(Элемент)
	
	УправлениеФормой();
	
КонецПроцедуры

Процедура СоставКоктейляПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	ОформлениеСтроки.Ячейки.НомерПозиции.ОтображатьТекст = Истина;
	ОформлениеСтроки.Ячейки.НомерПозиции.Текст = ДанныеСтроки.Позиция.Код;
	
КонецПроцедуры

Процедура СоставКоктейляПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И НЕ Копирование Тогда
		Элемент.ТекущаяСтрока.Количество = 1;
	КонецЕсли; 
	
КонецПроцедуры

Процедура СоставКоктейляПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если НЕ ЗначениеЗаполнено(Элемент.ТекущаяСтрока.Позиция) И НЕ (НоваяСтрока И ОтменаРедактирования) Тогда
		Элемент.ТекущаяКолонка = Элемент.Колонки.Позиция;
		Отказ = Истина;
	КонецЕсли; 
	
КонецПроцедуры

