
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	ТипСписокВыбора = ЭлементыФормы.Тип.СписокВыбора;
	ТипСписокВыбора.Добавить(Перечисления.ТипыОплаты.Нал);
	ТипСписокВыбора.Добавить(Перечисления.ТипыОплаты.Карта);
	ТипСписокВыбора.Добавить(Перечисления.ТипыОплаты.Безнал);
	ТипСписокВыбора.Добавить(Перечисления.ТипыОплаты.Неплательщик);
	Если глВерсия > 1 Тогда
		ТипСписокВыбора.Добавить(Перечисления.ТипыОплаты.Бонусы);
		ТипСписокВыбора.Добавить(Перечисления.ТипыОплаты.Сертификат);
		Если Константы.РумбаИнтерфейсВкл.Получить() Тогда
			ТипСписокВыбора.Добавить(Перечисления.ТипыОплаты.НаНомер);
			ТипСписокВыбора.Добавить(Перечисления.ТипыОплаты.НаФолио);
		КонецЕсли;
	КонецЕсли;
	
	Если ЭтоНовый() И НЕ ЗначениеЗаполнено(ПараметрОбъектКопирования) Тогда 
		Наименование = "Наличные";
		Тип = Перечисления.ТипыОплаты.Нал;
		НомерВККМ = 1;
		УровеньДоступа = Перечисления.УровниДоступа.Свободный;
    	ТипОграниченияКатегорий = Перечисления.ТипыОграниченийПоСписку.Нет;
	КонецЕсли;
	
	ЗаполнитьНомерВККМСписокВыбора();
	
	Если НЕ ЗначениеЗаполнено(ПараметрОбъектКопирования) Тогда
		ИнтерфейсАдмина.ЗаполнитьДопНаименования(Ссылка,ТаблицаДопНаименования);
		ИнтерфейсАдмина.ЗаполнитьСписокПометокПоТабЧасти(Категории,ДопустимыеКатегории,"Категория");
	Иначе
		ИнтерфейсАдмина.ЗаполнитьДопНаименования(ПараметрОбъектКопирования,ТаблицаДопНаименования);
		ИнтерфейсАдмина.ЗаполнитьСписокПометокПоТабЧасти(Категории,ПараметрОбъектКопирования.ДопустимыеКатегории,"Категория");
	КонецЕсли;
	
	Если глВерсия=1 Тогда
		ЭлементыФормы.Скидка.Видимость = Ложь;
		ЭлементыФормы.НадписьСкидка.Видимость = Ложь;
		ЭлементыФормы.НоминалСертификата.Видимость = Ложь;
	КонецЕсли;
	Если глВерсия=1 ИЛИ НЕ Константы.ДопЯзыки.Получить() Тогда
		ЭлементыФормы.Панель1.Страницы.ДопНаименования.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ТипОграниченияКатегорий = Перечисления.ТипыОграниченийПоСписку.Нет Тогда
		ДопустимыеКатегории.Очистить();
	Иначе
		ИнтерфейсАдмина.ЗаполнитьТабЧастьПоСпискуПометок(Категории,ДопустимыеКатегории,"Категория");
		Если ДопустимыеКатегории.Количество()=0 Тогда
			ТипОграниченияКатегорий	= Перечисления.ТипыОграниченийПоСписку.Нет;
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	ИнтерфейсАдмина.ЗаписатьДопНаименования(Ссылка,ТаблицаДопНаименования);
	
КонецПроцедуры

Процедура ОбновлениеОтображения()
	
	Если глВерсия > 1 Тогда
		Если Тип = Перечисления.ТипыОплаты.Сертификат Тогда
			ЭлементыФормы.НадписьСкидка.Заголовок = "Номинал:";
			ЭлементыФормы.Скидка.Видимость = Ложь;
			ЭлементыФормы.НоминалСертификата.Видимость = Истина;
		Иначе
			ЭлементыФормы.НадписьСкидка.Заголовок = "Скидка:";
			ЭлементыФормы.Скидка.Видимость = Истина;
			ЭлементыФормы.НоминалСертификата.Видимость = Ложь;
		КонецЕсли; 
	КонецЕсли; 
	
	Флаг = ТипОграниченияКатегорий <> Перечисления.ТипыОграниченийПоСписку.Нет;
	ЭлементыФормы.Категории.Доступность					= Флаг;
	ЭлементыФормы.КоманднаяПанельКатегории.Доступность	= Флаг;
	
	Флаг = глВерсия>1 И Тип=Перечисления.ТипыОплаты.Карта;
	ЭлементыФормы.НадписьПлатежнаяСистема.Видимость = Флаг;
	ЭлементыФормы.ПлатежнаяСистема.Видимость		= Флаг;
	
	Элементыформы.ОграничениеПериод.Доступность			= Тип = Перечисления.ТипыОплаты.Безнал ИЛИ Тип = Перечисления.ТипыОплаты.Бонусы ИЛИ Тип = Перечисления.ТипыОплаты.Неплательщик;
	Элементыформы.ОграничениеСумма.Доступность			= ОграничениеПериод > 0;
	Флаг = ОграничениеПериод = 4;
	Элементыформы.ОграничениеПроцент.Видимость			= Флаг;
	Элементыформы.НадписьОграничениеПроцент.Видимость	= Флаг;
	Элементыформы.НадписьОграничениеПроцент1.Видимость	= Флаг;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура ЗаполнитьНомерВККМСписокВыбора()
	
	НомерВККМСписокВыбора = ЭлементыФормы.НомерВККМ.СписокВыбора;
	НомерВККМСписокВыбора.Очистить();
	НомерВККМСписокВыбора.Добавить(0,"не регистрировать");
	
	Если Тип = Перечисления.ТипыОплаты.Нал Тогда
		НомерВККМСписокВыбора.Добавить(1, "Счетчик № 1");
	Иначе
		НомерВККМСписокВыбора.Добавить(2, "Счетчик № 2");
		НомерВККМСписокВыбора.Добавить(3, "Счетчик № 3");
		НомерВККМСписокВыбора.Добавить(4, "Счетчик № 4");
	КонецЕсли; 
	
КонецПроцедуры
 
Процедура ТипПриИзменении(Элемент)
	
	ЗаполнитьНомерВККМСписокВыбора();
	
	НомерВККМ = ?(Тип = Перечисления.ТипыОплаты.Нал, 1, 0);
	
	Если НЕ ЗначениеЗаполнено(Наименование) Тогда
		Наименование = Строка(Тип);
	КонецЕсли;
	
	Если НЕ (Тип = Перечисления.ТипыОплаты.Безнал ИЛИ Тип = Перечисления.ТипыОплаты.Бонусы ИЛИ Тип = Перечисления.ТипыОплаты.Неплательщик) Тогда
		ДоступностьОграниченийОплаты(0);
	КонецЕсли;		
	
КонецПроцедуры

Процедура СкидкаНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИнтерфейсАдмина.ВыборСкидкиБонуса(Элемент, Ложь);
	
КонецПроцедуры

Процедура КоманднаяПанельКатегорииУстановитьФлажки(Кнопка)
	Категории.ЗаполнитьПометки(Истина);
КонецПроцедуры

Процедура КоманднаяПанельКатегорииСнятьФлажки(Кнопка)
	Категории.ЗаполнитьПометки(Ложь);
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	УправлениеРИБ.ПередОткрытиемЭлементаСправочника(ЭтотОбъект, ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ПлатежнаяСистемаНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СписокВидов = Новый СписокЗначений;
	СписокВидов.Добавить("АО");
	СписокВидов.Добавить("ПС");
	тоВыбратьИзСпискаТО(СписокВидов, ЭтаФорма, Элемент);
	
КонецПроцедуры

Процедура ОграничениеПериодПриИзменении(Элемент)
	
	ДоступностьОграниченийОплаты(Элемент.Значение);
	
КонецПроцедуры

Процедура ДоступностьОграниченийОплаты(ИзменитьОграничениеПериод)

	ОграничениеПериод = ИзменитьОграничениеПериод;
	
	Если ОграничениеПериод = 0 Тогда
		ОграничениеСумма	= 0;
		ОграничениеПроцент	= 0;
	ИначеЕсли ОграничениеПериод <> 4 Тогда
		ОграничениеПроцент	= 0;
	КонецЕсли;
	
КонецПроцедуры	
////////////////////////////////////////////////////////////////////////////////
// ТЕЛО МОДУЛЯ
