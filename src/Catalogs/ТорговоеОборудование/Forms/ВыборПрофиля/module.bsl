
Перем ТипПрофиля Экспорт;	// вариант хранения параметров
// 2 - указывается только ИмяКомпьютера
// 3 - указывается ИмяКомпьютера и ИмяПользователя

Функция ПолучитьСписокПользователей()
	
	СписокПользователей = Новый СписокЗначений;
	
	МассивПользователей = ПользователиИнформационнойБазы.ПолучитьПользователей();
	
	Для каждого Пользователь Из МассивПользователей Цикл
		СписокПользователей.Добавить(Пользователь.Имя);
	КонецЦикла; 
	
	Возврат СписокПользователей;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура ОсновныеДействияФормыВыбрать(Кнопка)
	
	ТекущаяСтрока = ЭлементыФормы.ТаблицаПрофилей.ТекущаяСтрока;
	
	Если ЗначениеЗаполнено(ТекущаяСтрока) Тогда
		Закрыть(ТекущаяСтрока.Профиль);
	КонецЕсли; 
	
КонецПроцедуры

Процедура ТаблицаПрофилейВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Закрыть(ВыбраннаяСтрока.Профиль);
	
КонецПроцедуры

Процедура ТаблицаПрофилейПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	СтрокаПрофиля = Элемент.ТекущаяСтрока;
	
	Если НЕ ЗначениеЗаполнено(СтрокаПрофиля.ИмяКомпьютера) Тогда
		Сообщить("Необходимо задать имя компьютера!",СтатусСообщения.Важное);
		ОтменаРедактирования = Истина;
	КонецЕсли; 
	
	Если ТипПрофиля=3 И НЕ ЗначениеЗаполнено(СтрокаПрофиля.ИмяПользователя) Тогда
		Сообщить("Необходимо задать имя пользователя!",СтатусСообщения.Важное);
		ОтменаРедактирования = Истина;
	КонецЕсли;
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли; 
	
	СтрокаПрофиля.ИмяКомпьютера = ВРег(СтрокаПрофиля.ИмяКомпьютера);
	
	СтрокаПрофиля.ТипПрофиля	= ТипПрофиля;
	СтрокаПрофиля.Профиль		= СтрокаПрофиля.ИмяКомпьютера+"\"+?(ТипПрофиля=2,"Все пользователи",СтрокаПрофиля.ИмяПользователя);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	МассивЛишнихСтрок = Новый Массив;
	
	ТаблицаПрофилей.Колонки.Добавить("ИмяКомпьютера"		,Новый ОписаниеТипов("Строка"), "Имя компьютера", 20);
	Если ТипПрофиля = 3 Тогда
		ТаблицаПрофилей.Колонки.Добавить("ИмяПользователя"	,Новый ОписаниеТипов("Строка"), "Имя пользователя", 20);
	КонецЕсли; 
	
	Для каждого СтрокаПрофиля Из ТаблицаПрофилей Цикл
		
		Если СтрокаПрофиля.ТипПрофиля = ТипПрофиля Тогда
			
			ПозРазделителя = Найти(СтрокаПрофиля.Профиль,"\");
			СтрокаПрофиля.ИмяКомпьютера = Лев(СтрокаПрофиля.Профиль,ПозРазделителя-1);
			Если ТипПрофиля = 3 Тогда
				СтрокаПрофиля.ИмяПользователя = Сред(СтрокаПрофиля.Профиль,ПозРазделителя+1);
			КонецЕсли;
			
		Иначе
			МассивЛишнихСтрок.Добавить(СтрокаПрофиля);
		КонецЕсли; 
		
	КонецЦикла;
	
	Для каждого СтрокаПрофиля Из МассивЛишнихСтрок Цикл
		ТаблицаПрофилей.Удалить(СтрокаПрофиля);
	КонецЦикла; 
	
	ЭлементыФормы.ТаблицаПрофилей.СоздатьКолонки();
	ЭлементыФормы.ТаблицаПрофилей.Колонки.Профиль	.Видимость = Ложь;
	ЭлементыФормы.ТаблицаПрофилей.Колонки.ТипПрофиля.Видимость = Ложь;
	ЭлементыФормы.ТаблицаПрофилей.Колонки.Параметры	.Видимость = Ложь;
	
	ЭлементыФормы.ТаблицаПрофилей.Колонки.ИмяКомпьютера.ЭлементУправления.КнопкаВыбора = Истина;
	Если ТипПрофиля = 3 Тогда
		ИмяПользователя = ЭлементыФормы.ТаблицаПрофилей.Колонки.ИмяПользователя.ЭлементУправления;
		ИмяПользователя.КнопкаСпискаВыбора = Истина;
		ИмяПользователя.СписокВыбора = ПолучитьСписокПользователей();
		//ИмяПользователя.УстановитьДействие("Открытие", Новый Действие("ПриОткрытииСодержанияРаботы"));
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПриЗакрытии()
	
	ТаблицаПрофилей.Колонки.Удалить("ИмяКомпьютера");
	Если ТипПрофиля = 3 Тогда
		ТаблицаПрофилей.Колонки.Удалить("ИмяПользователя");
	КонецЕсли;
	
КонецПроцедуры

