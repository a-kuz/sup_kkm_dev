
Перем ТаблицаИменКолонокТарифнойСетки;

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	УправлениеРИБ.ПередОткрытиемЭлементаСправочника(ЭтотОбъект, ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	Если ЭтоНовый() И НЕ ЗначениеЗаполнено(ПараметрОбъектКопирования) Тогда
		Валюта = Константы.ОсновнаяВалюта.Получить();
		ПравилоОкругления = Константы.ОсновноеПравилоОкругления.Получить();
	КонецЕсли;
	
	ФормаТарифнаяСетка = Защита.ЗаполнитьТарифнуюСетку( ?(ЗначениеЗаполнено(ПараметрОбъектКопирования), ПараметрОбъектКопирования, Ссылка), ТаблицаИменКолонокТарифнойСетки);
	
	Колонки = ЭлементыФормы.ТарифнаяСетка.Колонки;
	Колонки.Время.Данные = "Время";
	
	Для каждого СтрокаИмен Из ТаблицаИменКолонокТарифнойСетки Цикл
		КолонкаЦена = Колонки.Добавить(СтрокаИмен.Цена, СтрокаИмен.ТипДня.Наименование);
		КолонкаЦена.Данные = СтрокаИмен.Цена;
		КолонкаЦена.ЭлементУправления.КнопкаСпискаВыбора = Истина;
		КолонкаЦена.ЭлементУправления.УстановитьДействие("ОбработкаВыбора", Новый Действие("ТарифнаяСеткаЦенаОбработкаВыбора"));
		СписокДействий = КолонкаЦена.ЭлементУправления.СписокВыбора;
		СписокДействий.Добавить("Очистить");
		СписокДействий.Добавить("Бесплатно");
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбновлениеОтображения()
	
	ЭлементыФормы.ПанельЗакладки.Страницы.ТарифнаяСетка.Видимость = (ВариантРасчета < 2);
	
	ЭлементыФормы.НадписьВремяПодготовки.Видимость = НЕ ЗапросВремени;
	ЭлементыФормы.ВремяПодготовки.Видимость = НЕ ЗапросВремени;
	
	ЭлементыФормы.НадписьЦена.Доступность = (ВариантРасчета = 2);
	ЭлементыФормы.Цена.Доступность = (ВариантРасчета = 2);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ЗапросВремени Тогда
		ВремяПодготовки = Неопределено;
	КонецЕсли;
	
	ТарифнаяСетка.Очистить();
	
	Для каждого СтрокаИмен Из ТаблицаИменКолонокТарифнойСетки Цикл
		Для Каждого СтрокаСетки Из ФормаТарифнаяСетка Цикл
			
			Если СтрокаСетки[СтрокаИмен.Цена]<>0 ИЛИ СтрокаСетки[СтрокаИмен.Бесплатно] Тогда
				НоваяСтрока = ТарифнаяСетка.Добавить();
				НоваяСтрока.ТипДня	= СтрокаИмен.ТипДня;
				НоваяСтрока.Время	= СтрокаСетки.Время;
				НоваяСтрока.Цена	= СтрокаСетки[СтрокаИмен.Цена];
			КонецЕсли;
			
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура ТарифнаяСеткаПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	Для каждого СтрокаИмен Из ТаблицаИменКолонокТарифнойСетки Цикл
		Если ДанныеСтроки[СтрокаИмен.Бесплатно] И ДанныеСтроки[СтрокаИмен.Цена]=0 Тогда
			ОформлениеСтроки.Ячейки[СтрокаИмен.Цена].Текст = "Бесплатно";
		КонецЕсли; 
	КонецЦикла;
	
	//ОформлениеСтроки.Ячейки.Время.УстановитьТекст(Формат(ДанныеСтроки.Время,"ДФ=ЧЧ:мм;ДП=00:00"));
	
КонецПроцедуры

Процедура ТарифнаяСеткаПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	// проверка существования строки с таким же временем
	Если НЕ ОтменаРедактирования Тогда
		МассивСтрок = ФормаТарифнаяСетка.НайтиСтроки(Новый Структура("Время",Элемент.ТекущиеДанные.Время));
		Если МассивСтрок.Количество() > 1 Тогда
			Предупреждение("Такое время уже есть!");
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ТарифнаяСеткаПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если НЕ ОтменаРедактирования Тогда
		ФормаТарифнаяСетка.Сортировать("Время");
	КонецЕсли;
	
КонецПроцедуры

Процедура ТарифнаяСеткаВремяНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИнтерфейсАдмина.ВыбратьВремяИзСписка(ЭтаФорма, Элемент);
	
КонецПроцедуры

Процедура ТарифнаяСеткаЦенаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИмяБесплатно = СтрЗаменить(ЭлементыФормы.ТарифнаяСетка.ТекущаяКолонка.Имя, "Цена", "Бесплатно");
	
	Если ВыбранноеЗначение = "Очистить" Тогда
		Элемент.Значение = 0;
		ЭлементыФормы.ТарифнаяСетка.ТекущиеДанные[ИмяБесплатно] = Ложь;
		
	ИначеЕсли ВыбранноеЗначение = "Бесплатно" Тогда
		Элемент.Значение = 0;
		ЭлементыФормы.ТарифнаяСетка.ТекущиеДанные[ИмяБесплатно] = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

