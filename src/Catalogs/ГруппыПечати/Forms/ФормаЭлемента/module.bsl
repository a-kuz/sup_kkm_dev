
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	УправлениеРИБ.ПередОткрытиемЭлементаСправочника(ЭтотОбъект, ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	Если ЭтоНовый() Тогда
		Если НЕ ЗначениеЗаполнено(ПараметрОбъектКопирования) Тогда
			Тип = Перечисления.ТипыГруппПечати.Принтер;
			ЕстьПринтер = Истина;
			КоличествоКопий = 1;
		КонецЕсли; 
	Иначе
		НомерМарки = РегистрыСведений.ГруппыПечатиДопИнф.Получить(Новый Структура("ГруппаПечати",Ссылка)).НомерМарки;
	КонецЕсли;
	
	Если ПараметрыСеанса.РаспределенныйРежим Тогда
		ЭлементыФормы.ПанельПринтеры.ТекущаяСтраница = ЭлементыФормы.ПанельПринтеры.Страницы.РИБ;
	Иначе
		ЭлементыФормы.ПанельПринтеры.ТекущаяСтраница = ЭлементыФормы.ПанельПринтеры.Страницы.Список;
	КонецЕсли; 
	
	СигналПослеПечати = ДлительностьСигнала<>0;
	
	Если НЕ ЗначениеЗаполнено(ПараметрОбъектКопирования) Тогда
		ИнтерфейсАдмина.ЗаполнитьСписокПометокПоТабЧасти(ДопРМОповещения, ЭтотОбъект.РабочиеМестаОповещения, "РабочееМесто");
		ИнтерфейсАдмина.ЗаполнитьДопНаименования(Ссылка, ТаблицаДопНаименования);
	Иначе
		ИнтерфейсАдмина.ЗаполнитьСписокПометокПоТабЧасти(ДопРМОповещения, ПараметрОбъектКопирования.РабочиеМестаОповещения, "РабочееМесто");
		ИнтерфейсАдмина.ЗаполнитьДопНаименования(ПараметрОбъектКопирования, ТаблицаДопНаименования);
	КонецЕсли;
	
	ОбновлениеОтображения();
	
	Попытка
		ЭлементыФормы.Панель1.ТекущаяСтраница = ЭлементыФормы.Панель1.Страницы[ВосстановитьЗначение("ТекущаяСтраницаГруппыПечати")];
	Исключение
	КонецПопытки;
		
	ЭлементыФормы.Тип.СписокВыбора.Добавить(Перечисления.ТипыГруппПечати.Принтер);
	Если глВерсия > 1 И Константы.РаботаСПодтверждениямиПозицийЗаказа.Получить() Тогда
		ЭлементыФормы.Тип.СписокВыбора.Добавить(Перечисления.ТипыГруппПечати.Монитор);
		ЭлементыФормы.Тип.СписокВыбора.Добавить(Перечисления.ТипыГруппПечати.МониторПринтер);
	КонецЕсли; 
	ЭлементыФормы.Тип.СписокВыбора.Добавить(Перечисления.ТипыГруппПечати.Нет);
	
КонецПроцедуры

Процедура ПриЗакрытии()
	
	СохранитьЗначение("ТекущаяСтраницаГруппыПечати", ЭлементыФормы.Панель1.ТекущаяСтраница.Имя);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если НЕ СигналПослеПечати Тогда
		ДлительностьСигнала = 0;
	КонецЕсли; 
	
	Если глВерсия>1 Тогда
		Если ЭтотОбъект.ПодтвержденияОтключить ИЛИ ЭтотОбъект.РежимОповещения<2 Тогда
			ДопРМОповещения.Очистить();
		КонецЕсли;
		ИнтерфейсАдмина.ЗаполнитьТабЧастьПоСпискуПометок(ДопРМОповещения, ЭтотОбъект.РабочиеМестаОповещения, "РабочееМесто");
    КонецЕсли;
		
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если Тип = Перечисления.ТипыГруппПечати.Принтер И НумерацияМарок Тогда
		
		ДопИнф = РегистрыСведений.ГруппыПечатиДопИнф.СоздатьМенеджерЗаписи();
		ДопИнф.ГруппаПечати = Ссылка;
		ДопИнф.НомерМарки	= НомерМарки;
		Попытка
			ДопИнф.Записать();
		Исключение
			Сообщить(ОписаниеОшибки());
			Отказ = Истина;
			Возврат;
		КонецПопытки;
		
	КонецЕсли; 
	
	ИнтерфейсАдмина.ЗаписатьДопНаименования(Ссылка,ТаблицаДопНаименования);
	
КонецПроцедуры

Процедура ОбновлениеОтображения()
	
	ЭлементыФормы.Панель1.Страницы.Принтер.Видимость = ЕстьПринтер;
	//ЭлементыФормы.Панель1.Страницы.Подтверждение.Видимость	= (ЕстьПринтер ИЛИ ЕстьМонитор) И глВерсия>1 И Константы.ПодтверждениеГотовности.Получить();
	ЭлементыФормы.Панель1.Страницы.Подтверждение.Видимость	= (ЕстьПринтер ИЛИ ЕстьМонитор) И глВерсия>1 И Константы.РаботаСПодтверждениямиПозицийЗаказа.Получить();
	ЭлементыФормы.Панель1.Страницы.ДопНаименования.Видимость= ЕстьПринтер И глВерсия>1 И Константы.ДопЯзыки.Получить();
	
	ЭлементыФормы.НадписьНомерМарки	.Видимость	= НумерацияМарок;
	ЭлементыФормы.НомерМарки		.Видимость	= НумерацияМарок;
	
	ЭлементыФормы.НадписьДлительность.Видимость	= СигналПослеПечати;
	ЭлементыФормы.ДлительностьСигнала.Видимость	= СигналПослеПечати;
	
	ЭлементыФормы.ПодтвержденияОтключить.Видимость	= НЕ Тип = Перечисления.ТипыГруппПечати.Монитор;
	Если НЕ ЭлементыФормы.ПодтвержденияОтключить.Видимость И ЭтотОбъект.ПодтвержденияОтключить Тогда
		ПодтвержденияОтключить = Ложь;
	КонецЕсли;
	
	Если глВерсия>1 Тогда
		ЭлементыФормы.ПанельПодтверждение			.Доступность	= НЕ (ЭтотОбъект.ПодтвержденияОтключить И Тип <> Перечисления.ТипыГруппПечати.МониторПринтер);
		ЭлементыФормы.ДопРМОповещения				.Видимость		= ЭтотОбъект.РежимОповещения=2;
		ЭлементыФормы.КоманднаяПанельДопРМОповещения.Видимость		= ЭтотОбъект.РежимОповещения=2;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура ТипПриИзменении(Элемент)
	
	ЕстьПринтер = (Тип = Перечисления.ТипыГруппПечати.Принтер ИЛИ Тип = Перечисления.ТипыГруппПечати.МониторПринтер);
	ЕстьМонитор = (Тип = Перечисления.ТипыГруппПечати.Монитор ИЛИ Тип = Перечисления.ТипыГруппПечати.МониторПринтер);
	
КонецПроцедуры

Процедура ТипОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры
 
Процедура ОборудованиеПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	Если ДанныеСтроки.НомерСтроки = 1 Тогда
		ОформлениеСтроки.Ячейки.Тип.Текст = "Основной";
	ИначеЕсли ДанныеСтроки.Копия Тогда
		ОформлениеСтроки.Ячейки.Тип.Текст = "Печатать копию";
	Иначе
		ОформлениеСтроки.Ячейки.Тип.Текст = "Запасной";
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОборудованиеВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Если Колонка.Имя = "Тип" Тогда
		Если ВыбраннаяСтрока.НомерСтроки = 1 Тогда
			Предупреждение("Первый принтер всегда основной!",3);
		Иначе
			ВыбраннаяСтрока.Копия = НЕ ВыбраннаяСтрока.Копия;
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОборудованиеПринтерНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СписокВидов = Новый СписокЗначений;
	СписокВидов.Добавить("АО");
	СписокВидов.Добавить("ПРН");
	тоВыбратьИзСпискаТО(СписокВидов, ЭтаФорма, Элемент);
	
КонецПроцедуры

Процедура КоманднаяПанельДопРМОповещенияУстановитьФлажки(Кнопка)
	
	ДопРМОповещения.ЗаполнитьПометки(Истина);
	
КонецПроцедуры

Процедура КоманднаяПанельДопРМОповещенияСнятьФлажки(Кнопка)
	
	ДопРМОповещения.ЗаполнитьПометки(Ложь);
	
КонецПроцедуры

Процедура Язык1НачалоВыбора(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ИнтерфейсАдмина.ВыбратьИзСпискаЯзыков(ЭтаФорма, Элемент);
КонецПроцедуры

Процедура НадписьНастройкаРМНажатие(Элемент)
	
	ИнтерфейсАдмина.ОткрытьНастройкуРМ("Принтеры");
	
КонецПроцедуры
