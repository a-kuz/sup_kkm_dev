
Процедура ЗакрытьНажатие(Элемент)
	Закрыть();
КонецПроцедуры

Процедура ЗаполнитьНажатие(Элемент)
	
	Если КоличествоМест = 0 Тогда
		Предупреждение("Укажите количество создаваемых элементов!");
		Возврат;	
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВыбНаименование) Тогда
		Предупреждение("Укажите наиненование для создаваемых элементов!");
		Возврат;
	КонецЕсли;	
	
	Если Не ЗначениеЗаполнено(ВыбГруппа) Тогда
		Предупреждение("Укажите группу для создаваемых элементов!");
		Возврат;	
	КонецЕсли;
	
	Если Вопрос("Создать посадочные места?", РежимДиалогаВопрос.ОКОтмена) = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	ПоследнийНомер = ПолучитьПоследнийНомерВГруппе(ВыбГруппа);
	
	МестоРеализации = ?(ЛимитнаяКарта, Справочники.МестаРеализации.ПустаяСсылка(), ВыбМестоРеализации);
	Если МестоРеализации.Пустая() Тогда
		Если глВерсия > 1 И НЕ ЗначениеЗаполнено(ИнформационнаяБаза) Тогда
			ИнформационнаяБаза = ПараметрыСеанса.ТекущаяИБ;
		КонецЕсли; 
	Иначе
		ИнформационнаяБаза = ПараметрыСеанса.ТекущаяИБ;
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Для НомерМеста=1 По КоличествоМест Цикл                       			
		
		Место = Справочники.ПосадочныеМеста.СоздатьЭлемент();
		
		Место.Родитель							= ВыбГруппа;
		Место.МестоРеализации					= МестоРеализации;
		Место.Номер								= НомерМеста + ПоследнийНомер;

		Если НЕ ЛимитнаяКарта Тогда
			Место.ЗапретНесколькихЗаказов		= ВыбЗапретНесколькихЗаказов;
		Иначе
			Место.ЛимитнаяКарта					= Истина;
			Место.СуммаЛимита                	= ВыбСуммаЛимита;
			Место.СуммаМинимальнойПредоплаты 	= ВыбМинПредоплата;
			Место.ЗапросСуммыПредоплаты      	= ВыбЗапросСуммыПредоплаты;
			Место.ЗапретДовнесенияПредоплаты	= ВыбЗапретДовнесения;
		КонецЕсли;
		
		Если глВерсия > 1 тогда
			Место.ИнформационнаяБаза			= ИнформационнаяБаза;
			Место.Скидка						= ?(ЛимитнаяКарта,ВыбСкидкаЛимит,ВыбСкидка);
			Место.НеИнтернетБронь				= НеИнтернетБронь;
		КонецЕсли;

		Для Каждого СтрокаТЧ Из АвтоТовары Цикл 
			Место.АвтоТовары.Добавить().Товар	= СтрокаТЧ.Товар;
		КонецЦикла;
		
		Место.УстановитьКодВнутриИБ();
		Место.Наименование						= Вычислить(Шаблон2Выражение(ВыбНаименование));	
		
		Место.Записать();
		
	КонецЦикла;			
	
	ЗафиксироватьТранзакцию();
	
	Закрыть();
	
КонецПроцедуры

Процедура ВыбГруппаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ВыбГруппа) Тогда
		ОбщийСписок.Отбор.Сбросить();
		ОбщийСписок.Отбор["Ссылка"].Использование = Истина;
		ОбщийСписок.Отбор["Ссылка"].Значение = ВыбГруппа;
		ОбщийСписок.Отбор["Ссылка"].ВидСравнения = ВидСравнения.ВИерархии;
		ЭлементыФормы.ОбщийСписок.ИерархическийПросмотр = Ложь;	
		ВыбНаименование = ВыбГруппа.ШаблонНаименования;
	Иначе 
		ОбщийСписок.Отбор.Сбросить();
	КонецЕсли;
	
КонецПроцедуры

Процедура СкидкаНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИнтерфейсАдмина.ВыборСкидкиБонуса(Элемент, Ложь);
	
КонецПроцедуры

Процедура СкидкаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ВыбСкидка) И НЕ ВыбСкидка.ЭтоГруппа И ВыбСкидка.Бонусная  Тогда
		Элемент.Значение = Справочники.Скидки.ПустаяСсылка();
		Сообщить("Нельзя привязать бонус к посадочному месту!");
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	ЭлементыФормы.НадписьСкидка.Видимость	= глВерсия>1;
	ЭлементыФормы.Скидка.Видимость			= глВерсия>1;
	
	Если глВерсия=3 Тогда
		ЭлементыФормы.Скидка.ВыборГруппИЭлементов = ИспользованиеГруппИЭлементов.ГруппыИЭлементы;
	Иначе
		ЭлементыФормы.Скидка.ВыборГруппИЭлементов = ИспользованиеГруппИЭлементов.Элементы;
	КонецЕсли; 
	
	ЭлементыФормы.ИнформационнаяБаза.Видимость = ПараметрыСеанса.РаспределенныйРежим;
	Если ПараметрыСеанса.РаспределенныйРежим  Тогда
		ЭлементыФормы.ИнформационнаяБаза.Значение = ПараметрыСеанса.ТекущаяИБ;
	КонецЕсли;
	
	
	Если глВерсия<3 Тогда
		ЭлементыФормы.ЛимитнаяКарта.Видимость = Ложь;
		ЭлементыФормы.Панель1.Страницы.Страница1.Видимость = Истина;
		ЭлементыФормы.Панель1.Страницы.Страница2.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолеВвода4ПриИзменении(Элемент)
	Если ПараметрыСеанса.РаспределенныйРежим Тогда
		ИнформационнаяБаза = ?(ЗначениеЗаполнено(Элемент.Значение),Элемент.Значение.ИнформационнаяБаза,ПараметрыСеанса.ТекущаяИБ); 
        ЭлементыФормы.ИнформационнаяБаза.Значение = ИнформационнаяБаза; 
	КонецЕсли;
КонецПроцедуры

Процедура ЛимитнаяКартаПриИзменении(Элемент)
	ЭлементыФормы.Панель1.Страницы.Страница1.Видимость = НЕ Элемент.Значение;
	ЭлементыФормы.Панель1.Страницы.Страница2.Видимость = Элемент.Значение;
КонецПроцедуры

Процедура ИнформационнаяБазаНажатие(Элемент)
		
	ВыбЭлемент = ИнтерфейсАдмина.ИнфБазаВыбратьИзСписка(ЭтаФорма, Элемент);
	Если ВыбЭлемент <> Неопределено Тогда
		ИнформационнаяБаза = ВыбЭлемент.Значение; 
		ЭлементыФормы.ИнформационнаяБаза.Значение = ИнформационнаяБаза;
	КонецЕсли; 

КонецПроцедуры

Процедура ОбновлениеОтображения()
	ЭлементыФормы.ИнформационнаяБаза.ГиперСсылка = ВыбМестоРеализации.Пустая() ИЛИ (глВерсия=3 И ЛимитнаяКарта);
КонецПроцедуры

//
Функция ПолучитьПоследнийНомерВГруппе(ВыбГруппа) 

		ПоследнийНомер = 0;
	
	// Получаем последний номер в группе
	Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПосадочныеМеста.Номер КАК Номер
		|ИЗ
		|	Справочник.ПосадочныеМеста КАК ПосадочныеМеста
		|ГДЕ
		|	ПосадочныеМеста.Родитель В ИЕРАРХИИ(&Родитель)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Номер УБЫВ
		|");
	
	Запрос.УстановитьПараметр("Родитель", ВыбГруппа);
	
	Если НЕ Запрос.Выполнить().Пустой() Тогда
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		ПоследнийНомер = Выборка.Номер;
	КонецЕсли;
	
	Возврат ПоследнийНомер;

КонецФункции

Процедура ШаблонНаименованияВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	Меню = Новый СписокЗначений;
	Меню.Добавить("Типовой ШАБЛОН");
	Меню.Добавить("Место.Номер"					, "Внутренний номер");
	Меню.Добавить("Место.Код"					, "Код");
	Меню.Добавить("Место.Емкость"				, "Емкость");
	Меню.Добавить("Место.Родитель.Наименование"	, "Родитель (группа)");
	Меню.Добавить("Место.МестоРеализации"		, "Место реализации");
	Меню.Добавить("Место.ИнформационнаяБаза"	, "Информационная база");
	Меню.Добавить("Место.Скидка"				, "Скидка");
	Меню.Добавить("Место.Клиент"				, "Клиент");
	Меню.Добавить("Место.СуммаЛимита"			, "Сумма лимита");
	Меню.Добавить("Место.Наименование"			, "Наименование");
	
	ВыбЭлемент = ВыбратьИзМеню(Меню);
	Если ВыбЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыбЭлемент.Значение = "Типовой ШАБЛОН" Тогда
		ВыбНаименование = "Стол №[Место.Номер]";
	Иначе	
		ВыбНаименование = ВыбНаименование+"["+ВыбЭлемент.Значение+"]";
	КонецЕсли;
	
КонецПроцедуры
