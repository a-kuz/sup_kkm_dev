
// Получить массив Мониторов повара (pr не используется)
//
Функция ПолучитьСписокМониторовМарок() Экспорт 	
	
	Если ПараметрыСеанса.Версия = 1 Тогда
		Возврат Новый Массив;
	КонецЕсли;	
	
	Запрос = Новый Запрос("ВЫБРАТЬ
		|	РабочиеМеста.Ссылка
		|ИЗ
		|	Справочник.РабочиеМеста КАК РабочиеМеста
		|ГДЕ
		|	РабочиеМеста.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыРМ.Производство)
		|	И НЕ РабочиеМеста.ПометкаУдаления");
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

// Получить соответствие ГруппаПечати (для монитора марок) к РМ
//
Функция ПолучитьСоответствиеМониторРМ() Экспорт 
	
	СоответствиеМониторРМ = Новый Соответствие;
	
	Если ПараметрыСеанса.Версия = 1 Тогда
		Возврат СоответствиеМониторРМ;
	КонецЕсли;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	РабочиеМеста.Ссылка
	|ИЗ
	|	Справочник.РабочиеМеста КАК РабочиеМеста
	|ГДЕ
	|	РабочиеМеста.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыРМ.Производство)
	|	И НЕ РабочиеМеста.ПометкаУдаления
	|	И РабочиеМеста.ИнформационнаяБаза = &ИнформационнаяБаза");
	
	// выборка РМ монитора марок по текущей информационной базе
	Запрос.УстановитьПараметр("ИнформационнаяБаза", ПараметрыСеанса.ТекущаяИБ);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() > 0 Тогда
		
		Пока Выборка.Следующий() Цикл
			// получить объект справочника РМ
			ОбъектРМ = Выборка.Ссылка.ПолучитьОбъект();
			// получить параметры РМ
			Попытка
				СтруктураПараметровРМ = ЗначениеИзСтрокиВнутр(ОбъектРМ.ПараметрыРМ);
			Исключение
				Продолжить;
			КонецПопытки;
			// Получить список ГруппПечати если есть
			Если СтруктураПараметровРМ.Свойство("МестоПроизводстваТаблица") Тогда
				
				МассивГруппПечати = СтруктураПараметровРМ.МестоПроизводстваТаблица.ВыгрузитьКолонку("МестоПроизводства");
				
				Для Каждого ГруппаПечати из МассивГруппПечати Цикл
					// Определить есть ли группа печати в соответствии
					СписокРМ = СоответствиеМониторРМ.Получить(ГруппаПечати);	
					// Если нет добавляем новое
					Если СписокРМ = Неопределено Тогда
						СписокРМ = Новый СписокЗначений;
						СписокРМ.Добавить(Выборка.Ссылка);
						СоответствиеМониторРМ.Вставить(ГруппаПечати, СписокРМ);	
					// Если есть ГруппаПечати и нет РМ то добавляем	
					ИначеЕсли СписокРМ.НайтиПоЗначению(Выборка.Ссылка) = Неопределено Тогда
						СписокРМ.Добавить(Выборка.Ссылка);
						СоответствиеМониторРМ.Вставить(ГруппаПечати, СписокРМ);
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли; 
			
		КонецЦикла;	
	КонецЕсли;
	
	Возврат СоответствиеМониторРМ;
		
КонецФункции