// Функция формирует наименование файла с размером
// 
Функция СформироватьНаименование(ИмяФайла) Экспорт
	
	Файл = Новый Файл(ИмяФайла);
	
	Возврат Файл.Имя + " (" + Справочники.ХранилищеДополнительнойИнформации.ПолучитьПредставлениеРазмера(Файл.Размер()) + ")";
	
КонецФункции //СформироватьНаименование()

// Процедура выполняет обновление значения реквизита "Хранилища" на основании параметра ДанныеИзображения
// Если параметр "Записывать" = Истина, то происходит запись объекта
Процедура ОбновитьИзображение(ДанныеИзображения, Записывать = Истина) Экспорт
	Хранилище = Справочники.ХранилищеДополнительнойИнформации.УпаковатьИзображениеВХранилище(ДанныеИзображения);
	
	Если Записывать Тогда
		Записать();
	КонецЕсли;
КонецПроцедуры

// Процедура выполняет удаление хранилища значения
// Если параметр "Записывать" = Истина, то происходит запись объекта
Процедура УдалитьИзображение(Записывать = Истина) Экспорт
	Хранилище = Неопределено;
	Наименование = "";
	
	Если Записывать Тогда
		Записать();
	КонецЕсли;
КонецПроцедуры

// Клинетская процедура выбора и сохранения изображения
// Предлагает пользователю выбрать изображение из файла
// Возвращает Картинку в случае успешного выбора и Ложь в случает отказа от выбора
Функция ВыбратьИзображениеИзФайла(ЗаписыватьОбъект = Ложь) Экспорт
	
	#ЕСЛИ ТолстыйКлиентОбычноеПриложение ТОГДА
		ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
		ДиалогОткрытияФайла.ПредварительныйПросмотр = Истина;
		ДиалогОткрытияФайла.Заголовок = "Выберите файл с фотографией";
		ДиалогОткрытияФайла.ПолноеИмяФайла = "";
		ДиалогОткрытияФайла.Фильтр = ХранилищеДополнительнойИнформации.ПолучитьФильтрИзображений();
		
		Если ДиалогОткрытияФайла.Выбрать() Тогда
			ВыбранноеФото = Новый Картинка(ДиалогОткрытияФайла.ПолноеИмяФайла,Ложь);
			Хранилище = Новый ХранилищеЗначения(ВыбранноеФото, Новый СжатиеДанных());
			Наименование = СформироватьНаименование(ДиалогОткрытияФайла.ПолноеИмяФайла);
			
			Если ЗаписыватьОбъект Тогда
				Записать();
			КонецЕсли;
			
			Возврат ВыбранноеФото;
		Иначе
			Возврат Ложь;
		КонецЕсли;
	#ИНАЧЕ
		Возврат Ложь;
	#КОНЕЦЕСЛИ
	
КонецФункции