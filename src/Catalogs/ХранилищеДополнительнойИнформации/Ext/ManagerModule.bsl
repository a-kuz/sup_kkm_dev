
// Функция создает новый элемент справочника на основании параметра ДанныеИзображения
// ДанныеИзображения может принимать следующие значения:
//     Строка - ссылка на файл с изображением  
//     Файл - файл с изображением
//     Картинка - картинка сразу же упаковывается в хранилище значения
//     Двоичные данные - на основании двоичных данных формируется картинка, а затем она упаковывается в хр. значения
//     Хранилище значения - упаковка не требуется, ДанныеИзображения возвращается в качестве результата
// На основании данных изображения формируется наименование элемента справочника
// Возвращаемое значение - справочник объект "Хранилище доп. информации"
// Если параметр Записывать = Истина, то результат-объект будет записыван в базу данных, иначе возвращенный объект не будет записан.
Функция СоздатьПоИзображению(ДанныеИзображения, Записывать = Истина) Экспорт
	ТипДанныхПараметра = ТипЗнч(ДанныеИзображения);
	
	ОбъектХДИ = Справочники.ХранилищеДополнительнойИнформации.СоздатьЭлемент();
	
	Если ТипДанныхПараметра = Тип("Строка") Тогда
		ОбъектХДИ.Наименование = ОбъектХДИ.СформироватьНаименование(ДанныеИзображения);
	ИначеЕсли ТипДанныхПараметра = Тип("Файл") Тогда
		ОбъектХДИ.Наименование = ОбъектХДИ.СформироватьНаименование(ДанныеИзображения.ПолноеИмя);
	ИначеЕсли ТипДанныхПараметра = Тип("Картинка") Тогда
		ОбъектХДИ.Наименование = " (" + ПолучитьПредставлениеРазмера(ДанныеИзображения.ПолучитьДвоичныеДанные().Размер()) + ")";
	ИначеЕсли ТипДанныхПараметра = Тип("ДвоичныеДанные") Тогда
		ОбъектХДИ.Наименование = " (" + ПолучитьПредставлениеРазмера(ДанныеИзображения.Размер()) + ")";
	ИначеЕсли ТипДанныхПараметра = Тип("ХранилищеЗначения") Тогда
		ЗначениеХранилищаЗначения = ДанныеИзображения.Получить();
		Если ТипЗнч(ЗначениеХранилищаЗначения) = Тип("Картинка") Тогда
			ОбъектХДИ.Наименование = " (" + ПолучитьПредставлениеРазмера(ЗначениеХранилищаЗначения.ПолучитьДвоичныеДанные().Размер()) + ")";
		ИначеЕсли ТипЗнч(ЗначениеХранилищаЗначения) = Тип("ДвоичныеДанные") Тогда
			ОбъектХДИ.Наименование = " (" + ПолучитьПредставлениеРазмера(ЗначениеХранилищаЗначения.Размер()) + ")";
		Иначе
			ВызватьИсключение "Некорректное значение данных. В качестве значения хранилища значения следует передавать Картинку либо Двоичные данные."
		КонецЕсли;
	КонецЕсли;
	
	ОбъектХДИ.Хранилище = УпаковатьИзображениеВХранилище(ДанныеИзображения);
	
	Если Записывать Тогда
		ОбъектХДИ.Записать();	
	КонецЕсли;
	
	Возврат ОбъектХДИ;
		
КонецФункции

// Функция получает на входе данные изображения и возвращает хранилище значения, в которое упаковано изображение
// ДанныеИзображения может принимать следующие значения:
//     Строка - ссылка на файл с изображением  
//     Файл - файл с изображением
//     Картинка - картинка сразу же упаковывается в хранилище значения
//     Двоичные данные - на основании двоичных данных формируется картинка, а затем она упаковывается в хр. значения
//     Хранилище значения - упаковка не требуется, ДанныеИзображения возвращается в качестве результата
Функция УпаковатьИзображениеВХранилище(ДанныеИзображения) Экспорт
	
	ХранилищеЗначенияРезультат = Неопределено;
	
	ТипДанныхИзображения = ТипЗнч(ДанныеИзображения);
	
	Если ТипДанныхИзображения = Тип("Строка") Тогда
		Файл = Новый Файл(ДанныеИзображения);
		Если Файл.Существует() Тогда
			Изображение = Новый Картинка(ДанныеИзображения, Ложь);
			ХранилищеЗначенияРезультат = Новый ХранилищеЗначения(Изображение, Новый СжатиеДанных());
		Иначе
			ВызватьИсключение "Некорректное значение данных. Файл по адресу: """ + ДанныеИзображения + """ не существует.";
		КонецЕсли;
	ИначеЕсли ТипДанныхИзображения = Тип("Файл") Тогда
		Если ДанныеИзображения.Существует() Тогда
			Изображение = Новый Картинка(ДанныеИзображения.ПолноеИмя, Ложь);
			ХранилищеЗначенияРезультат = Новый ХранилищеЗначения(Изображение, Новый СжатиеДанных());
		Иначе
			ВызватьИсключение "Некорректное значение данных. Файл по адресу: """ + ДанныеИзображения + """ не существует.";
		КонецЕсли;
	ИначеЕсли ТипДанныхИзображения = Тип("Картинка") Тогда
		ХранилищеЗначенияРезультат = Новый ХранилищеЗначения(ДанныеИзображения, Новый СжатиеДанных());
	ИначеЕсли ТипДанныхИзображения = Тип("ДвоичныеДанные") Тогда
		Изображение = Новый Картинка(ДанныеИзображения, Ложь);
		ХранилищеЗначенияРезультат = Новый ХранилищеЗначения(ДанныеИзображения, Новый СжатиеДанных());
	ИначеЕсли ТипДанныхИзображения = Тип("ХранилищеЗначения") Тогда
		ХранилищеЗначенияРезультат = ДанныеИзображения;	
	КонецЕсли;
	
	Возврат ХранилищеЗначенияРезультат;
	
КонецФункции

// Функция возвращает строкове представление размера
// В качестве параметра передается число - размер в битах
Функция ПолучитьПредставлениеРазмера(РазмерВБитах) Экспорт
	
	Килобайты = 1024;
	Мегабайты = 1024 * Килобайты;
	Гигабайты = 1024 * Мегабайты;
	
	Если РазмерВБитах > Гигабайты Тогда
		Размер = РазмерВБитах / Гигабайты;
		Единицы = " ГБ";
	ИначеЕсли РазмерВБитах > Мегабайты Тогда
		Размер = РазмерВБитах / Мегабайты;
		Единицы = " МБ";
	ИначеЕсли РазмерВБитах > Килобайты Тогда
		Размер = РазмерВБитах / Килобайты;
		Единицы = " КБ";
	Иначе
		Размер = РазмерВБитах;
		Единицы = " Б";		
	КонецЕсли;
	
	Возврат Формат(Размер, "ЧЦ=10; ЧДЦ=0; ЧН=0") + Единицы;	
КонецФункции