// Возвращает массив узлов плана обмена по группе ИБ
//
// Параметры:
//	ГруппаИБ  	 - СправочникСсылка.ГруппыИнформационныхБаз - группа ИБ
//
// Возвращаемое значение:
//   Массив   - узлы
//
Функция УзлыПоГруппе(ГруппаИБ, КромеТекущего = Ложь) Экспорт
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	СоставГруппИнформационныхБаз.ИнформационнаяБаза КАК ИнформационнаяБаза
	|ПОМЕСТИТЬ ИнформационныеБазы
	|ИЗ
	|	РегистрСведений.СоставГруппИнформационныхБаз КАК СоставГруппИнформационныхБаз
	|ГДЕ
	|	СоставГруппИнформационныхБаз.Группа = &Группа
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Основной.Ссылка КАК Узел
	|ИЗ
	|	ПланОбмена.Основной КАК Основной
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИнформационныеБазы КАК ИнформационныеБазы
	|		ПО (ИнформационныеБазы.ИнформационнаяБаза = Основной.ИнформационнаяБаза)
	|ГДЕ
	|	(НЕ ИнформационныеБазы.ИнформационнаяБаза ЕСТЬ NULL
	|			ИЛИ &Группа = ЗНАЧЕНИЕ(Справочник.ГруппыИнформационныхБаз.ВсеИБ))
	|	И (Основной.ЭтотУзел = ЛОЖЬ
	|			ИЛИ &КромеТекущего = ЛОЖЬ)");
	Запрос.УстановитьПараметр("Группа", ГруппаИБ);
	Запрос.УстановитьПараметр("КромеТекущего", КромеТекущего);
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Узел");
КонецФункции

Функция ИБпоГруппе(ГруппаИБ) Экспорт
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	СоставГруппИнформационныхБаз.ИнформационнаяБаза КАК ИнформационнаяБаза
	|ИЗ
	|	РегистрСведений.СоставГруппИнформационныхБаз КАК СоставГруппИнформационныхБаз
	|ГДЕ
	|	СоставГруппИнформационныхБаз.Группа = &Группа");
	Запрос.УстановитьПараметр("Группа", ГруппаИБ);
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку(0);
КонецФункции

Функция ГруппыПоИБ(ИБ) Экспорт
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	СоставГруппИнформационныхБаз.Группа КАК Группа
	|ИЗ
	|	РегистрСведений.СоставГруппИнформационныхБаз КАК СоставГруппИнформационныхБаз
	|ГДЕ
	|	СоставГруппИнформационныхБаз.ИнформационнаяБаза = &ИБ");
	Запрос.УстановитьПараметр("ИБ", ИБ);
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку(0);
КонецФункции