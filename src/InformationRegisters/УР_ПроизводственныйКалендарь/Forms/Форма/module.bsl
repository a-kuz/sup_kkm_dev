////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем СписокВидовДней;
Перем ТекущийВидДня;

Перем ЭталоннаяТаблицаРегистра; 

Перем ТаблицаРегистра; 

Перем ТаблицаПраздников;

// Виды дней
Перем ВидДняРабочий;
Перем ВидДняСуббота;
Перем ВидДняВоскресенье;
Перем ВидДняПраздник;
Перем ВидДняПредпраздничный;

Перем мДлинаСуток;

// Цвета дат календаря
Перем ЦветРабочегоДня; 	  
Перем ЦветСубботы;	          
Перем ЦветВоскресенья; 	  
Перем ЦветПредпразничногоДня; 
Перем ЦветПраздничногоДня;	  

////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура УстановитьИнтервалКалендаря(ДатаУстановки)
	
	ЭлементыФормы.ПроизводственныйКалендарь.НачалоПериодаОтображения = НачалоГода(ДатаУстановки);
	ЭлементыФормы.ПроизводственныйКалендарь.КонецПериодаОтображения  = КонецГода(ДатаУстановки);
	
КонецПроцедуры

Процедура ЗаписатьДанныеРегистра()
	
	Если НЕ ТаблицыКалендаряИдентичны() Тогда
		
		ЗаписатьИзТаблицыВРегистр(ТаблицаРегистра, ГодВФорме);
		
		ДатаУстановки = Дата(Формат(ГодВФорме,"ЧГ=0")+"0101");
		РезЗапроса = СформироватьЗапросПоКалендарю(ДатаУстановки, КонецГода(ДатаУстановки));
		
		Если РезЗапроса.Пустой() Тогда
			ТаблицаРегистра = ПервоначальноеЗаполнениеРегистра(ДатаУстановки);
			ТаблицаРегистра.Индексы.Добавить("ВидДня");
			ТаблицаРегистра.Индексы.Добавить("ДатаКалендаря");
			// Обновим эталонную таблицу
			ЭталоннаяТаблицаРегистра.Очистить();
		Иначе
			ТаблицаРегистра = РезЗапроса.Выгрузить();
			ТаблицаРегистра.Индексы.Добавить("ВидДня");
			ТаблицаРегистра.Индексы.Добавить("ДатаКалендаря");
			// Обновим эталонную таблицу
			ЭталоннаяТаблицаРегистра = ТаблицаРегистра.Скопировать();
		КонецЕсли;
		
	КонецЕсли;
	
	Модифицированность = Ложь;
	
КонецПроцедуры

// Процедура обрабатывает перенос выходного дня, 
// проверяет доступность и правильность этого действия
Процедура ОбработкаПереносаПраздничногоДня(ЗаголовокФормы, ВидВыходногоДня)
	
	ВыбраннаяДата = ПроизводственныйКалендарь;
	
	ФормаВводаДаты = РегистрыСведений.УР_ПроизводственныйКалендарь.ПолучитьФорму("ФормаВводаДаты");
	ФормаВводаДаты.КалендарнаяДата = ПроизводственныйКалендарь;
	ФормаВводаДаты.ТаблицаРегистра = ТаблицаРегистра;
	ФормаВводаДаты.УстановитьЗаголовокФормы(ЗаголовокФормы);
	РезультатВыбора = ФормаВводаДаты.ОткрытьМодально();
	
	Если РезультатВыбора <> Неопределено Тогда
		ВыбраннаяДатаПереноса = РезультатВыбора;
		
		ДеньНеделиВыбраннойДаты = ДеньНедели(ВыбраннаяДата);
		ДеньНеделиВыбраннойДатыПереноса = ДеньНедели(ВыбраннаяДатаПереноса);
			
		
		СтрокаТаблицыВыбраннойДаты = ТаблицаРегистра.Найти(ВыбраннаяДата, "ДатаКалендаря");
		СтрокаТаблицыВыбраннойДатыПереноса = ТаблицаРегистра.Найти(ВыбраннаяДатаПереноса, "ДатаКалендаря");
			
		Если ДеньНеделиВыбраннойДаты = 6 ИЛИ ДеньНеделиВыбраннойДаты = 7 Тогда
			
			Если СтрокаТаблицыВыбраннойДаты.ВидДня = ВидДняСуббота 
				ИЛИ СтрокаТаблицыВыбраннойДаты.ВидДня = ВидДняВоскресенье Тогда
				УстанавливаемыйВидДняВыбраннойДаты = СтрокаТаблицыВыбраннойДатыПереноса.ВидДня;
			Иначе
				УстанавливаемыйВидДняВыбраннойДаты = СтрокаТаблицыВыбраннойДаты.ВидДня;
			КонецЕсли;
			
			Если ДеньНеделиВыбраннойДаты = 6 Тогда
				СтрокаТаблицыВыбраннойДатыПереноса.ВидДня = ВидДняСуббота;
			Иначе
				СтрокаТаблицыВыбраннойДатыПереноса.ВидДня = ВидДняВоскресенье
			КонецЕсли;
			СтрокаТаблицыВыбраннойДаты.ВидДня = УстанавливаемыйВидДняВыбраннойДаты;
				
		Иначе
			
			УстанавливаемыйВидДняВыбраннойДаты = СтрокаТаблицыВыбраннойДатыПереноса.ВидДня;
			
			ПереносимыйВидДня = СтрокаТаблицыВыбраннойДаты.ВидДня;
			Если ДеньНеделиВыбраннойДатыПереноса = 6 Тогда
				СтрокаТаблицыВыбраннойДаты.ВидДня = ВидДняСуббота;
			ИначеЕсли ДеньНеделиВыбраннойДатыПереноса = 7 Тогда	
				СтрокаТаблицыВыбраннойДаты.ВидДня = ВидДняВоскресенье
			Иначе
				СтрокаТаблицыВыбраннойДаты.ВидДня = ВидДняРабочий
			КонецЕсли;
			
			
			СтрокаТаблицыВыбраннойДаты.ВидДня = УстанавливаемыйВидДняВыбраннойДаты;
			СтрокаТаблицыВыбраннойДатыПереноса.ВидДня = ПереносимыйВидДня;
		КонецЕсли;
		
		ТекущийВидДня = СписокВидовДней.НайтиПоЗначению(СтрокаТаблицыВыбраннойДаты.ВидДня);
		
		ЭлементыФормы.ПроизводственныйКалендарь.Обновить();
	
	КонецЕсли;

КонецПроцедуры

Функция СформироватьЗапросПоКалендарю(ДатаНачалаЗапроса, ДатаОкончанияЗапроса)
	
	ЗапросПоКалендарю = Новый Запрос();
	ЗапросПоКалендарю.УстановитьПараметр("ДатаКалендаря1", ДатаНачалаЗапроса);
	ЗапросПоКалендарю.УстановитьПараметр("ДатаКалендаря2", ДатаОкончанияЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	УР_ПроизводственныйКалендарь.ДатаКалендаря,
	|	УР_ПроизводственныйКалендарь.ВидДня
	|ИЗ
	|	РегистрСведений.УР_ПроизводственныйКалендарь КАК УР_ПроизводственныйКалендарь
	|
	|ГДЕ
	|	УР_ПроизводственныйКалендарь.ДатаКалендаря МЕЖДУ &ДатаКалендаря1 И &ДатаКалендаря2";	
	
	ЗапросПоКалендарю.Текст = ТекстЗапроса;
	Возврат ЗапросПоКалендарю.Выполнить()
	
КонецФункции     

Функция ТаблицыКалендаряИдентичны()
	
	Если ЭталоннаяТаблицаРегистра.Количество() <> ТаблицаРегистра.Количество() Тогда
		Возврат Ложь
	КонецЕсли;
	
	Для Сч = 1 По ЭталоннаяТаблицаРегистра.Количество() Цикл
		Если ЭталоннаяТаблицаРегистра[Сч-1].ВидДня <> ТаблицаРегистра[Сч-1].ВидДня Тогда
			Возврат Ложь
		КонецЕсли; 
	КонецЦикла; 
	
	Возврат Истина
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	СписокВидовДней = ПолучитьСписокЭлементовПеречисления("УР_ВидыДнейПроизводственногоКалендаря");
	
	// Назначим цвета
	ЦветРабочегоДня 	   = Новый Цвет(  0,  0,   0); // Черный
	ЦветСубботы	           = Новый Цвет(153, 51,   0); // Темно-красный
	ЦветВоскресенья 	   = Новый Цвет(255,  0,   0); // Красный
	ЦветПредпразничногоДня = Новый Цвет(  0,  0, 186); // Темно-синий
	ЦветПраздничногоДня	   = Новый Цвет(255,  0, 255); // Фиолетовый
	
	Если ГодВФорме = 0 Тогда
		ГодВФорме = Год(ПолучитьРабочуюДату());
	КонецЕсли;
	ДатаКалендаря = Дата(ГодВФорме, 1, 1);
	
	УстановитьИнтервалКалендаря(ДатаКалендаря);
	
	РезЗапроса = СформироватьЗапросПоКалендарю(НачалоГода(ДатаКалендаря), КонецГода(ДатаКалендаря));
	
	ТаблицаРегистраПоУмолчанию = ПервоначальноеЗаполнениеРегистра(ДатаКалендаря);
	
	Если РезЗапроса.Пустой() Тогда
		Модифицированность = Истина;
		ТаблицаРегистра = ТаблицаРегистраПоУмолчанию.Скопировать();
		ТаблицаРегистра.Индексы.Добавить("ВидДня");
		ТаблицаРегистра.Индексы.Добавить("ДатаКалендаря");
	Иначе
		ТаблицаРегистра = РезЗапроса.Выгрузить();
		ТаблицаРегистра.Индексы.Добавить("ВидДня");
		ТаблицаРегистра.Индексы.Добавить("ДатаКалендаря");
		ЭталоннаяТаблицаРегистра = ТаблицаРегистра.Скопировать();
	КонецЕсли;
	
	// Отображение заполненого календаря
	ЭлементыФормы.ПроизводственныйКалендарь.Обновить();
	
	ЭлементыФормы.ПроизводственныйКалендарь.ТекущаяДата = НачалоГода(ДатаКалендаря);
	Модифицированность = Ложь;
	
	Заголовок = "Производственный календарь на " + Формат(ГодВФорме,"ЧГ=0") + " год";
	
КонецПроцедуры

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если НЕ ТаблицыКалендаряИдентичны() Тогда
		ЗаКакойГод = Год(ТаблицаРегистра[0].ДатаКалендаря);
		Если Вопрос("Записать измененные данные за " + Формат(ЗаКакойГод,"ЧГ=0") + " год?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
			ЗаписатьИзТаблицыВРегистр(ТаблицаРегистра,ЗаКакойГод);
		КонецЕсли;  
	КонецЕсли;
	
	Оповестить("ЗаполнениеПроизводственногоКалендаря");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ КОМАНДНОЙ ПАНЕЛИ ФОРМЫ

// Обработчик события нажатия кнопки "Первоначальное заполнение"
Процедура ДействияФормыПервоначальноеЗаполнение(Кнопка)
	
	Если Вопрос("Восстановить первоначальное заполнение производственного календаря?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
		
		ТаблицаРегистраПоУмолчанию = ПервоначальноеЗаполнениеРегистра(Дата(Формат(ГодВФорме,"ЧГ=0")+"0101"));
		ТаблицаРегистра = ТаблицаРегистраПоУмолчанию.Скопировать();
		ТаблицаРегистра.Индексы.Добавить("ВидДня");
		ТаблицаРегистра.Индексы.Добавить("ДатаКалендаря");

		// Отображение заполненого календаря
		ТекущийВидДня = Неопределено;
		ЭлементыФормы.ПроизводственныйКалендарь.Обновить();
		
	КонецЕсли;  
	
КонецПроцедуры

// Обработцик события нажатия кнопки "Печать"
Процедура ДействияФормыПечать(Кнопка)
	
	Если Модифицированность тогда
		Если Вопрос("Перед печать календаря необходимо записать внесенные изменения. Записать?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да тогда
			
			ЗаписатьДанныеРегистра();
		Иначе
			
			Возврат;
			
		КонецЕсли;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ГОД(УР_ПроизводственныйКалендарь.ДатаКалендаря) КАК ГодКалендаря,
	|	КВАРТАЛ(УР_ПроизводственныйКалендарь.ДатаКалендаря) КАК КварталКалендаря,
	|	МЕСЯЦ(УР_ПроизводственныйКалендарь.ДатаКалендаря) КАК МесяцКалендаря,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ УР_ПроизводственныйКалендарь.ДатаКалендаря) КАК КалендарныеДни,
	|	УР_ПроизводственныйКалендарь.ВидДня КАК ВидДня
	|ИЗ
	|	РегистрСведений.УР_ПроизводственныйКалендарь КАК УР_ПроизводственныйКалендарь
	|ГДЕ
	|	УР_ПроизводственныйКалендарь.Год = &Год
	|
	|СГРУППИРОВАТЬ ПО
	|	ГОД(УР_ПроизводственныйКалендарь.ДатаКалендаря),
	|	КВАРТАЛ(УР_ПроизводственныйКалендарь.ДатаКалендаря),
	|	МЕСЯЦ(УР_ПроизводственныйКалендарь.ДатаКалендаря),
	|	УР_ПроизводственныйКалендарь.ВидДня
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГодКалендаря,
	|	КварталКалендаря,
	|	МесяцКалендаря
	|ИТОГИ ПО
	|	ГодКалендаря,
	|	КварталКалендаря,
	|	МесяцКалендаря";
	
	МакетПечати = РегистрыСведений.УР_ПроизводственныйКалендарь.ПолучитьМакет("ПроизводственныйКалендарь");
	ПечатнаяФорма = Новый ТабличныйДокумент;
	ПечатнаяФорма.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПроизводственныйКалендарь_";
	ЗаголовокПечати =  МакетПечати.ПолучитьОбласть("Заголовок");
	ЗаголовокПечати.Параметры.Год = Формат(ГодВФорме, "ЧЦ=4; ЧГ=0");
	ПечатнаяФорма.Вывести(ЗаголовокПечати);
	
	// начальные значения, независимо от результата выполнения запроса
	РабочееВремя40Год = 0;
	РабочееВремя36Год = 0;
	РабочееВремя24Год = 0;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Год", ГодВФорме);
	Результат = Запрос.Выполнить();
	ВыборкаПоГоду = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоГоду.Следующий() Цикл
		ВыборкаПоКварталу = ВыборкаПоГоду.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоКварталу.Следующий() Цикл
			НомерКвартала =  МакетПечати.ПолучитьОбласть("Квартал");
			НомерКвартала.Параметры.НомерКвартала = "" + ВыборкаПоКварталу.КварталКалендаря + " квартал";
			ПечатнаяФорма.Вывести(НомерКвартала);
			ШапкаКвартала = МакетПечати.ПолучитьОбласть("ШапкаКвартала");
			ПечатнаяФорма.Вывести(ШапкаКвартала);
			
			КалендарныеДниКв = 0;
			РабочееВремя40Кв = 0;
			РабочееВремя36Кв = 0;
			РабочееВремя24Кв = 0;
			РабочиеДниКв	 = 0;
			ВыходныеДниКв	 = 0;
			
			Если ВыборкаПоКварталу.КварталКалендаря = 1 или ВыборкаПоКварталу.КварталКалендаря = 3 тогда
				КалендарныеДниПолугодие1 = 0;
				РабочееВремя40Полугодие1 = 0;
				РабочееВремя36Полугодие1 = 0;
				РабочееВремя24Полугодие1 = 0;
				РабочиеДниПолугодие1	 = 0;
				ВыходныеДниПолугодие1	 = 0;
			КонецЕсли;
			
			Если ВыборкаПоКварталу.КварталКалендаря = 1 тогда
				КалендарныеДниГод = 0;
				РабочееВремя40Год = 0;
				РабочееВремя36Год = 0;
				РабочееВремя24Год = 0;
				РабочиеДниГод	 = 0;
				ВыходныеДниГод	 = 0;
			КонецЕсли;
			
			ВыборкаПоМесяцу = ВыборкаПоКварталу.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаПоМесяцу.Следующий() Цикл
				ВыходныеДни 	= 0;
				РабочееВремя40 	= 0;
				РабочееВремя36 	= 0;
				РабочееВремя24 	= 0;
				КалендарныеДни 	= 0;
				РабочиеДни 		= 0;
				ВыборкаПоВидуДня = ВыборкаПоМесяцу.Выбрать(ОбходРезультатаЗапроса.Прямой);
				Пока ВыборкаПоВидуДня.Следующий() Цикл
					Если ВыборкаПоВидуДня.ВидДня = ВидДняСуббота или 
						ВыборкаПоВидуДня.ВидДня = ВидДняВоскресенье или
						ВыборкаПоВидуДня.ВидДня = ВидДняПраздник тогда
						ВыходныеДни = ВыходныеДни + ВыборкаПоВидуДня.КалендарныеДни
					ИначеЕсли ВыборкаПоВидуДня.ВидДня = ВидДняРабочий тогда
						РабочееВремя40 = РабочееВремя40 + ВыборкаПоВидуДня.КалендарныеДни * 8;
						РабочееВремя36 = РабочееВремя36 + ВыборкаПоВидуДня.КалендарныеДни*36/5;
						РабочееВремя24 = РабочееВремя24 + ВыборкаПоВидуДня.КалендарныеДни*24/5;
						РабочиеДни 	= РабочиеДни + ВыборкаПоВидуДня.КалендарныеДни;
					ИначеЕсли ВыборкаПоВидуДня.ВидДня = ВидДняПредпраздничный тогда
						РабочееВремя40 = РабочееВремя40 + ВыборкаПоВидуДня.КалендарныеДни * 7;
						РабочееВремя36 = РабочееВремя36 + ВыборкаПоВидуДня.КалендарныеДни*36/5 - 1;
						РабочееВремя24 = РабочееВремя24 + ВыборкаПоВидуДня.КалендарныеДни*24/5 - 1;
						РабочиеДни 	= РабочиеДни + ВыборкаПоВидуДня.КалендарныеДни;
					КонецЕсли;
					КалендарныеДни = КалендарныеДни + ВыборкаПоВидуДня.КалендарныеДни;
				КонецЦикла; // вид дня
				КалендарныеДниКв = КалендарныеДниКв + КалендарныеДни;
				РабочееВремя40Кв = РабочееВремя40Кв + РабочееВремя40;
				РабочееВремя36Кв = РабочееВремя36Кв + РабочееВремя36;
				РабочееВремя24Кв = РабочееВремя24Кв + РабочееВремя24;
				РабочиеДниКв	 = РабочиеДниКв 	+ РабочиеДни;
				ВыходныеДниКв	 = ВыходныеДниКв	+ ВыходныеДни;
				
				КалендарныеДниПолугодие1 = КалендарныеДниПолугодие1 + КалендарныеДни;
				РабочееВремя40Полугодие1 = РабочееВремя40Полугодие1 + РабочееВремя40;
				РабочееВремя36Полугодие1 = РабочееВремя36Полугодие1 + РабочееВремя36;
				РабочееВремя24Полугодие1 = РабочееВремя24Полугодие1 + РабочееВремя24;
				РабочиеДниПолугодие1	 = РабочиеДниПолугодие1 	+ РабочиеДни;
				ВыходныеДниПолугодие1	 = ВыходныеДниПолугодие1	+ ВыходныеДни;
				
				КалендарныеДниГод = КалендарныеДниГод + КалендарныеДни;
				РабочееВремя40Год = РабочееВремя40Год + РабочееВремя40;
				РабочееВремя36Год = РабочееВремя36Год + РабочееВремя36;
				РабочееВремя24Год = РабочееВремя24Год + РабочееВремя24;
				РабочиеДниГод	 = РабочиеДниГод 	+ РабочиеДни;
				ВыходныеДниГод	 = ВыходныеДниГод	+ ВыходныеДни;
				
				КолонкаМесяца = МакетПечати.ПолучитьОбласть("КолонкаМесяца");
				КолонкаМесяца.Параметры.ВыходныеДни = ВыходныеДни;
				КолонкаМесяца.Параметры.РабочееВремя40 	= РабочееВремя40;
				КолонкаМесяца.Параметры.РабочееВремя36 	= РабочееВремя36;
				КолонкаМесяца.Параметры.РабочееВремя24 	= РабочееВремя24;
				КолонкаМесяца.Параметры.КалендарныеДни 	= КалендарныеДни;
				КолонкаМесяца.Параметры.РабочиеДни 		= РабочиеДни;
				КолонкаМесяца.Параметры.ИмяМесяца 		= Формат(Дата(2000,ВыборкаПоМесяцу.МесяцКалендаря, 1), "ДФ='ММММ'");
				ПечатнаяФорма.Присоединить(КолонкаМесяца);
			КонецЦикла; // месяц
			КолонкаМесяца = МакетПечати.ПолучитьОбласть("КолонкаМесяца");
			КолонкаМесяца.Параметры.ВыходныеДни 	= ВыходныеДниКв;
			КолонкаМесяца.Параметры.РабочееВремя40 	= РабочееВремя40Кв;
			КолонкаМесяца.Параметры.РабочееВремя36 	= РабочееВремя36Кв;
			КолонкаМесяца.Параметры.РабочееВремя24 	= РабочееВремя24Кв;
			КолонкаМесяца.Параметры.КалендарныеДни 	= КалендарныеДниКв;
			КолонкаМесяца.Параметры.РабочиеДни 		= РабочиеДниКв;
			КолонкаМесяца.Параметры.ИмяМесяца 		= "" + ВыборкаПоКварталу.КварталКалендаря + " квартал";
			ПечатнаяФорма.Присоединить(КолонкаМесяца);
			
			Если ВыборкаПоКварталу.КварталКалендаря = 2 или ВыборкаПоКварталу.КварталКалендаря = 4 тогда
				КолонкаМесяца = МакетПечати.ПолучитьОбласть("КолонкаМесяца");
				КолонкаМесяца.Параметры.ВыходныеДни 	= ВыходныеДниПолугодие1;
				КолонкаМесяца.Параметры.РабочееВремя40 	= РабочееВремя40Полугодие1;
				КолонкаМесяца.Параметры.РабочееВремя36 	= РабочееВремя36Полугодие1;
				КолонкаМесяца.Параметры.РабочееВремя24 	= РабочееВремя24Полугодие1;
				КолонкаМесяца.Параметры.КалендарныеДни 	= КалендарныеДниПолугодие1;
				КолонкаМесяца.Параметры.РабочиеДни 		= РабочиеДниПолугодие1;
				КолонкаМесяца.Параметры.ИмяМесяца 		= "" + ВыборкаПоКварталу.КварталКалендаря/2 + " полугодие";
				ПечатнаяФорма.Присоединить(КолонкаМесяца);
			КонецЕсли;
			
		КонецЦикла;  // квартал
		КолонкаМесяца = МакетПечати.ПолучитьОбласть("КолонкаМесяца");
		КолонкаМесяца.Параметры.ВыходныеДни 	= ВыходныеДниГод;
		КолонкаМесяца.Параметры.РабочееВремя40 	= РабочееВремя40Год;
		КолонкаМесяца.Параметры.РабочееВремя36 	= РабочееВремя36Год;
		КолонкаМесяца.Параметры.РабочееВремя24 	= РабочееВремя24Год;
		КолонкаМесяца.Параметры.КалендарныеДни 	= КалендарныеДниГод;
		КолонкаМесяца.Параметры.РабочиеДни 		= РабочиеДниГод;
		КолонкаМесяца.Параметры.ИмяМесяца 		= "" + Формат(ВыборкаПоГоду.ГодКалендаря, "ЧЦ=4; ЧГ=0") + " год";
		ПечатнаяФорма.Присоединить(КолонкаМесяца);
		
	КонецЦикла; // год
	
	КолонкаМесяца = МакетПечати.ПолучитьОбласть("Среднемесячный");
	КолонкаМесяца.Параметры.РабочееВремя40 	= РабочееВремя40Год;
	КолонкаМесяца.Параметры.РабочееВремя36 	= РабочееВремя36Год;
	КолонкаМесяца.Параметры.РабочееВремя24 	= РабочееВремя24Год;
	КолонкаМесяца.Параметры.ИмяМесяца 		= "" + Формат(ГодВФорме, "ЧЦ=4; ЧГ=0") + " год";
	ПечатнаяФорма.Вывести(КолонкаМесяца);
	
	КолонкаМесяца = МакетПечати.ПолучитьОбласть("КолонкаМесяцаСр");
	КолонкаМесяца.Параметры.РабочееВремя40 	= Формат(РабочееВремя40Год / 12, "ЧДЦ=2; ЧГ=0");
	КолонкаМесяца.Параметры.РабочееВремя36 	= Формат(РабочееВремя36Год / 12, "ЧДЦ=2; ЧГ=0");
	КолонкаМесяца.Параметры.РабочееВремя24 	= Формат(РабочееВремя24Год / 12, "ЧДЦ=2; ЧГ=0");
	КолонкаМесяца.Параметры.ИмяМесяца 		= "среднемесячное количество";
	ПечатнаяФорма.Присоединить(КолонкаМесяца);
	
	ИнтерфейсАдмина.НапечататьДокумент(ПечатнаяФорма, 1, Ложь, "Производственный календарь");
	
КонецПроцедуры

// Обработцик события нажатия кнопки "Записать"
Процедура ДействияФормыЗаписать(Кнопка)
	
	ЗаписатьДанныеРегистра();
	
КонецПроцедуры

//////////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ КОМАНДНОЙ ПАНЕЛИ ФОРМЫ, СВЯЗАННЫЕ С ПРОИЗВОДСТВЕННЫМ КАЛЕНДАРЕМ

Процедура ДействияПеренестиВыходнойДеньСубботу(Элемент)
	
	ТекстКомментария = "Выберите рабочий день, на который необходимо перенести выходной день, "+Формат(ПроизводственныйКалендарь, "ДФ=""д ММММ, дддд""");
	ОбработкаПереносаПраздничногоДня(ТекстКомментария, ВидДняСуббота);
	
КонецПроцедуры

Процедура ДействияПеренестиВыходнойДеньВоскресенье(Элемент)
	
	ТекстКомментария = "Выберите рабочий день, на который необходимо перенести выходной день, "+Формат(ПроизводственныйКалендарь, "ДФ=""д ММММ, дддд""");
	ОбработкаПереносаПраздничногоДня(ТекстКомментария, ВидДняВоскресенье);
	
КонецПроцедуры

Процедура ДействияПеренестиРабочийДень(Элемент)
	
	ТекстКомментария = "Выберите выходной день, на который необходимо перенести рабочий день, "+Формат(ПроизводственныйКалендарь, "ДФ=""д ММММ, дддд""");
	ОбработкаПереносаПраздничногоДня(ТекстКомментария, ВидДняРабочий);
	
КонецПроцедуры

Процедура ДействияНазначитьПраздничным(Элемент)
	
	ТекущийВидДня = СписокВидовДней.НайтиПоЗначению(ВидДняПраздник);
	ЭлементыФормы.ПроизводственныйКалендарь.Обновить();
	
КонецПроцедуры

Процедура ДействияНазначитьПредпраздничным(Элемент)
	
	ТекущийВидДня = СписокВидовДней.НайтиПоЗначению(ВидДняПредпраздничный);
	ЭлементыФормы.ПроизводственныйКалендарь.Обновить();

КонецПроцедуры

Процедура ДействияНазначитьРабочим(Элемент)
	
	ТекущийВидДня = СписокВидовДней.НайтиПоЗначению(ВидДняРабочий);
	ЭлементыФормы.ПроизводственныйКалендарь.Обновить();	 
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

//Процедура, обработчик события ПриИзменении элемента ВыборГода
Процедура ВыборГодаПриИзменении(Элемент)
	
	ЗаКакойГод = Год(ТаблицаРегистра[0].ДатаКалендаря);
	
	Если НЕ ТаблицыКалендаряИдентичны() Тогда
		
		Если Вопрос("Записать измененные данные за " + Формат(ЗаКакойГод,"ЧГ=0") + " год?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
			ЗаписатьИзТаблицыВРегистр(ТаблицаРегистра,ЗаКакойГод)
		КонецЕсли;  
		
	КонецЕсли;
	
	ДатаУстановки = Дата(Формат(ГодВФорме,"ЧГ=0")+"0101");
	РезЗапроса = СформироватьЗапросПоКалендарю(ДатаУстановки, КонецГода(ДатаУстановки));
	ТаблицаРегистраПоУмолчанию = ПервоначальноеЗаполнениеРегистра(ДатаУстановки);
	Если РезЗапроса.Пустой() Тогда
		Модифицированность = Истина;
		ТаблицаРегистра = ТаблицаРегистраПоУмолчанию.Скопировать();
		ТаблицаРегистра.Индексы.Добавить("ВидДня");
		ТаблицаРегистра.Индексы.Добавить("ДатаКалендаря");
		// Обновим эталонную таблицу
		ЭталоннаяТаблицаРегистра.Очистить();
	Иначе
		ТаблицаРегистра = РезЗапроса.Выгрузить();
		ТаблицаРегистра.Индексы.Добавить("ВидДня");
		ТаблицаРегистра.Индексы.Добавить("ДатаКалендаря");
		// Обновим эталонную таблицу
		ЭталоннаяТаблицаРегистра = ТаблицаРегистра.Скопировать();
	КонецЕсли;
	
	//ОбновитьСтрокуСведенийОПеренесенныхДнях(Истина);
	
	// Отображение заполненого календаря
	ТекущийВидДня = Неопределено;
	ЭлементыФормы.ПроизводственныйКалендарь.Обновить();
	
	ПроизводственныйКалендарь = ДатаУстановки;
	УстановитьИнтервалКалендаря(ДатаУстановки);
	
	Заголовок = "Регламентированный производственный календарь на " + Формат(ГодВФорме,"ЧГ=0") + " год";
	
КонецПроцедуры

// Процедура, обработчик события ПриВыводеПериода элемента формы ПроизводственныйКалендарь
Процедура ПроизводственныйКалендарьПриВыводеПериода(Элемент, ОформлениеПериода)
	
	Если  ТекущийВидДня <> Неопределено Тогда  // Оформление календаря и запись в таблицу значений
		
		Коллекция_ОформленияДаты = ОформлениеПериода.Даты;
		
		Для каждого Строка_ОформленияДаты Из Коллекция_ОформленияДаты Цикл
			
			СтрокаТаблицыРегистра = ТаблицаРегистра.Найти(Строка_ОформленияДаты.Дата,);
			
			Если СтрокаТаблицыРегистра <> Неопределено Тогда
				
				Если Строка_ОформленияДаты.Дата = Элемент.ТекущаяДата Тогда
					ЗначениеСравнения = ТекущийВидДня.Значение
				Иначе
					ЗначениеСравнения = СтрокаТаблицыРегистра.ВидДня
				КонецЕсли; 
				
				Если ЗначениеСравнения = ВидДняРабочий Тогда
					
					СтрокаТаблицыРегистра.ВидДня = ВидДняРабочий;
					Строка_ОформленияДаты.ЦветТекста = ЦветРабочегоДня;
					
				ИначеЕсли ЗначениеСравнения = ВидДняПредпраздничный Тогда	 
					
					СтрокаТаблицыРегистра.ВидДня = ВидДняПредпраздничный;
					Строка_ОформленияДаты.ЦветТекста = ЦветПредпразничногоДня;
					
				ИначеЕсли ЗначениеСравнения = ВидДняПраздник Тогда
					
					СтрокаТаблицыРегистра.ВидДня = ВидДняПраздник;
					Строка_ОформленияДаты.ЦветТекста = ЦветПраздничногоДня;
					
				ИначеЕсли ЗначениеСравнения = ВидДняСуббота Тогда	
					
					СтрокаТаблицыРегистра.ВидДня = ВидДняСуббота;
					Строка_ОформленияДаты.ЦветТекста = ЦветСубботы;
					
				ИначеЕсли ЗначениеСравнения = ВидДняВоскресенье Тогда	 
					
					СтрокаТаблицыРегистра.ВидДня = ВидДняВоскресенье;
					Строка_ОформленияДаты.ЦветТекста = ЦветВоскресенья;
					
				КонецЕсли;	 
				
			КонецЕсли;
			
		КонецЦикла; 
		
	Иначе  // Только оформление календаря
		
		Коллекция_ОформленияДаты = ОформлениеПериода.Даты;
		
		Для каждого Строка_ОформленияДаты Из Коллекция_ОформленияДаты Цикл
			
			СтрокаТаблицыРегистра = ТаблицаРегистра.Найти(Строка_ОформленияДаты.Дата,);
			
			Если СтрокаТаблицыРегистра <> Неопределено Тогда
				
				ЗначениеСравнения = СтрокаТаблицыРегистра.ВидДня;
				
				Если ЗначениеСравнения = ВидДняРабочий Тогда
					Строка_ОформленияДаты.ЦветТекста = ЦветРабочегоДня;
				ИначеЕсли ЗначениеСравнения = ВидДняПредпраздничный Тогда	 
					Строка_ОформленияДаты.ЦветТекста = ЦветПредпразничногоДня;
				ИначеЕсли ЗначениеСравнения = ВидДняПраздник Тогда
					Строка_ОформленияДаты.ЦветТекста = ЦветПраздничногоДня;
				ИначеЕсли ЗначениеСравнения = ВидДняСуббота Тогда	
					Строка_ОформленияДаты.ЦветТекста = ЦветСубботы;
				ИначеЕсли ЗначениеСравнения = ВидДняВоскресенье Тогда	 
					Строка_ОформленияДаты.ЦветТекста = ЦветВоскресенья;
				КонецЕсли;	 
				
			КонецЕсли;
			
		КонецЦикла; 
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура, обработчик события ПриИзменении элемента формы ПроизводственныйКалендарь
// формирует список элементов меню для выбранного дня
Процедура ПроизводственныйКалендарьПриИзменении(Элемент)
	
	ДеньНедели = ДеньНедели(ПроизводственныйКалендарь);
	
	СтрокаТаблицыРегистра = ТаблицаРегистра.Найти(ПроизводственныйКалендарь, "ДатаКалендаря");
	Если СтрокаТаблицыРегистра <> Неопределено Тогда
		ВидДня = СтрокаТаблицыРегистра.ВидДня;
		
		ДействияКнопки = ЭлементыФормы.ДействияФормы.Кнопки.Действия.Кнопки;
		КонтекстДействияКнопки = ЭлементыФормы.КонтекстнаяКоманднаяПанель.Кнопки.Действия.Кнопки;
		КонтекстДействияКнопки.Очистить();
		
		МассивКнопокКУдалению = Новый Массив;
		Для каждого Кнопка Из ДействияКнопки Цикл
			Если Кнопка.Имя = "ПеренестиРабочийДень"
				ИЛИ Кнопка.Имя = "ПеренестиВыходнойДень" 
				
				ИЛИ Кнопка.Имя = "НазначитьПраздничным"
				ИЛИ Кнопка.Имя = "НазначитьПредпраздничным"
				
				ИЛИ Кнопка.Имя = "ОтменитьПредпраздничныйДень"
				ИЛИ Кнопка.Имя = "НазначитьРабочим" Тогда
				
				МассивКнопокКУдалению.Добавить(Кнопка);
				
			КонецЕсли;
		КонецЦикла;
		
		Для каждого ЭлементМассива Из МассивКнопокКУдалению Цикл
			ДействияКнопки.Удалить(ЭлементМассива);
		КонецЦикла;
		
		ТипКнопки = ТипКнопкиКоманднойПанели.Действие;
		
		Если ВидДня = ВидДняРабочий Тогда
			
			НовоеДействие = Новый Действие("ДействияПеренестиРабочийДень");
			ДействияКнопки.Вставить(0, "ПеренестиРабочийДень", ТипКнопки, "Перенести рабочий день", НовоеДействие);
			КонтекстДействияКнопки.Вставить(0, "ПеренестиРабочийДень", ТипКнопки, "Перенести рабочий день", НовоеДействие);
			
			НовоеДействие = Новый Действие("ДействияНазначитьПраздничным");
			ДействияКнопки.Вставить(1, "НазначитьПраздничным", ТипКнопки, "Назначить этот день праздничным днем", НовоеДействие);
			КонтекстДействияКнопки.Вставить(1, "НазначитьПраздничным", ТипКнопки, "Назначить этот день праздничным днем", НовоеДействие);
			
			НовоеДействие = Новый Действие("ДействияНазначитьПредпраздничным");
			ДействияКнопки.Вставить(2, "НазначитьПредпраздничным", ТипКнопки, "Назначить этот день рабочим предпраздничным днем", НовоеДействие);
			КонтекстДействияКнопки.Вставить(2, "НазначитьПредпраздничным", ТипКнопки, "Назначить этот день рабочим предпраздничным днем", НовоеДействие);
			
		ИначеЕсли ВидДня = ВидДняПредпраздничный Тогда
			
			НовоеДействие = Новый Действие("ДействияНазначитьПраздничным");
			ДействияКнопки.Вставить(0, "НазначитьПраздничным", ТипКнопки, "Назначить этот день праздничным днем", НовоеДействие);
			КонтекстДействияКнопки.Вставить(0, "НазначитьПраздничным", ТипКнопки, "Назначить этот день праздничным днем", НовоеДействие);
			
			НовоеДействие = Новый Действие("ДействияПеренестиРабочийДень");
			ДействияКнопки.Вставить(1, "ПеренестиРабочийДень", ТипКнопки, "Перенести рабочий день", НовоеДействие);
			КонтекстДействияКнопки.Вставить(1, "ПеренестиРабочийДень", ТипКнопки, "Перенести рабочий день", НовоеДействие);
			
			НовоеДействие = Новый Действие("ДействияНазначитьРабочим");
			ДействияКнопки.Вставить(2, "НазначитьРабочим", ТипКнопки, "Назначить рабочим днем", НовоеДействие);
			КонтекстДействияКнопки.Вставить(2, "НазначитьРабочим", ТипКнопки, "Назначить рабочим днем", НовоеДействие);
			
		ИначеЕсли ВидДня = ВидДняПраздник Тогда
			
			ИндексВставки = 0;
			Если ДеньНедели = 6 Тогда
				
				НовоеДействие = Новый Действие("ДействияПеренестиВыходнойДеньСубботу");
				ДействияКнопки.Вставить(0, "ПеренестиВыходнойДень", ТипКнопки, "Перенести выходной день", НовоеДействие);
				КонтекстДействияКнопки.Вставить(0, "ПеренестиВыходнойДень", ТипКнопки, "Перенести выходной день", НовоеДействие);
				ИндексВставки = ИндексВставки + 1;
				
			ИначеЕсли ДеньНедели = 7 Тогда
				
				НовоеДействие = Новый Действие("ДействияПеренестиВыходнойДеньВоскресенье");
				ДействияКнопки.Вставить(0, "ПеренестиВыходнойДень", ТипКнопки, "Перенести выходной день", НовоеДействие);
				КонтекстДействияКнопки.Вставить(0,"ПеренестиВыходнойДень", ТипКнопки, "Перенести выходной день", НовоеДействие);
				ИндексВставки = ИндексВставки + 1;
				
			КонецЕсли;
			
			НовоеДействие = Новый Действие("ДействияНазначитьРабочим");
			ДействияКнопки.Вставить(ИндексВставки, "НазначитьРабочим", ТипКнопки, "Назначить рабочим днем", НовоеДействие);
			КонтекстДействияКнопки.Вставить(ИндексВставки, "НазначитьРабочим", ТипКнопки, "Назначить рабочим днем", НовоеДействие);
			
		ИначеЕсли ВидДня = ВидДняСуббота Тогда
			
			Если ДеньНедели = 6 Тогда
				
				НовоеДействие = Новый Действие("ДействияНазначитьПраздничным");
				ДействияКнопки.Вставить(0, "НазначитьПраздничным", ТипКнопки, "Назначить этот день праздничным днем", НовоеДействие);
				КонтекстДействияКнопки.Вставить(0, "НазначитьПраздничным", ТипКнопки, "Назначить этот день праздничным днем", НовоеДействие);
				
				НовоеДействие = Новый Действие("ДействияПеренестиВыходнойДеньСубботу");
				ДействияКнопки.Вставить(1, "ПеренестиВыходнойДень", ТипКнопки, "Перенести выходной день", НовоеДействие);
				КонтекстДействияКнопки.Вставить(1, "ПеренестиВыходнойДень", ТипКнопки, "Перенести выходной день", НовоеДействие);
				
			КонецЕсли;
			
		ИначеЕсли ВидДня = ВидДняВоскресенье Тогда	
			
			Если ДеньНедели = 7 Тогда
				
				НовоеДействие = Новый Действие("ДействияНазначитьПраздничным");
				ДействияКнопки.Вставить(0, "НазначитьПраздничным", ТипКнопки, "Назначить этот день праздничным днем", НовоеДействие);
				КонтекстДействияКнопки.Вставить(0, "НазначитьПраздничным", ТипКнопки, "Назначить этот день праздничным днем", НовоеДействие);
				
				НовоеДействие = Новый Действие("ДействияПеренестиВыходнойДеньВоскресенье");
				ДействияКнопки.Вставить(1, "ПеренестиВыходнойДень", ТипКнопки, "Перенести выходной день", НовоеДействие);
				КонтекстДействияКнопки.Вставить(1, "ПеренестиВыходнойДень", ТипКнопки, "Перенести выходной день", НовоеДействие);
				
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ИНИЦИАЛИЗАЦИЯ ПЕРЕМЕННЫХ МОДУЛЯ

ЭталоннаяТаблицаРегистра = Новый ТаблицаЗначений();
ЭталоннаяТаблицаРегистра.Колонки.Добавить("ДатаКалендаря");
ЭталоннаяТаблицаРегистра.Колонки.Добавить("ВидДня");

ВидДняРабочий			= Перечисления.УР_ВидыДнейПроизводственногоКалендаря.Рабочий;
ВидДняСуббота			= Перечисления.УР_ВидыДнейПроизводственногоКалендаря.Суббота;
ВидДняВоскресенье		= Перечисления.УР_ВидыДнейПроизводственногоКалендаря.Воскресенье;
ВидДняПраздник			= Перечисления.УР_ВидыДнейПроизводственногоКалендаря.Праздник;
ВидДняПредпраздничный	= Перечисления.УР_ВидыДнейПроизводственногоКалендаря.Предпраздничный;
