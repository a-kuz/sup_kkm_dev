
Перем СтарыйИД;

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(ОписаниеПрофиля) ТОгда
		Предупреждение("Данная обработка открывается только из справочника торгового оборудования!",5);
		Отказ = Истина;
		Возврат;  		
	КонецЕсли;   
	
	СтарыйИД = ИдУстройства;
	
	ЭлементыФормы.тНомерключа.Заголовок = "Номер ключа: "+НомерКлюча;
	
	СписокТипов = ЭлементыФормы.ТестовыйТип.СписокВыбора;
	Для каждого ТипСообщения Из ТипыСообщений Цикл
		СписокТипов.Добавить(ТипСообщения.Ключ, СтрЗаменить(ТипСообщения.Ключ, "ТГ_", "Трактиръ: Гость - "));
	КонецЦикла; 
	ЭлементыФормы.ТестовыйТип.Значение = СписокТипов[0].Значение;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура ОсновныеДействияФормыОсновныеДействияФормыВыполнить(Кнопка)
	
	Если ЗначениеЗаполнено(СтарыйИД) Тогда
		ТО.Наименование = СтрЗаменить(ТО.Наименование, СтарыйИД, ИдУстройства);
	КонецЕсли; 
	
	ЗаполнитьЗначенияСвойств(ПараметрыТО,ЭтотОбъект);
	
	Закрыть("ОК");
	
КонецПроцедуры

Процедура ТестОтправитьНажатие(Элемент)
	
	Параметры = Новый Структура;
	Параметры.Вставить("Заголовок"	,ТестовыйЗаголовок);
	Параметры.Вставить("Текст"		,ТестовыйТекст);
	Параметры.Вставить("Тип"		,ТестовыйТип);
	
	ВыполнитьДействие("ОтправитьСообщение", Параметры);
	
	Если Результат.Ошибка Тогда
		Предупреждение(Результат.Описание + Символы.ПС + Результат.Подробно);
		
	ИначеЕсли ЗначениеЗаполнено(Результат.ТаблицаСтатусовОтправки) Тогда
		Результат.ТаблицаСтатусовОтправки.ВыбратьСтроку("Результат отправки сообщения");
		
	Иначе
		Предупреждение("Сообщение получено!");
		
	КонецЕсли;
	
КонецПроцедуры

Процедура КнопкаАктивироватьЛицензиюНажатие(Элемент)
	
	КодАктивации = "";
	Если НЕ ВвестиСтроку(КодАктивации, "Введите код активации:") Тогда
		Возврат;
	КонецЕсли; 
	
	Параметры = Новый Структура;
	Параметры.Вставить("КодАктивации"	,КодАктивации);
	
	ВыполнитьДействие("АктивироватьЛицензию", Параметры);
	
	Если Результат.Ошибка Тогда
		Предупреждение(Результат.Описание + Символы.ПС + Результат.Подробно);
	Иначе
		Предупреждение("ОК!",3);
	КонецЕсли;
	
КонецПроцедуры

Процедура КнопкаПроверитьЛицензииНажатие(Элемент)
	
	Параметры = Новый Структура;
	ВыполнитьДействие("ПроверитьЛицензии", Параметры);
	
	Если Результат.Ошибка Тогда
		Предупреждение(Результат.Описание + Символы.ПС + Результат.Подробно);
	Иначе
		Результат.ТаблицаЛицензий.ВыбратьСтроку("Действующие лицензии для ключа: "+НомерКлюча);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЦветПоУмолчаниюОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ИнтерфейсАдмина.ПроверитьЦветНаАбсолютный(ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры




