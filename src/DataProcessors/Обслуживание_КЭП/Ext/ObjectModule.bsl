
Перем ПараметрыТО Экспорт;   // Параметры торгового оборудования.
Перем Результат Экспорт;     // Результат выполнения действия.
Перем DRV Экспорт;           // Драйвер
Перем КодОшибки, ОписаниеОшибки;

#Если Клиент Тогда

// Производит инициализацию торгового оборудования.
//
Процедура Инициализация() Экспорт
	
	// это наш драйвер, зашит в компоненту защиты
	DRV = РаботаСокнами;
	
	ПрочитатьПараметр("ИспользоватьСервер"	, Ложь );
	ПрочитатьПараметр("СерверIPадрес"		, "" );
	ПрочитатьПараметр("СерверIPпорт"		, 5555 );
	ПрочитатьПараметр("ПортПодключения"		, 1 );
	
КонецПроцедуры

// Выполняет чтение параметра ТО.
//
// Параметры:
//  ИмяПараметра        - имя параметра,
//  ЗначениеПоУмолчанию - значение по умолчанию для данного параметра.
//
// Возвращаемое значение:
//  Значение параметра или значение по умолчанию
//
Процедура ПрочитатьПараметр(ИмяПараметра,ЗначениеПоУмолчанию)
	
	Если НЕ ПараметрыТО.Свойство(ИмяПараметра) Тогда
		ПараметрыТО.Вставить(ИмяПараметра,ЗначениеПоУмолчанию);
	КонецЕсли; 
	
	ЭтотОбъект[ИмяПараметра] = ПараметрыТО[ИмяПараметра];
	
КонецПроцедуры

// Выполняет действие с ТО.
//
// Параметры:
//  Действие - имя действия,
//  ПараметрыДействия - произвольный набор параметров
//
Процедура ВыполнитьДействие(Действие, ПараметрыДействия=Неопределено) Экспорт
	
	Если Действие = "Подключить" Тогда
		// для проверки связи
		ПолучитьКанал(1);
		
	ИначеЕсли Действие = "Отключить" Тогда
		// ничего делать не надо
		
	ИначеЕсли Действие = "ПоставитьВОчередь" Тогда
		ПоставитьВОчередь( ПараметрыДействия.ИдСобытия, ПараметрыДействия.НомерКанала, ПараметрыДействия.ВремяНач, ПараметрыДействия.ВремяКон);
		
	ИначеЕсли Действие = "ПолучитьКанал" Тогда
		ПолучитьКанал( ПараметрыДействия.НомерКанала );
		
	ИначеЕсли Действие = "УстановитьКанал" Тогда
		УстановитьКанал( ПараметрыДействия.НомерКанала, ПараметрыДействия.СостояниеКанала );
		
	ИначеЕсли Действие = "ОчиститьОчередь" Тогда
		ОчиститьОчередь();
		
	Иначе
		Результат.Ошибка	= Истина;
		Результат.Описание	= "Неизвестная команда!";
		Результат.Подробно	= "Команда """+Действие+""" не определена для "+ТО.Наименование;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаОшибки()
	
	Если КодОшибки=0 Тогда
		Возврат;
	КонецЕсли; 
	
	ЗарегистрироватьСобытие("Торговое оборудование.Ошибка", УровеньЖурналаРегистрации.Ошибка, ТО.Метаданные(), ТО.Ссылка, ОписаниеОшибки);
	
	Результат.Ошибка = Истина;
	Результат.Описание = "Ошибка коммутатора!";
	Результат.Подробно = ОписаниеОшибки;
	
КонецПроцедуры

Процедура Подключить()
	
	Если НЕ ИспользоватьСервер Тогда
		СерверIPадрес = "";
		СерверIPпорт = 0;
	КонецЕсли;
	
	Результат.Ошибка = Ложь;
	
КонецПроцедуры

Процедура ПолучитьКанал(НомерКанала)
	
	Перем СостояниеКанала;
	
	Подключить();
	
	DRV.GetChannel( СерверIPадрес, СерверIPпорт, НомерКанала, ПортПодключения, СостояниеКанала, КодОшибки, ОписаниеОшибки);   
	
	ОбработкаОшибки();
	
	Если Результат.Ошибка Тогда
		Результат.Вставить("СостояниеКанала", Неопределено);
	Иначе
		Результат.Вставить("СостояниеКанала", СостояниеКанала);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьКанал(НомерКанала, СостояниеКанала)
	
	Подключить();
	
	DRV.SetChannel( СерверIPадрес, СерверIPпорт, НомерКанала, ПортПодключения, СостояниеКанала, КодОшибки, ОписаниеОшибки);   
	
	ОбработкаОшибки();
	
КонецПроцедуры

Процедура ПоставитьВОчередь( ИдСобытия, НомерКанала, ВремяНач, ВремяКон)
	
	ТекВремя = ТекущаяДата();
	
	Если НЕ ЗначениеЗаполнено(ВремяНач) Тогда
		ВремяНач = ТекущаяДата();	// время уже не важно, чтобы правильно отформатировалось
		Команда="CANCEL";	// отмена события
		
	ИначеЕсли ЗначениеЗаполнено(ВремяКон) И ВремяКон<ВремяНач Тогда
		Команда="CANCEL";	// отмена события если конец раньше начала
		
	Иначе
		Команда="ON";		// включить
		
		//Если ВремяНач <= ТекВремя И (НЕ ЗначениеЗаполнено(ВремяКон) ИЛИ ВремяКон > ТекВремя) Тогда
		//КонецЕсли; 
		
	КонецЕсли;
	
	Стр = Команда+" "+
		НомерКанала+" "+
		ПортПодключения+" "+
		Формат(	ВремяНач, "ДФ=ЧЧ")+" "+
		Формат(	ВремяНач, "ДФ=мм")+" "+
		Формат(	ВремяНач, "ДФ=сс")+" "+
		Формат(	ВремяНач, "ДФ=дд")+" "+
		Формат(	ВремяНач, "ДФ=ММ")+" "+
		Формат(	ВремяНач, "ДФ=гггг");
	
	Если ЗначениеЗаполнено(ВремяКон) И Команда="ON" Тогда
		Стр = Стр+" "+
		Формат(	ВремяКон, "ДФ=ЧЧ")+" "+
		Формат(	ВремяКон, "ДФ=мм")+" "+
		Формат(	ВремяКон, "ДФ=сс")+" "+
		Формат(	ВремяКон, "ДФ=дд")+" "+
		Формат(	ВремяКон, "ДФ=ММ")+" "+
		Формат(	ВремяКон, "ДФ=гггг");
	КонецЕсли;
	
	Стр = Стр+" "+Формат(ИдСобытия,"ЧГ=");
	
	Подключить();
	
	DRV.AddCmnd( СерверIPадрес, СерверIPпорт, Стр, КодОшибки, ОписаниеОшибки);
	
	ОбработкаОшибки();
	
КонецПроцедуры

Процедура ОчиститьОчередь()
	
	Подключить();
		
	DRV.ClearQueue( СерверIPадрес, СерверIPпорт,  КодОшибки, ОписаниеОшибки);   
	
	ОбработкаОшибки();
	
КонецПроцедуры

#КонецЕсли

Результат = Новый Структура("Ошибка,Описание,Подробно", Ложь,"","");
