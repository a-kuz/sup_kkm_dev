
Перем ПараметрыОкна Экспорт;	// структура, определяет положение и размеры окна

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	ФормаВыбора = ЭтаФорма;
	
	ЗаполнитьТаблицуВыбора();
	
	Заголовок = ЗаголовокФормы;
	ЭлементыФормы.ТаблицаВыбора.СоздатьКолонки();
	ЭлементыФормы.ТаблицаВыбора.Колонки.НаименованиеДляКнопки	.Видимость = Ложь;
	ЭлементыФормы.ТаблицаВыбора.Колонки.Значение				.Видимость = Ложь;
	ЭлементыФормы.ТаблицаВыбора.Колонки.ГоризонтальноеПоложение	.Видимость = Ложь;
	
	Если ТаблицаВыбора.Колонки.Количество()=2 Тогда
		ЭлементыФормы.ТаблицаВыбора.Шапка = Ложь;
	КонецЕсли; 
	
	Если МножественныйПодбор Тогда 
		ЭлементыФормы.ТаблицаВыбора.Колонки.Пиктограмма.КартинкиСтрок = БиблиотекаКартинок.Галка;
		ЭлементыФормы.ТаблицаВыбора.Колонки.Пиктограмма.ТекстШапки = "";
		// кнопка ОК нужна, стрелки посередине
		ЭлементыФормы.КнопкаОК.Видимость = Истина;
		ЭлементыФормы.ПанельСтрелки.Лево = ЭтаФорма.Ширина/2 - ЭлементыФормы.ПанельСтрелки.Ширина/2;
	Иначе
		// кнопка ОК не нужна, стрелки прижимаем к правому краю
		ЭлементыФормы.КнопкаОК.Видимость = Ложь;
		ЭлементыФормы.ПанельСтрелки.Лево = ЭтаФорма.Ширина - ЭлементыФормы.ПанельСтрелки.Ширина - 4;
	КонецЕсли; 
	
	// вызов должен быть в конце обработчика
	ИнтерфейсРМ.ПриОткрытииОкна(ЭтаФорма);
	
КонецПроцедуры

Процедура ПриЗакрытии()
	
	ИнтерфейсРМ.ПриЗакрытииОкна();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура ТаблицаВыбораВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
 	
	ОбработкаВыбораЗначения( ВыбраннаяСтрока.Значение );
	
КонецПроцедуры

Процедура КнопкаВыбратьНажатие(Элемент)
	
	ВыбраннаяСтрока = ЭлементыФормы.ТаблицаВыбора.ТекущаяСтрока;
	Если ВыбраннаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ОбработкаВыбораЗначения( ВыбраннаяСтрока.Значение );
	
КонецПроцедуры

Процедура КнопкаСтрелкаВверхНажатие(Элемент)
	
	ИнтерфейсРМ.ПереместитьКурсорВТаблице(ЭлементыФормы.ТаблицаВыбора, -1);
	
КонецПроцедуры

Процедура КнопкаСтрелкаВнизНажатие(Элемент)
	
	ИнтерфейсРМ.ПереместитьКурсорВТаблице(ЭлементыФормы.ТаблицаВыбора, 1);
	
КонецПроцедуры

Процедура ТаблицаВыбораПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	Если МножественныйПодбор Тогда
		ОформлениеСтроки.Ячейки.Пиктограмма.ОтображатьТекст  = Ложь;
		
		Если МассивПомеченных.Найти(ДанныеСтроки.Значение) = Неопределено Тогда 
			ОформлениеСтроки.Ячейки.Пиктограмма.ОтображатьКартинку = Ложь;
			ОформлениеСтроки.Шрифт = Новый Шрифт(,10,Ложь);			// не жирный
		Иначе 
			ОформлениеСтроки.Ячейки.Пиктограмма.ОтображатьКартинку = Истина;
			ОформлениеСтроки.Ячейки.Пиктограмма.ИндексКартинки = 0;
			ОформлениеСтроки.Шрифт = Новый Шрифт(,10,Истина);			// жирный
		КонецЕсли;
	КонецЕсли;
	
	ВысотаЯчейки =  СтрЧислоСтрок(ДанныеСтроки.Наименование);
	ОформлениеСтроки.Ячейки.Наименование.ВысотаЯчейки = ВысотаЯчейки;
	
КонецПроцедуры

Процедура ОК(Элемент)
	
	Закрыть(МассивПомеченных);
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ТЕЛО МОДУЛЯ

ПараметрыОкна = Новый Структура("Центр, Лево, Верх, Ширина, Высота", Истина);
