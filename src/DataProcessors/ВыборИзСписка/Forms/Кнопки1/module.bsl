
Перем ПараметрыОкна Экспорт;	// структура, определяет положение и размеры окна

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	ФормаВыбора = ЭтаФорма;
	
	ЗаполнитьТаблицуВыбора();
	
	Заголовок = ЗаголовокФормы;
	
	ВывестиТекущуюСтраницу();
	
	// для двухстрочных наименований надо уменьшить шрифт
	Если СтрЧислоСтрок( ТаблицаВыбора[0].НаименованиеДляКнопки ) = 2 Тогда
		Для НомерКнопки=1 По КолвоКнопок Цикл
			Кнопка = ЭлементыФормы["Кнопка"+НомерКнопки];
			Кнопка.Шрифт = ШрифтыСтиля.Шрифт7;
		КонецЦикла;
	КонецЕсли; 
	
	ЭлементыФормы.КнопкаОК.Видимость = МножественныйПодбор;
	
	// вызов должен быть в конце обработчика
	ИнтерфейсРМ.ПриОткрытииОкна(ЭтаФорма);
	
КонецПроцедуры

Процедура ПриЗакрытии()
	
	ИнтерфейсРМ.ПриЗакрытииОкна();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура КнопкаСтрелкаВверхНажатие(Элемент)
	
	ТекСтраница = Макс(1, ТекСтраница - 1);
	ВывестиТекущуюСтраницу();
	
КонецПроцедуры

Процедура КнопкаСтрелкаВнизНажатие(Элемент)
	
	ТекСтраница = Мин(ВсегоСтраниц, ТекСтраница + 1);
	ВывестиТекущуюСтраницу();
	
КонецПроцедуры

Процедура КнопкаВыбораНажатие(Элемент)
	
	ИндексЗначенияПервойКнопки = (ТекСтраница-1) * КолвоКнопок;
	НомерКнопки = Число(Сред(Элемент.Имя,7));
	
	ЗначениеВыбора = ТаблицаВыбора[ИндексЗначенияПервойКнопки + НомерКнопки-1].Значение;
	
	ОбработкаВыбораЗначения( ЗначениеВыбора );
	
	Если МножественныйПодбор Тогда
		Элемент.Картинка = ?(МассивПомеченных.Найти(ЗначениеВыбора), БиблиотекаКартинок.Галка, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОК(Элемент)
	
	Закрыть(МассивПомеченных);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ТЕЛО МОДУЛЯ

ПараметрыОкна = Новый Структура("Центр, Лево, Верх, Ширина, Высота", Истина);

КолвоКнопок = 9;
