
Перем ПараметрыОкна Экспорт;	// структура, определяет положение и размеры окна

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	// установим отбор по сотрудникам, имеющим права доступа в текущем месте реализации
	СписокСотрудников = Новый СписокЗначений;
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ Ссылка
	                      |ИЗ
	                      |	Справочник.Сотрудники.ПраваДоступа
	                      |ГДЕ
	                      |	НЕ Ссылка.ПометкаУдаления
	                      |	И МестоРеализации = &МестоРеализации
	                      |	И НаборПрав <> ЗНАЧЕНИЕ(Справочник.НаборыПравДоступа.ПустаяСсылка)
						  |");
						  
	Запрос.УстановитьПараметр("МестоРеализации", глПараметрыРМ.МестоРеализации);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СписокСотрудников.Добавить(Выборка.Ссылка);
	КонецЦикла; 
	
	Сотрудники.Отбор.Ссылка.Использование	= Истина;
	Сотрудники.Отбор.Ссылка.ВидСравнения	= ВидСравнения.ВСписке;
	Сотрудники.Отбор.Ссылка.Значение		= СписокСотрудников;
	
	Сотрудники.Отбор.ПометкаУдаления.Установить(Ложь);
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	ИнтерфейсРМ.ПриОткрытииОкна(ЭтаФорма);
	
КонецПроцедуры

Процедура ПриЗакрытии()
	
	ИнтерфейсРМ.ПриЗакрытииОкна();
	
КонецПроцедуры

Процедура ОбновлениеОтображения()
	
	глОтсечкаПростоя();
	ТекущийЭлемент = ЭлементыФормы.Сотрудники;
	
	ЭлементыФормы.тЗаголовок.Заголовок = "Выбрано сотрудников: "+Формат(ТаблицаСотрудников.Количество(),"ЧН=");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура КнопкаВыбратьНажатие(Элемент)
	
	ВыбСотрудник = ЭлементыФормы.Сотрудники.ТекущаяСтрока;
	
	Если ВыбСотрудник.ЭтоГруппа Тогда
		ТекущийРодитель = ВыбСотрудник;
	Иначе
		ОбработкаВыбораСотрудника(ВыбСотрудник);
	КонецЕсли; 
	
КонецПроцедуры

Процедура КнопкаУровеньВверхНажатие(Элемент)
	
	ТекущийЭлемент = ЭлементыФормы.Сотрудники;
	WshShell.SendKeys("^{UP}");
	
КонецПроцедуры

Процедура КнопкаСтрелкаВверхНажатие(Элемент)
	
	ТекущийЭлемент = ЭлементыФормы.Сотрудники;
	WshShell.SendKeys("{UP}");
	
КонецПроцедуры

Процедура КнопкаСтрелкаВнизНажатие(Элемент)
	
	ТекущийЭлемент = ЭлементыФормы.Сотрудники;
	WshShell.SendKeys("{DOWN}");
	
КонецПроцедуры

Процедура КнопкаУровеньВнизНажатие(Элемент)
	
	ТекущийЭлемент = ЭлементыФормы.Сотрудники;
	WshShell.SendKeys("^{DOWN}");
	
КонецПроцедуры

Процедура СотрудникиВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	Если НЕ ВыбраннаяСтрока.ЭтоГруппа Тогда
		СтандартнаяОбработка = Ложь;
		ОбработкаВыбораСотрудника(ВыбраннаяСтрока);
	КонецЕсли;
	
КонецПроцедуры

Процедура СотрудникиПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	
	ОформлениеСтроки.Ячейки.Пиктограмма.ОтображатьТекст  = Ложь;
	
	Если ТаблицаСотрудников.Найти(ДанныеСтроки.Ссылка,"Сотрудник") = Неопределено Тогда
		ОформлениеСтроки.Ячейки.Пиктограмма.ОтображатьКартинку = Ложь;
		ОформлениеСтроки.Шрифт = Новый Шрифт(,10,Ложь);			// не жирный
	Иначе
		ОформлениеСтроки.Ячейки.Пиктограмма.ОтображатьКартинку = Истина;
		ОформлениеСтроки.Ячейки.Пиктограмма.ИндексКартинки = 0;
		ОформлениеСтроки.Шрифт = Новый Шрифт(,10,Истина);			// жирный
	КонецЕсли; 
	
	СтрокаТЧПраваДоступа = ДанныеСтроки.Ссылка.ПраваДоступа.Найти(глПараметрыРМ.МестоРеализации, "МестоРеализации");
	
	ОформлениеСтроки.Ячейки.НаборПрав.ОтображатьТекст  = Истина;
	ОформлениеСтроки.Ячейки.НаборПрав.Текст = ?(СтрокаТЧПраваДоступа = Неопределено, "", СтрокаТЧПраваДоступа.НаборПрав.Наименование);
	
КонецПроцедуры



////////////////////////////////////////////////////////////////////////////////
// ТЕЛО МОДУЛЯ

ПараметрыОкна = Новый Структура("Центр, Лево, Верх, Ширина, Высота", Истина,,,,);
