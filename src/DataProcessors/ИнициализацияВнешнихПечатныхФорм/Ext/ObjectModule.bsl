//Обработка выполняет инициализацию печатных форм документов.
//
//Создаёт или перезаписывает элементы справочника "Внешние обработки" с указанием 
//области действия печатной формы и собственно самой обработки печатной формы.
//
//Чтобы добавить новую печатную форму, надо добавить макет с типом "Двоичные данные", 
//в который поместить обработку печати нужного объекта.
//
//В синоним макета необходимо записать наименование печатной формы.
//В комментарий область действия в формате:
//<ВидОбъектаМетаданных>___<ИмяОбъектаМетаданных> [,<ВидОбъектаМетаданных>___<ИмяОбъектаМетаданных>]
//
//Например, Документ___ПриходнаяНакладная, Документ___ПеремещениеТоваров
//
Процедура ИнициализацияПечатныхФорм() Экспорт
	
	Сообщить("Производится инициализация внешних обработок...");
	
	Для каждого Макет Из ЭтотОбъект.Метаданные().Макеты Цикл
		
		Попытка
			
			СтрокаПараметров = Макет.Комментарий;
			НаименовениеОбработки = Макет.Синоним;
			
			СтруктураПараметров = Новый Структура(СтрокаПараметров);
			СправочникСсылка = Справочники.ВнешниеОбработки.НайтиПоНаименованию(НаименовениеОбработки, Истина);
			
			СлужебноеСообщение = "";
			Если СправочникСсылка.Пустая() Тогда
				СправочникОбъект = Справочники.ВнешниеОбработки.СоздатьЭлемент();
				СлужебноеСообщение = "Создан новый элемент справочника ""Внешние обработки"": " + НаименовениеОбработки;
			Иначе
				СправочникОбъект = СправочникСсылка.ПолучитьОбъект();
				СлужебноеСообщение = "Обновлен элемент справочника ""Внешние обработки"": " + НаименовениеОбработки;
			КонецЕсли;
			
			СправочникОбъект.ХранилищеОбработки = Новый ХранилищеЗначения(ЭтотОбъект.ПолучитьМакет(Макет.Имя));
			СправочникОбъект.ВидОбработки = Перечисления.ВидыОбработок.ПечатнаяФорма;
			СправочникОбъект.ПутьДоОбработки = "";
			СправочникОбъект.Наименование = НаименовениеОбработки;
			СправочникОбъект.ПометкаУдаления = Ложь;
			СправочникОбъект.ОбластьДействияОбработки.Очистить();
			
			СлужебноеСообщение = СлужебноеСообщение + ", область действия: ";
			Для каждого ЭлементСтруктуры Из СтруктураПараметров Цикл
				КоличествоСимволов = Метаданные.НайтиПоТипу(ТипЗнч(СправочникОбъект.ОбластьДействияОбработки)).Реквизиты.ИмяМетаданных.Тип.КвалификаторыСтроки.Длина;
				ПредставлениеОбъектаМетаданных = СтрЗаменить(ЭлементСтруктуры.Ключ, "___", ".");
				
				Если СтрДлина(ПредставлениеОбъектаМетаданных) > КоличествоСимволов Тогда
					Сообщить("Представление объекта метаданных должно не должно быть длиннее " + КоличествоСимволов + " символов!", СтатусСообщения.Важное);
					Сообщить("Объект """ + ПредставлениеОбъектаМетаданных + """ не добавлен в область действия обработки", СтатусСообщения.Важное);
					Продолжить;
				КонецЕсли;
				
				СлужебноеСообщение = СлужебноеСообщение + """" + СтрЗаменить(ПредставлениеОбъектаМетаданных, ".", ": ") + """" + ", ";
				НоваяСтрока = СправочникОбъект.ОбластьДействияОбработки.Добавить();
				НоваяСтрока.ИмяМетаданных = ПредставлениеОбъектаМетаданных;
				
			КонецЦикла;
			
			СправочникОбъект.Записать();
			Сообщить(Лев(СлужебноеСообщение, СтрДлина(СлужебноеСообщение) - 2) + ".");
		Исключение
			Сообщить(ОписаниеОшибки());
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры
