
Перем ПараметрыОкна Экспорт;	// структура, определяет положение и размеры окна

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	СписокДействияПередОткрытием(ЭтаФорма);
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	Если глВерсия=1 ИЛИ ВызовИзСтопЛиста Тогда
		ЭлементыФормы.КнопкаСтопЛист.Видимость = Ложь;
	КонецЕсли; 
	
	Если РежимНастройки Тогда
		Ширина = ПараметрыНастройки.ШиринаОкна - 6;
		Высота = ПараметрыНастройки.ВысотаОкна - 25;
		Возврат;
	КонецЕсли;
	
	ПараметрыОкна = ПолучитьПараметрыОкна();
	
	// вызов должен быть в конце обработчика
	ИнтерфейсРМ.ПриОткрытииОкна(ЭтаФорма);
	
КонецПроцедуры

Процедура ПриЗакрытии()
	
	Если РежимНастройки Тогда
		
	Иначе
		ВладелецФормы.ПриЗакрытииМеню();	// это обязательно сначала
		ИнтерфейсРМ.ПриЗакрытииОкна();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновлениеОтображения()
	
	Если НЕ РежимНастройки Тогда
		глОтсечкаПростоя();
	КонецЕсли;
	
	ТекущийЭлемент = ЭлементыФормы.Товары;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура ТоварыПриПолученииДанных(Элемент, ОформленияСтрок)
	
	Для каждого Строка Из ОформленияСтрок Цикл
		
		Если Строка.ДанныеСтроки.ЭтоГруппа Тогда
			Строка.Ячейки.ЕдиницаИзмерения.ОтображатьТекст = Ложь;
			
		ИначеЕсли ПараметрыНастройки.СписокПоказЦены Тогда
			ЦенаТов = ЦенаТовара(Строка.ДанныеСтроки.Ссылка, ТипЦен);
			Строка.Ячейки.Цена.Текст = ФорматСумм(ЦенаТов.Цена);
			Строка.Ячейки.Цена.ОтображатьТекст = Истина;
			
		КонецЕсли; 
		
	КонецЦикла; 
	
КонецПроцедуры

Процедура ТоварыВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СписокОбработкаВыбораТовара();
	
КонецПроцедуры

Процедура КнопкаВыбратьНажатие(Элемент)
	
	СписокОбработкаВыбораТовара();
	
КонецПроцедуры

Процедура КнопкаМенюНажатие(Элемент)
	
	ВыборМеню();
	
КонецПроцедуры

Процедура КнопкаПодсказкаНажатие(Элемент)
	
	Если РежимНастройки Тогда
		Возврат;
	КонецЕсли;
	
	Товар = ЭлементыФормы.Товары.ТекущаяСтрока;
	
	Если Товар <> Неопределено И НЕ Товар.Пустая() И НЕ Товар.ЭтоГруппа Тогда
		ИнтерфейсРМ.Подсказка(Товар.Наименование, Товар.ДопИнформация);
	КонецЕсли;
	
КонецПроцедуры

Процедура КнопкаУровеньВверхНажатие(Элемент)
	
	ТекущийЭлемент = ЭлементыФормы.Товары;
	//РаботаСокнами.SendKeyMsg(2, 38, 1);	// Ctrl + Стрелка вверх
	WshShell.SendKeys("^{UP}");
	
КонецПроцедуры

Процедура КнопкаСтрелкаВверхНажатие(Элемент)
	
	ТекущийЭлемент = ЭлементыФормы.Товары;
	//РаботаСокнами.SendKeyMsg(0, 38, 1);	// Стрелка вверх
	WshShell.SendKeys("{UP}");
	
КонецПроцедуры

Процедура КнопкаСтрелкаВнизНажатие(Элемент)
	
	ТекущийЭлемент = ЭлементыФормы.Товары;
	//РаботаСокнами.SendKeyMsg(0, 40, 1);	// Стрелка вниз
	WshShell.SendKeys("{DOWN}");
	
КонецПроцедуры

Процедура КнопкаУровеньВнизНажатие(Элемент)
	
	ТекущийЭлемент = ЭлементыФормы.Товары;
	//РаботаСокнами.SendKeyMsg(2, 40, 1);	// Ctrl + Стрелка вверх
	WshShell.SendKeys("^{DOWN}");
	
КонецПроцедуры

Процедура КнопкаСтопЛистНажатие(Элемент)
	
	Если РежимНастройки Тогда
		Возврат;
	КонецЕсли;
	
	Закрыть();
	
	СтопЛист = ИнтерфейсРМ.ПолучитьОбъектОбработки("СтопЛист").ПолучитьФорму("Форма" + глПараметрыРМ.ИнтерфейсТип);
	СтопЛист.ВызовИзМеню = Истина;
	СтопЛист.Открыть();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ТЕЛО МОДУЛЯ
