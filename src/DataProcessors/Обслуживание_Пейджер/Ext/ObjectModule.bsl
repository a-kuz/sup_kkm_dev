#Если Клиент Тогда

Перем ПараметрыТО Экспорт;   // Параметры торгового оборудования.
Перем Результат Экспорт;     // Результат выполнения действия.
Перем DRV Экспорт;           // Объект драйвера торгового оборудования.

// Производит инициализацию торгового оборудования.
//
Процедура Инициализация() Экспорт
	
	Если НЕ ЗагрузитьДрайвер() Тогда
		Результат.Ошибка	= Истина;
		Результат.Описание	= "Ошибка загрузки драйвера!";
		Результат.Подробно	= "Программе не удалось загрузить драйвер весов...";
		Возврат;
	КонецЕсли; 
	
	ПрочитатьПараметр("PortNumber"	       , 1);
	ПрочитатьПараметр("BaudRate"	       , 9600);
	ПрочитатьПараметр("CapCode"		       , 0);
		
КонецПроцедуры
	
// Загружает драйвер ТО.
//
// Возвращаемое значение:
//  Истина - драйвер загружен, ложь - нет.
//
Функция ЗагрузитьДрайвер()
	
	// драйвер встроенный
	DRV = РаботаСокнами;
	
	Возврат Истина;
КонецФункции

// Выполняет чтение параметра ТО.
//
// Параметры:
//  ИмяПараметра        - имя параметра,
//  ЗначениеПоУмолчанию - значение по умолчанию для данного параметра.
//
// Возвращаемое значение:
//  Значение параметра или значение по умолчанию
//
Процедура ПрочитатьПараметр(ИмяПараметра,ЗначениеПоУмолчанию)
	
	Если НЕ ПараметрыТО.Свойство(ИмяПараметра) Тогда
		ПараметрыТО.Вставить(ИмяПараметра,ЗначениеПоУмолчанию);
	КонецЕсли; 
	
	ЭтотОбъект[ИмяПараметра] = ПараметрыТО[ИмяПараметра];
	
КонецПроцедуры

// Выполняет действие с ТО.
//
// Параметры:
//  Действие - имя действия,
//  ПараметрыДействия - произвольный набор параметров
//
Процедура ВыполнитьДействие(Действие,ПараметрыДействия) Экспорт
	
	Если Действие = "Подключить" Тогда
		//Подключить(); ничего не надо
				
	ИначеЕсли Действие = "Отключить" Тогда
		//Отключить(); ничего не надо
		
	ИначеЕсли Действие = "ОтправитьСообщение" Тогда
		ОтправитьСообщение(ПараметрыДействия.Текст);
				
	Иначе
		Результат.Ошибка	= Истина;
		Результат.Описание	= "Неизвестная команда!";
		Результат.Подробно	= "Команда """+Действие+""" не определена для "+ТО.Наименование;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОтправитьСообщение(ТекстСообщения) Экспорт
	
	ResultCode			= DRV.ApolloMessage(PortNumber, BaudRate, CapCode, ТекстСообщения);
	ResultDescription	= DRV.ApolloErrorMessage();
	
	Если ResultCode <> 0 Тогда
		ЗаписьЖурналаРегистрации("Торговое оборудование.Ошибка", УровеньЖурналаРегистрации.Предупреждение, , ТО.Ссылка,
								Строка(ResultCode)+" - "+ResultDescription);
		
		Результат.Ошибка	= Истина;
		Результат.Описание	= "Ошибка пейджера: "+ResultCode;
		Результат.Подробно	= СокрЛП(ResultDescription);
	КонецЕсли;
	
КонецПроцедуры

Результат = Новый Структура("Ошибка,Описание,Подробно",Ложь,"","");

#КонецЕсли
