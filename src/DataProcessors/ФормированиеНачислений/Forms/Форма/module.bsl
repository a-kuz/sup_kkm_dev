////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ МОДУЛЯ

Процедура УправлениеВидимостью()
	ВидимостьКонтрагента = ОбъектЗаполнения = "Контрагент";
	ЭлементыФормы.НадписьКонтрагент.Видимость = ВидимостьКонтрагента;
	ЭлементыФормы.Контрагент.Видимость        = ВидимостьКонтрагента;
	
	ВидимостьНомера = ОбъектЗаполнения = "Номера";
	ЭлементыФормы.НадписьНомера.Видимость = ВидимостьНомера;
	ЭлементыФормы.НомерС.Видимость 		  = ВидимостьНомера;
	ЭлементыФормы.НадписьПо.Видимость     = ВидимостьНомера;
	ЭлементыФормы.НомерПо.Видимость		  = ВидимостьНомера;
	
	ВидимостьСумма = Не (ОбъектЗаполнения = "Минус" ИЛИ ОбъектЗаполнения = "Плюс");
	ЭлементыФормы.Надпись1.Видимость     = ВидимостьСумма;
	ЭлементыФормы.СуммаНаКарту.Видимость = ВидимостьСумма;
КонецПроцедуры

Процедура СформироватьКомментарий()
	ЭлементыФормы.Всего.Заголовок = "Всего начислено (удержано) на сумму: " + Клиенты.Итог("Сумма"); 	
КонецПроцедуры

Функция ПолучитьОстатокПоКлиенту(Момент, Клиент)
	
	Отбор = Новый Структура;
	Отбор.Вставить("Клиент", Клиент);
	Возврат 0;	
КонецФункции	

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	ОбъектЗаполнения = ЭлементыФормы.ОбъектЗаполнения.СписокВыбора.НайтиПоЗначению("Контрагент").Значение;
	Дата = ТекущаяДата();
	УправлениеВидимостью();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура ОбъектЗаполненияПриИзменении(Элемент)
	УправлениеВидимостью();
КонецПроцедуры

Процедура ЗаполнитьНажатие(Элемент)
	
	Если Клиенты.Количество() > 0 Тогда
		Ответ = Вопрос("Текущий список клиентов будет очищен!", РежимДиалогаВопрос.ОКОтмена);
		Если Ответ = КодВозвратаДиалога.Отмена Тогда
			Возврат;
		Иначе
			Клиенты.Очистить();
		КонецЕсли;	
	КонецЕсли;
	
	Если ОбъектЗаполнения = "Контрагент" Тогда
		Если Не ЗначениеЗаполнено(Контрагент) Тогда
			Сообщить("Не выбран контрагент");
			Возврат;
		КонецЕсли;	
		
		Запрос = Новый Запрос("ВЫБРАТЬ
		|	Клиенты.Ссылка
		|ИЗ
		|	Справочник.Клиенты КАК Клиенты
		|ГДЕ
		|	НЕ Клиенты.ПометкаУдаления
		|	И Клиенты.Безнал
		|	И Клиенты.Контрагент = &Контрагент
		|"); 
		Запрос.УстановитьПараметр("Контрагент", Контрагент);
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Стр = Клиенты.Добавить();
			Стр.Клиент  = Выборка.Ссылка;
			Стр.Карта   = КартаПоПривязке(Выборка.Ссылка).Код;
			Стр.Остаток = ПолучитьОстатокПоКлиенту(Дата, Выборка.Ссылка);
			Стр.Сумма   = СуммаНаКарту;
		КонецЦикла;	
		
	ИначеЕсли ОбъектЗаполнения = "Номера" Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ
		|	КартыДоступа.Привязка КАК Ссылка,
		|	КартыДоступа.Код
		|ИЗ
		|	Справочник.КартыДоступа КАК КартыДоступа
		|ГДЕ
		|	НЕ ПометкаУдаления
		|	И Код МЕЖДУ &Код1 И &Код2
		|");
		
		Запрос.УстановитьПараметр("Код1", НомерС);
		Запрос.УстановитьПараметр("Код2", НомерПо);
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Если ТипЗнч(Выборка.Ссылка) = Тип("СправочникСсылка.Клиенты") И Выборка.Ссылка.Безнал Тогда
				Стр = Клиенты.Добавить();
				Стр.Клиент  = Выборка.Ссылка;
				Стр.Карта   = Выборка.Код;
				Стр.Остаток = ПолучитьОстатокПоКлиенту(Дата, Выборка.Ссылка);
				Стр.Сумма   = СуммаНаКарту;
			КонецЕсли;
		КонецЦикла;	
		
	ИначеЕсли ОбъектЗаполнения = "Минус" Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ
		|	НакопленияКлиентовОстатки.Клиент,
		|	НакопленияКлиентовОстатки.СуммаБезналОстаток,
		|	КартыДоступа.Код
		|ИЗ
		|	РегистрНакопления.НакопленияКлиентов.Остатки(&Дата, ) КАК НакопленияКлиентовОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КартыДоступа КАК КартыДоступа
		|		ПО НакопленияКлиентовОстатки.Клиент = КартыДоступа.Привязка
		|ГДЕ
		|	НакопленияКлиентовОстатки.СуммаБезналОстаток < 0");
		Запрос.УстановитьПараметр("Дата",Дата);
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Стр = Клиенты.Добавить();
			Стр.Клиент  = Выборка.Клиент;
			Стр.Карта   = Выборка.Код;
			Стр.Остаток = Выборка.СуммаБезналОстаток;
			Стр.Сумма   = - Выборка.СуммаБезналОстаток;
		КонецЦикла;	
		
	ИначеЕсли ОбъектЗаполнения = "Плюс" Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ
		|	НакопленияКлиентовОстатки.Клиент,
		|	НакопленияКлиентовОстатки.СуммаБезналОстаток,
		|	КартыДоступа.Код
		|ИЗ
		|	РегистрНакопления.НакопленияКлиентов.Остатки(&Дата, ) КАК НакопленияКлиентовОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КартыДоступа КАК КартыДоступа
		|		ПО НакопленияКлиентовОстатки.Клиент = КартыДоступа.Привязка
		|ГДЕ
		|	НакопленияКлиентовОстатки.СуммаБезналОстаток > 0");
		Запрос.УстановитьПараметр("Дата",Дата);
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Стр = Клиенты.Добавить();
			Стр.Клиент  = Выборка.Клиент;
			Стр.Карта   = Выборка.Код;
			Стр.Остаток = Выборка.СуммаБезналОстаток;
			Стр.Сумма   = - Выборка.СуммаБезналОстаток;
		КонецЦикла;
	КонецЕсли;	
	
	СформироватьКомментарий();	
КонецПроцедуры

Процедура КлиентыСуммаПриИзменении(Элемент)
	СформироватьКомментарий(); 
КонецПроцедуры

Процедура КлиентыКлиентПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Элемент.Значение) Тогда
		Стр = ЭлементыФормы.Клиенты.ТекущиеДанные;
		Стр.Карта = КартаПоПривязке(Элемент.Значение).Код;
		Стр.Остаток = ПолучитьОстатокПоКлиенту(Дата, Элемент.Значение);
	КонецЕсли;
КонецПроцедуры

Процедура КлиентыПослеУдаления(Элемент)
	СформироватьКомментарий();
КонецПроцедуры

Процедура ДатаПриИзменении(Элемент)
	Для Каждого Стр Из Клиенты Цикл
		Стр.Остаток = ПолучитьОстатокПоКлиенту(Дата, Стр.Клиент);	
	КонецЦикла;	
	
КонецПроцедуры

Процедура КнопкаВыполнитьНажатие(Кнопка)
	
	Если Клиенты.Количество() = 0 Тогда
		Сообщить("Не указан ни один клиент!");
		Возврат;
	КонецЕсли;	
	
	Для Каждого Стр Из Клиенты Цикл
		Если  Не ЗначениеЗаполнено(Стр.Клиент) Тогда
			Сообщить("Не указан клиент. Строка " + (Клиенты.Индекс(Стр) + 1));
			Возврат;		
		КонецЕсли;
		
		Если Стр.Сумма = 0 Тогда
			Сообщить("Не указана сумма. Строка " + (Клиенты.Индекс(Стр) + 1));
			Возврат;
		КонецЕсли;	
	КонецЦикла;	
	
	Для Каждого Стр Из Клиенты Цикл
		Сообщить("Обрабатывается элемент - " + Стр.Клиент);
		Документ = Документы.НачислениеБезнал.СоздатьДокумент();
		
		Документ.Дата = Дата;           
		Документ.Клиент = Стр.Клиент;
		Документ.Сумма = Стр.Сумма;
		
		Попытка
			Документ.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			Сообщить("Не удалось выполнить обработку: " + ОписаниеОшибки());
		КонецПопытки;		
	КонецЦикла;	
	
	Сообщить("Обработка завершена!");
КонецПроцедуры



////////////////////////////////////////////////////////////////////////////////
// ТЕЛО МОДУЛЯ

ЭлементыФормы.ОбъектЗаполнения.СписокВыбора.Добавить("Контрагент", "По контрагенту");
ЭлементыФормы.ОбъектЗаполнения.СписокВыбора.Добавить("Номера"    , "По диапазону номеров карт");
ЭлементыФормы.ОбъектЗаполнения.СписокВыбора.Добавить("Минус"     , "Обнуление задолженности клиентов");
ЭлементыФормы.ОбъектЗаполнения.СписокВыбора.Добавить("Плюс"      , "Обнуление задолженности перед клиентами");