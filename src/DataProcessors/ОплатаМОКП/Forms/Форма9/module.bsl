
Процедура УстановитьДоступностьКнопки(ИмяКнопки, Доступность, Акцент = 0) Экспорт
	Кнопка = ЭлементыФормы[ИмяКнопки];
	МассивТеней = Массив(ЭлементыФормы[ИмяКнопки+"Тень"]);
	Тень2 = ЭлементыФормы.Найти(ИмяКнопки+"Тень2");
	Если Не Тень2 = Неопределено Тогда
	
		МассивТеней.Добавить(Тень2);
	
	КонецЕсли; 
	
	ЦветКнопки = ?(Акцент, Метаданные.ЭлементыСтиля.Акцент.Значение, Метаданные.ЭлементыСтиля.ЦветТемы.Значение);
	ЦветФонаНеактивнойКнопки = Метаданные.ЭлементыСтиля.НеактивнаяКнопка.Значение;
	ЦветТекстаНеактивнойКнопки = Метаданные.ЭлементыСтиля.НеактивныйЭлемент.Значение;
	
	Кнопка.Доступность = Доступность;
	Кнопка.ЦветФонаКнопки 	=  ?(Доступность, ЦветКнопки, ЦветФонаНеактивнойКнопки);
	Кнопка.ЦветТекстаКнопки =  ?(Доступность, WebЦвета.Белый, 	ЦветТекстаНеактивнойКнопки);
	Кнопка.ЦветРамки = Кнопка.ЦветФонаКнопки;
	
	Для каждого Тень Из МассивТеней Цикл
		
		Тень.Видимость = Доступность;
		
	КонецЦикла; 
	
КонецПроцедуры

Процедура ПриИзмененииСуммы()
	
	Если Оплачено Тогда
		НадписьСдача = "-";
		Если Коплате > 0 Тогда
			НадписьКоплате = Формат(Коплате, "ЧДЦ=2; ЧРД=.") + " " + глСимволРубля;
		Иначе
			НадписьКоплате = "-";
		КонецЕсли;
		НадписьПолучено = НадписьКоплате;
	Иначе
		Если Макс(Получено - Коплате, 0) > 0 Тогда
			НадписьСдача = Формат(Макс(Получено - Коплате, 0), "ЧДЦ=2; ЧРД=.") + " " + глСимволРубля;	
		Иначе
			НадписьСдача = "-";
		КонецЕсли;
		
		Если Макс(Коплате - Получено, 0) > 0 Тогда
			НадписьКоплате = Формат(Макс(Коплате - Получено, 0), "ЧДЦ=2; ЧРД=.") + " " + глСимволРубля;
		Иначе
			НадписьКоплате = "-";
		КонецЕсли;
		
	КонецЕсли;
	
	Если Получено Или (Не Коплате И Не Оплачено) Тогда
		Если Оплачено Тогда
			УстановитьДоступностьОплатыКартой(Истина);
		Иначе 
			УстановитьДоступностьОплатыКартой(Ложь);
		КонецЕсли; 

		Если Получено >= Коплате Тогда    
			УстановитьДоступностьКнопки("ОК", Истина);
		КонецЕсли;
	Иначе
		Если Оплачено Тогда
			УстановитьДоступностьКнопки("ОК", Истина);
		Иначе 
			УстановитьДоступностьКнопки("ОК", Ложь);
		КонецЕсли;                                  		
		
		УстановитьДоступностьОплатыКартой(Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьДоступностьОплатыКартой(Доступность)
	ЭлементыФормы.ОплатаКартойТень.Видимость = Доступность;
	ЭлементыФормы.ОплатаКартой.Доступность = Доступность;
	Если Доступность Тогда
		ЭлементыФормы.ОплатаКартой.ЦветФонаКнопки = Метаданные.ЭлементыСтиля.ЦветТемы.Значение;
		ЭлементыФормы.ОплатаКартой.ЦветРамки = Метаданные.ЭлементыСтиля.ЦветТемы.Значение;
		ЭлементыФормы.ОплатаКартой.ЦветТекстаКнопки = WebЦвета.Белый;
	Иначе
		ЭлементыФормы.ОплатаКартой.ЦветФонаКнопки = Метаданные.ЭлементыСтиля.НеактивнаяКнопка.Значение;
		ЭлементыФормы.ОплатаКартой.ЦветРамки = Метаданные.ЭлементыСтиля.НеактивнаяКнопка.Значение;
		ЭлементыФормы.ОплатаКартой.ЦветТекстаКнопки = Метаданные.ЭлементыСтиля.НеактивныйЭлемент.Значение;
	КонецЕсли;
КонецПроцедуры

Процедура ДоступностьКнопок(Доступность) Экспорт
	Доступность = Не ЗначениеЗаполнено(Оплачено);
	Для Инд = 1 По 10 Цикл
	
		ЭлементыФормы["Кнопка"+(Инд%10)].Доступность = Доступность;
	
	КонецЦикла; 	
	
	ЭлементыФормы.КнопкаТочка.Доступность = Доступность;
	ЭлементыФормы.КнопкаОчистить.Доступность = Доступность;
	
КонецПроцедуры

Процедура НажатиеЦифры(Элемент)
	глОтсечкаПростоя();
	Цифра = Прав(Элемент.Имя,1);
	НадписьПолучено = СтрЗаменить(НадписьПолучено, " " + глСимволРубля,"") + Цифра;
	былоПолучено = Получено;
	Получено = Число(СтрЗаменить(НадписьПолучено, Символы.НПП, ""));
	Если былоПолучено <> Получено Тогда
		НадписьПолучено = "" + Формат(Получено,"ЧРД=.");
	КонецЕсли;
	НадписьПолучено = НадписьПолучено + " " + глСимволРубля;
 	ПриИзмененииСуммы();
КонецПроцедуры

Процедура КнопкаТочкаНажатие(Элемент)
	глОтсечкаПростоя();
	Если Найти(НадписьПолучено,". ") Тогда
		НадписьПолучено  = СтрЗаменить(НадписьПолучено,". "," ");
	ИначеЕсли Найти(НадписьПолучено,".") Тогда
		
	Иначе
		
		НадписьПолучено = СтрЗаменить(НадписьПолучено," " + глСимволРубля, "") + ".";
		НадписьПолучено = НадписьПолучено + " " + глСимволРубля;
	КонецЕсли;
КонецПроцедуры

Процедура КнопкаОчиститьНажатие(Элемент)
	глОтсечкаПростоя();
	НадписьПолучено = "0 " + глСимволРубля;
	Получено = 0;
	ПриИзмененииСуммы();
КонецПроцедуры

Процедура ОтменаНажатие(Элемент)
	глОтсечкаПростоя();
	РезультатОплаты.Вставить("ОТМЕНА", Истина);
	РезультатОплаты.Вставить("Успех", Ложь);
	Закрыть(РезультатОплаты);
КонецПроцедуры

Процедура ОкНажатие(Элемент)
	глОтсечкаПростоя();
	Оплата();          	
	РезультатОплаты.Вставить("Сдача", НадписьСдача);
	Закрыть(РезультатОплаты);
	глОтсечкаПростоя();
КонецПроцедуры

Процедура ОплатаКартойНажатие(Элемент)
	глОтсечкаПростоя();
	Если Оплачено Тогда
		ОтменаТранзакцииПлатежнойСистемы();
		Закрыть(РезультатОплаты);
	Иначе 
		Оплата();
		Закрыть(РезультатОплаты);
	КонецЕсли;
	глОтсечкаПростоя();
КонецПроцедуры

Процедура ПриОткрытии()
	//СменыОткрыты = ПроверитьОткрытиеСмен();
	//Если не СменыОткрыты Тогда
	//	Закрыть(Неопределено);
	//КонецЕсли;
	Оплачено = ПротоколРасчетов.Протокол.Итог("СуммаФакт");
	Товары = Заказ.Товары;
	СуммаЧека = СуммаРеализации();
	Коплате = СуммаЧека - Оплачено;
	Если ПротоколРасчетов.Фискализирован Тогда
		ЭлементыФормы.НадписьЗаказОплачен.Видимость = 1;
		УстановитьДоступностьКнопки("ОК", Истина);
		УстановитьДоступностьКнопки("ОплатаКартой", Ложь);
		ДоступностьКнопок(Ложь);
		Получено = Коплате;
		ИнтерфейсРМ.ПриОткрытииОкна(ЭтаФорма);
		НадписьСуммаЧека = ""+СуммаЧека + " " + глСимволРубля;
		Возврат;
	ИначеЕсли Оплачено Тогда
		Получено = Коплате;
		ЭлементыФормы.ОплатаКартой.Заголовок = "ВОЗВРАТ ДЕНЕГ НА КАРТУ";
	    ЭлементыФормы.НадписьЗаказОплачен.Видимость = 1;
	КонецЕсли; 
	
	НадписьКоплате = Формат(Коплате, "ЧДЦ=2; ЧРД=.") + " " + глСимволРубля;
	НадписьСуммаЧека = ""+Формат(СуммаЧека, "ЧДЦ=2; ЧРД=.") + " " + глСимволРубля;
	ИнтерфейсРМ.ПриОткрытииОкна(ЭтаФорма);
	ПриИзмененииСуммы(); 
	ДоступностьКнопок(Не Оплачено);
КонецПроцедуры

Процедура ПриЗакрытии()
	ИнтерфейсРМ.ПриЗакрытииОкна();
КонецПроцедуры

Процедура ОтправитьНаПочтуНажатие(Элемент)
	//РаботаСокнами.AOTon(Заголовок, "off");
	Email = ИнтерфейсРМ.ВводСтроки("Введите адрес электронной почты");	
    	
	ПроверкаEmail();
	Если ЗначениеЗаполнено(Email) Тогда
		ЭлементыФормы.ОтправитьНаПочту.ЦветТекстаКнопки = Метаданные.ЭлементыСтиля.Акцент.Значение;
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПроверкаEmail()
	RegExp = ирКэш.Получить().RegExp;
	RegExp.Pattern = "(^[a-zA-Z0-9_.+-]+@[a-zA-Z0-9-]+\.[a-zA-Z0-9-.]+$)";
	ЭтоEmail = RegExp.Test(Email);
	Если НЕ ЭтоEmail Тогда
		//РаботаСокнами.Подсказка("Некорректный email: `n " + Email);
		Email = "";
	КонецЕсли; 
КонецПроцедуры

Процедура ОтправитьСМСНажатие(Элемент)
	
	НомерТелефона = ИнтерфейсРМ.ВводЧисла("Введите номер телефона","Строка",20,0,,,,,"8 (\999) 999-99-99");
	Если ЗначениеЗаполнено(НомерТелефона) Тогда
		ЭлементыФормы.ОтправитьСМС.ЦветТекстаКнопки = Метаданные.ЭлементыСтиля.Акцент.Значение;
	КонецЕсли;	
КонецПроцедуры

Процедура ВнешнееСобытие(Источник, Событие, Данные)
	глОтсечкаПростоя();
	Если глСтекОкон[0].Форма <> ЭтаФорма Тогда
		Возврат;
	КонецЕсли;
	
	Email = ОбработкаВнешнихСобытий.ПолучитьДанные(Источник,Событие,Данные);
	ПроверкаEmail();
	Если Не ЗначениеЗаполнено(Email) Тогда
		Возврат;
	КонецЕсли;
	Если ЗначениеЗаполнено(Email) Тогда
		ЭлементыФормы.ОтправитьНаПочту.ЦветТекстаКнопки = Метаданные.ЭлементыСтиля.Акцент.Значение;
		РаботаСокнами.Подсказка("Email: " + Email);
	КонецЕсли;	
	
КонецПроцедуры


НадписьПолучено = "0";
Получено = 0;

ПараметрыОкна = Новый Структура("Центр, Лево, Верх, Ширина, Высота", Истина);

ЭлементыФормы.ОтправитьНаПочту.Заголовок = Шрифты.ПолучитьСимвол("envelope_o");
ЭлементыФормы.ОтправитьСМС.Заголовок = Шрифты.ПолучитьСимвол("comment_o");