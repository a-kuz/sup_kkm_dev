
//
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если глПараметрыРМ = Неопределено Тогда
		Возврат;
	КонецЕсли;	
		
КонецПроцедуры

//
//
Процедура ПриОткрытии()
		
	ПодключитьОбработчикОжидания("РазвернутьОкноИнфоДисплея", 0.1, Истина);

	ОбновитьТаймер();
	
	ПодключитьОбработчикОжидания("ОбновитьТаймер", 1);
	
	// Установить картинку рекламную
	Если НЕ Изображение.Пустая() Тогда
		ЭлементыФормы.ПолеКартинки.Картинка		= Изображение.Хранилище.Получить();
		ЭлементыФормы.тНаименование.Картинка	= Изображение.Хранилище.Получить();
	КонецЕсли;
	
	ЭлементыФормы.тНаименование.Заголовок		= Строка(глРабочееМесто);
	ЭлементыФормы.тИмяКонфигурации.Заголовок	= Метаданные.Синоним;
	
КонецПроцедуры

//
//
Процедура ПриЗакрытии()
	
КонецПроцедуры

// Вывести таблицу заказа на ИнфоДисплей
//
Процедура ЗаполнитьТаблицу() 
		
	ТабличныйДокумент = ЭлементыФормы.ПолеТабличногоДокумента;
	ТабличныйДокумент.Очистить();
	ТабличныйДокумент.ФиксацияСверху = 1;
	
	Если ЭтотОбъект.РазмерОкнаИнфоДисплея = 2 Тогда
		ИмяМакета = "м800х600";
	ИначеЕсли ЭтотОбъект.РазмерОкнаИнфоДисплея = 3 Тогда
		ИмяМакета = "м1024х768";
	Иначе
		ИмяМакета = "мСвободный";
	КонецЕсли;	
	
	Макет = ЭтотОбъект.ПолучитьМакет(ИмяМакета);
	
	ОбластьМакетаЗаголовок		= Макет.ПолучитьОбласть("Заголовок|ВидимаяОбласть");
	ОбластьМакетаСтрокаТовара	= Макет.ПолучитьОбласть("СтрокаТовара|ВидимаяОбласть");
	ОбластьМакетаСтрокаТовараДоп= Макет.ПолучитьОбласть("СтрокаТовараДоп|ВидимаяОбласть");
    ОбластьМакетаСпецифика		= Макет.ПолучитьОбласть("СтрокаСпецифики|ВидимаяОбласть");
	ОбластьМакетаПодвалТоваров	= Макет.ПолучитьОбласть("ПодвалТоваров|ВидимаяОбласть");	
	ОбластьМакетаПодвал			= Макет.ПолучитьОбласть("Подвал|ВидимаяОбласть");
	
	ТабличныйДокумент.Вывести(ОбластьМакетаЗаголовок);
	
	Если ТаблицаЗаказа.Строки.Количество() = 0 Тогда
		ПоказатьРекомендации(Макет, ТабличныйДокумент, СписокРекомендаций);
		Возврат;
	КонецЕсли;
	

	// Нет изображения для товара (пустая) //pr возможно нужна действительно картинка по умолчанию
	КартинкаПоУмолчанию = Новый Картинка;

	Кл = 0;
	
	Для Каждого СтрокаЗаказа Из ТаблицаЗаказа.Строки Цикл
		
		// Удаленные не показываем
		Если СтрокаЗаказа.Количество = 0 Тогда
			Продолжить;
		КонецЕсли;		
		
		Кл = Кл + 1;
		
		ЕстьДопИнфо	= ЗначениеЗаполнено(СтрокаЗаказа.Товар.ДопИнформация);
		ТекКартинка	= ?(ЗначениеЗаполнено(СтрокаЗаказа.Товар.Изображение), СтрокаЗаказа.Товар.Изображение.Хранилище.Получить(), КартинкаПоУмолчанию);
		
		ОбластьМакетаСтрока = ?(ЕстьДопИнфо, ОбластьМакетаСтрокаТовараДоп, ОбластьМакетаСтрокаТовара);
		ОбластьМакетаСтрока.Параметры.Заполнить(СтрокаЗаказа);
		ОбластьМакетаСтрока.Параметры.Кл = Кл;
		Если ЕстьДопИнфо Тогда
			ОбластьМакетаСтрока.Параметры.ДополнительлнаяИнформация	= СтрокаЗаказа.Товар.ДопИнформация;
			ОбластьМакетаСтрока.Рисунки.КартинкаОбъектаДоп.Картинка = ТекКартинка;
		Иначе
			ОбластьМакетаСтрока.Рисунки.КартинкаОбъекта.Картинка	= ТекКартинка;
		КонецЕсли;	
		
		ТабличныйДокумент.Вывести(ОбластьМакетаСтрока); 
		
		// Если есть специфики добавляем
		Если ЗначениеЗаполнено(СтрокаЗаказа.Строки) Тогда
			ОбластьМакетаСпецифика.Параметры.Наименование = ПолучитьПереченьСпецифик(СтрокаЗаказа);
			ТабличныйДокумент.Вывести(ОбластьМакетаСпецифика); 
		КонецЕсли;	
		
	КонецЦикла;
	
	Сумма	= ТаблицаЗаказа.Строки.Итог("Сумма");
	Итого	= ТаблицаЗаказа.Строки.Итог("СуммаРеализации");

	ОбластьМакетаПодвалТоваров.Параметры.Заказ	= Заказ;
	ОбластьМакетаПодвалТоваров.Параметры.Сумма	= Строка(ФорматСумм(Сумма));
	ОбластьМакетаПодвалТоваров.Параметры.Скидка	= Строка(ФорматСумм(Сумма - Итого));
	ОбластьМакетаПодвалТоваров.Параметры.Итого	= Строка(ФорматСумм(Итого));
	
	ТабличныйДокумент.Вывести(ОбластьМакетаПодвалТоваров);
	
    ПоказатьРекомендации(Макет, ТабличныйДокумент, СписокРекомендаций);
	
	// Показываем нижнюю стоку
	ТабличныйДокумент.ТекущаяОбласть = ТабличныйДокумент.Область("R"+Строка(ТабличныйДокумент.ВысотаТаблицы)+"C1");	
	
КонецПроцедуры

// Вывести рекомендации на экран
//
Процедура ПоказатьРекомендации(Макет, ТабличныйДокумент, СписокРекомендаций)
	
	// Если есть рекомендации показываем
	КоличествоРекомендаций = СписокРекомендаций.Количество();
	
	Если КоличествоРекомендаций = 0 Тогда
		Возврат;	
	КонецЕсли;
	
	// Максимальное количество рекомендаций показываемых пользователю. //pr надо взять из настроек 
	МаксисальноеКоличествоРекомендаций = 3;
	
	ОбластьМакетаРекомендацииЗ	= Макет.ПолучитьОбласть("РекомендацииЗаголовок|ВидимаяОбласть");
	ОбластьМакетаРекомендации	= Макет.ПолучитьОбласть("Рекомендация|ВидимаяОбласть");

	ТабличныйДокумент.Вывести(ОбластьМакетаРекомендацииЗ);
	
	Для Сч = 0 по КоличествоРекомендаций - 1 Цикл
		Если Сч > МаксисальноеКоличествоРекомендаций - 1 Тогда
			Прервать;
		КонецЕсли;	
		ОбластьМакетаРекомендации.Параметры.Что		= СписокРекомендаций[Сч].Значение;
		ОбластьМакетаРекомендации.Параметры.КЧему	= СписокРекомендаций[Сч];
		ТабличныйДокумент.Вывести(ОбластьМакетаРекомендации);
	КонецЦикла;

КонецПроцедуры	

// Получить в строку все специфики к товару
//
Функция  ПолучитьПереченьСпецифик(СтрокиСпецифик, ТекНаименование = "")
	
	Для Каждого СтрокаСпецифики Из СтрокиСпецифик.Строки Цикл
		ТекНаименование = ТекНаименование + ?(ЗначениеЗаполнено(ТекНаименование), ", " + СтрокаСпецифики.Наименование, СтрокаСпецифики.Наименование);
		Если ЗначениеЗаполнено(СтрокаСпецифики.Строки) Тогда
			ПолучитьПереченьСпецифик(СтрокаСпецифики, ТекНаименование);
		КонецЕсли;
   КонецЦикла;	
   
   Возврат ТекНаименование;
   
КонецФункции

// Спозиционировать форму дисплея на втором экране
//
Процедура РазвернутьОкноИнфоДисплея()
	//
	ТекущаяФормаhWnd = 1;
	
	
	// Открываем форму на втором экране (слева)
	РаботаСокнами.SetWndPosV8(ТекущаяФормаhWnd, -801, 0);
	РаботаСокнами.УстановитьРазмерДочернегоОкна(ТекущаяФормаhWnd, 800, 600);
	// Разворачиваем во весь второй экран
	РаботаСокнами.МаксимизироватьДочернее(ТекущаяФормаhWnd);
	
	ПодключитьОбработчикОжидания("УстановитьРазмерыИнфоДисплея", 0.1, Истина);	
	
КонецПроцедуры

// Установить размер формы
// Установить активную страницу рекламы
// Установить фокус на РМ
//
Процедура УстановитьРазмерыИнфоДисплея()
	
	Перем X, Y;
	
	// Получаем координаты левого верхнего угла
	РаботаСокнами.GetWndPosV8(ТекущаяФормаhWnd, X, Y );

	// Изменяем зазмер если не во весь экран
	Если РазмерОкнаИнфоДисплея <> 0 Тогда
		Если РазмерОкнаИнфоДисплея = 3 Тогда
			РаботаСокнами.УстановитьПоложениеОкна(ТекущаяФормаhWnd, X + 1, Y + 1);
			РаботаСокнами.УстановитьРазмерДочернегоОкна(ТекущаяФормаhWnd, 1025, 768);
		Иначе	
			РаботаСокнами.УстановитьПоложениеОкна(ТекущаяФормаhWnd, X + 1, Y + 1);
			РаботаСокнами.УстановитьРазмерДочернегоОкна(ТекущаяФормаhWnd, 800, 600);
		КонецЕсли;
	КонецЕсли;	
	
	// Активируем рекламную страницу
	АктивироватьСтраницуЗаказа(Ложь);
	
	// установка фокуса обратно на РМ
	WshShell.AppActivate(?(ТекущийЯзыкСистемы()="en","1C:Enterprise", "1С:Предприятие") + " - " + ПолучитьЗаголовокСистемы() );
	
КонецПроцедуры

// Свернуть форму ИнфоДисплея (не используется)
//
Процедура СвернутьИнфоДисплей()
	
	РаботаСокнами.MinimizeV8( ТекущаяФормаhWnd );
	
КонецПроцедуры

// чтобы часики тикали
//
Процедура ОбновитьТаймер()
	
	ТекВремя = ТекущаяДата();
	ЭлементыФормы.тВремя.Заголовок	= Формат(ТекВремя,"ДЛФ=В");
	
КонецПроцедуры

// перелистывание между заказом и рекламой
//
Процедура АктивироватьСтраницуЗаказа(ПоказатьЗаказ = Истина) Экспорт
	
	ЭлементыФормы.ПанельОсновная.Страницы.Заказ.Видимость			= ПоказатьЗаказ;
	ЭлементыФормы.ПанельОсновная.Страницы.Информационная.Видимость	= НЕ ПоказатьЗаказ;
	ЗаполнитьТаблицу();
	
КонецПроцедуры	
