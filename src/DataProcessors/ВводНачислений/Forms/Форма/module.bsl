
Перем ПараметрыОкна Экспорт;	// структура, определяет положение и размеры окна

Перем ККМ,СписокДопККМ;
Перем МассивВариантовОплат, ВариантОплаты;
Перем ДокОткрытияСмены;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ МОДУЛЯ

// Выбор клиента
//
Процедура ВыборКлиента(КодКлиента=Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(ДокОснование) Тогда
		Возврат;
	КонецЕсли;
	
	ВыбКлиент = ИнтерфейсРМ.ИдентификацияКлиента(КодКлиента, Истина);
	Если ВыбКлиент=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ВыбКлиент.Безнал Тогда
		Текст1="Нет доступа!";
		Текст2="Выбранному клиенту безналичные расчеты не разрешены!";
		ИнтерфейсРМ.ВопросПредупреждение("Ошибка",Текст1,Текст2,"","ОК","");
		Возврат;
	КонецЕсли;
	
	Клиент = ВыбКлиент;
	
	Если МассивВариантовОплат.Найти(Клиент.ОсновнойВариантОплаты) <> Неопределено Тогда
		ВариантОплаты = Клиент.ОсновнойВариантОплаты;
		ЭлементыФормы.КнопкаВариантОплаты.Заголовок = ВариантОплаты.Наименование;
	КонецЕсли;
	
	ПоказатьИнфуПоКлиенту();
	
КонецПроцедуры

Процедура ПоказатьИнфуПоКлиенту()
	
	Если ЗначениеЗаполнено(ДокОснование) Тогда
		ЭлементыФормы.КнопкаКлиент.Заголовок = ?(Клиент.Пустая(), КлиентСтр, Клиент.Наименование);
		ЭлементыФормы.тТекущийОстаток.Заголовок = "Текущий остаток: "+ФорматСумм( НакопленияКлиента(Клиент,ДокОснование).СуммаБезнал );
		
	ИначеЕсли Клиент.Пустая() Тогда
		ЭлементыФормы.КнопкаКлиент.Заголовок = "нажмите для выбора клиента...";
		ЭлементыФормы.тТекущийОстаток.Заголовок = "";
	Иначе
		ЭлементыФормы.КнопкаКлиент.Заголовок = Клиент.Наименование;
		ЭлементыФормы.тТекущийОстаток.Заголовок = "Текущий остаток: "+ФорматСумм( НакопленияКлиента(Клиент).СуммаБезнал );
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если НЕ глПараметрыРМ.ККМЕсть Тогда
		Текст1="Нет доступа!";
		Текст2="Данное рабочее место не является кассовым!";
		ИнтерфейсРМ.ВопросПредупреждение("Ошибка",Текст1,Текст2,"","ОК","");
		Отказ = Истина;
		Возврат;
		
	ИначеЕсли НЕ ИнтерфейсРМ.ПроверкаПользователяСмены() Тогда
		Отказ = Истина;
		Возврат;
		
	ИначеЕсли НЕ ИнтерфейсРМ.ПроверкаСмены() Тогда
		Отказ = Истина;
		Возврат;
		
	КонецЕсли;
	
	ДокОткрытияСмены = ИнтерфейсРМ.ТекущаяСмена();
	
	ККМ = глПараметрыРМ.ККМ;
	Если глПараметрыРМ.ККМЕстьДоп И ЗначениеЗаполнено(глПараметрыРМ.ККМСписокДоп) Тогда
		СписокДопККМ = глПараметрыРМ.ККМСписокДоп.Скопировать();
		СписокДопККМ.Вставить(0,ККМ);
	КонецЕсли; 
	
	ЭлементыФормы.КнопкаККМ.Заголовок = ККМ.Наименование;
	
	//----------------------------------------
	ЗапрещенныеТипыОплат = Новый Массив;
	ЗапрещенныеТипыОплат.Добавить(Перечисления.ТипыОплаты.Безнал);
	ЗапрещенныеТипыОплат.Добавить(Перечисления.ТипыОплаты.Неплательщик);
	Если глВерсия>1 Тогда
		ЗапрещенныеТипыОплат.Добавить(Перечисления.ТипыОплаты.Бонусы);
	КонецЕсли; 
	МассивВариантовОплат = ИнтерфейсРМ.ПолучитьРазрешенныеВариантыОплаты(Клиент,ЗапрещенныеТипыОплат);
	
	Если МассивВариантовОплат.Количество()=0 Тогда
		Текст1="Недостаточно прав!";
		Текст2="Не доступен ни один вариант оплаты!";
		ИнтерфейсРМ.ВопросПредупреждение("Ошибка",Текст1,Текст2,"","ОК","");
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ВариантОплаты = ?( ЗначениеЗаполнено(Клиент.ОсновнойВариантОплаты), Клиент.ОсновнойВариантОплаты,
	глПараметрыРМ.ОплатаОсновнойВариант );
	
	Если МассивВариантовОплат.Найти(ВариантОплаты)=Неопределено Тогда
		ВариантОплаты = МассивВариантовОплат[0];
	КонецЕсли;
	
	ЭлементыФормы.КнопкаВариантОплаты.Заголовок = ВариантОплаты.Наименование;
	
	//----------------------------------------
	Если ЗначениеЗаполнено(ДокОснование) Тогда
		ЭлементыФормы.тЗаголовок.Заголовок = ?(ЗнакОперации=1, "Предоплата по брони", "Возврат предоплаты");
	Иначе
		ЭлементыФормы.тЗаголовок.Заголовок = ?(ЗнакОперации=1, "Начисление на карту", "Удержание с карты");
	КонецЕсли; 
	
	ПоказатьИнфуПоКлиенту();
	
	ИнтерфейсРМ.ВыводНаДП("СоздатьОкно");
	ИнтерфейсРМ.ВыводНаДП("ВывестиТекст", ВРег(ЭлементыФормы.тЗаголовок.Заголовок) );
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	ИнтерфейсРМ.ПриОткрытииОкна(ЭтаФорма);
	
КонецПроцедуры

Процедура ПриЗакрытии()
	
	ИнтерфейсРМ.ПриЗакрытииОкна();
	
	ИнтерфейсРМ.ВыводНаДП("Ожидание");
	
КонецПроцедуры

Процедура ВнешнееСобытие(Источник, Событие, Данные)
	
	Если НЕ ВводДоступен() Тогда
		Возврат;
	КонецЕсли; 
	
	_Знач = ОбработкаВнешнихСобытий.ПолучитьДанные(Источник,Событие,Данные);
	Если НЕ ЗначениеЗаполнено(_Знач) Тогда
		Возврат;
	КонецЕсли;
	
	ВыборКлиента("Идентификатор_"+_Знач);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура КнопкаККМНажатие(Элемент)
	
	ТекущийЭлемент = ЭлементыФормы.КнопкаОплата;
	
	Если НЕ ИнтерфейсРМ.ПроверкаПраваДоступа("ВыборККМ") ИЛИ НЕ ЗначениеЗаполнено(СписокДопККМ) Тогда
		Возврат;
	КонецЕсли;
	
	ВыбККМ = ИнтерфейсРМ.ВыборИзСписка(СписокДопККМ);
	Если ВыбККМ=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ККМ = ВыбККМ;  
	ЭлементыФормы.КнопкаККМ.Заголовок = ККМ.Наименование;
	
КонецПроцедуры

Процедура КнопкаВариантОплатыНажатие(Элемент)
	
	ТекущийЭлемент = ЭлементыФормы.КнопкаОплата;
	
	ВыбВариант = ИнтерфейсРМ.ВыборИзСписка(МассивВариантовОплат);
	Если ВыбВариант=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВариантОплаты = ВыбВариант;
	ЭлементыФормы.КнопкаВариантОплаты.Заголовок = ВариантОплаты.Наименование;
	
КонецПроцедуры

Процедура КнопкаКлиентНажатие(Элемент)
	
	ВыборКлиента();
	
КонецПроцедуры

Процедура КнопкаОплатаНажатие(Элемент)
	
	Если НЕ ЗначениеЗаполнено(Клиент) И НЕ ЗначениеЗаполнено(ДокОснование) Тогда
		Текст1="Клиент не выбран!";
		Текст2="Сначала надо выбрать клиента!";
		ИнтерфейсРМ.ВопросПредупреждение("Ошибка",Текст1,Текст2,"","ОК","");
		Возврат;
	КонецЕсли;
	
	Если СуммаОперации = 0 Тогда
		Сумма = ИнтерфейсРМ.ВводЧисла("Введите сумму", "Число", 12, 2);
	Иначе
		Сумма = СуммаОперации;
	КонецЕсли; 
	
	Если Сумма=0 ИЛИ Сумма=Неопределено Тогда
		Возврат;
		
	ИначеЕсли ЗнакОперации = -1 И НакопленияКлиента(Клиент,ДокОснование).СуммаБезнал < Сумма Тогда
		Текст1="Превышение суммы!";
		Текст2="Указанная сумма больше, чем можно удержать!";
		ИнтерфейсРМ.ВопросПредупреждение("Предупреждение",Текст1,Текст2,"","ОК","");
		Возврат;
	КонецЕсли;
	
	Текст1=?(ЗнакОперации=1,"Начисление","Удержание");
	Текст2="Сумма: "+ФорматСумм(Сумма)+", "+ВариантОплаты.Наименование+"
			|Клиент: "+ЭлементыФормы.КнопкаКлиент.Заголовок;
	
	Ответ = ИнтерфейсРМ.ВопросПредупреждение("Подтверждение",Текст1,Текст2,"ОК","","Esc=Отмена", ГоризонтальноеПоложение.Лево);
	Если Ответ="Отмена" Тогда
		Возврат;
	КонецЕсли;
	
	ИнтерфейсРМ.ВыводНаДП("ВывестиТекст",
							ВРег(ЭлементыФормы.тЗаголовок.Заголовок)	,0,
							"СУММА:",Сумма);
	
	//--------------------------------------------------------
	СсылочныйНомер	= "";
	ДанныеКарты		= "";
	Если ВариантОплаты.Тип = Перечисления.ТипыОплаты.Карта Тогда
		ТипОперации = ?(ЗнакОперации=1, "Оплата", "Возврат");
		Если НЕ Защита.ЗапросПлатежнойСистемы(ТипОперации, Сумма, ДокОткрытияСмены.Номер, 0, СсылочныйНомер, ДанныеКарты, ВариантОплаты) Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	//--------------------------------------------------------
	// попробуем записать Документ и распечатать чек
	НачатьТранзакцию();
	
	глОжидание.Начало("Печать чека...",
					"Идет запись данных и печать чека на ККМ.
					|Пожалуйста, подождите...");
	
	Док = Документы.НачислениеБезнал.СоздатьДокумент();
	Док.Дата			= ТекущаяДата();
	Док.Автор			= глПользователь;
	Док.Смена			= ИнтерфейсРМ.ТекущаяСмена();
	Док.Клиент			= Клиент;
	Док.ДокОснование	= ДокОснование;
	Док.Сумма			= ЗнакОперации*Сумма;
	Док.ККМ				= ККМ;
	Док.ВариантОплаты	= ВариантОплаты;
	
	ФлагОК = ИнтерфейсРМ.ПопыткаДействияСОбъектом( Док, "Объект.Записать(РежимЗаписиДокумента.Проведение)" );
	
	Если ФлагОК Тогда
		ОбработкаПечати = ИнтерфейсРМ.ПолучитьОбъектОбработки("ПечатьЧека");
		ОбработкаПечати.ККМ = ККМ;
		ФлагОК = ОбработкаПечати.ПечатьНачислений(?(Клиент.Пустая(), КлиентСтр, Клиент), ДокОснование, ЗнакОперации*Сумма, ВариантОплаты);
	КонецЕсли;
	
	Если ФлагОК Тогда
		
		ЗафиксироватьТранзакцию();
		
		Если глПараметрыРМ.ПечатьСчетаНачисления Тогда
			ОбработкаПечатьЗаказа = ИнтерфейсРМ.ПолучитьОбъектОбработки("ПечатьЗаказа");
			ОбработкаПечатьЗаказа.Начисление = Док.Ссылка;
			ОбработкаПечатьЗаказа.ПечатьНачислений();
		КонецЕсли;
		
		Закрыть("ОК");
		
	Иначе
		
		ОтменитьТранзакцию();
		
		Если ЗначениеЗаполнено(ДанныеКарты) Тогда
			ТипОперации = ?(ЗнакОперации=1, "ОтменаОплаты", "ОтменаВозврата");
			Защита.ЗапросПлатежнойСистемы(ТипОперации, Сумма, ДокОткрытияСмены.Номер, 0, СсылочныйНомер, ДанныеКарты, ВариантОплаты);
		КонецЕсли;
		
	КонецЕсли;
	
	глОжидание.Конец();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ТЕЛО МОДУЛЯ

ПараметрыОкна = Новый Структура("Центр, Лево, Верх, Ширина, Высота", Истина);
