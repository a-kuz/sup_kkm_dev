#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Попытка
		КупоныГостя = Параметры.КупоныГостя;
	Исключение
	    Отказ = Истина;
		Возврат;
	КонецПопытки;
	
	Если НЕ ТипЗнч(КупоныГостя) = Тип("Массив") Тогда
	    Отказ = Истина;
		Возврат;
	КонецЕсли;
	ПроектФильтр = Параметры.ПроектФильтр;
	
	Заголовок = Параметры.ЗаголовокСтанции + ?(ПроектФильтр, "в " + ЛояльностьКлиентСерверПовтИсп.МестоРеализацииПоПроекту(ПроектФильтр, Истина), "");
		
	ГруппаКупоны = Элементы.ГруппаКупоны;
	КнопкаЭталон = Элементы.Закрыть;
	КоличествоЭлементовВСтроке = 3;
	
	ЦветФонаОплаченнойСтроки = Метаданные.ЭлементыСтиля.ЦветФонаОплаченнойСтроки.Значение; //Новый Цвет(222, 252, 222);
	ЦветНеактивнаяКнопка = Метаданные.ЭлементыСтиля.НеактивнаяКнопка.Значение; //Новый Цвет(223, 223, 223);
	
	ВыведеноКупонов = 0;
	
	Для Сч = 0 По КупоныГостя.ВГраница() Цикл
		
		ТекПроект = КупоныГостя[Сч].Проект;
		Если НЕ ПроектФильтр = 0 И НЕ ТекПроект = 0 И НЕ ТекПроект = ПроектФильтр Тогда
			Продолжить;
		КонецЕсли;
		
		Если ВыведеноКупонов % КоличествоЭлементовВСтроке = 0 Тогда
			ТекСтрока = Элементы.Добавить("СтрокаКнопок" + Формат(ВыведеноКупонов / КоличествоЭлементовВСтроке, "ЧГ=0"), Тип("ГруппаФормы"), ГруппаКупоны);
			ТекСтрока.Вид = ВидГруппыФормы.ОбычнаяГруппа;
			ТекСтрока.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
			ТекСтрока.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
			ТекСтрока.ОтображатьЗаголовок = Ложь;
			ТекСтрока.Отображение = ОтображениеОбычнойГруппы.Нет;
		КонецЕсли;
		
		КупонКод = КупоныГостя[Сч].Код;
		КупонСтатус = КупоныГостя[Сч].Статус;
		
		ТекЭлемент = Элементы.Добавить("Купон_" + КупонКод, Тип("КнопкаФормы"), ТекСтрока);
		ТекЭлемент.ИмяКоманды = Параметры.ИмяКоманды;
		ТекЭлемент.Заголовок = КупоныГостя[Сч].ИнфоСтанции;
		//ТекЭлемент.Высота = 3;
		ТекЭлемент.ВысотаЗаголовка = 3;
		ЗаполнитьЗначенияСвойств(ТекЭлемент, КнопкаЭталон, "Высота,Ширина,Шрифт");

		Если КупонСтатус = -1 Тогда
			ТекЭлемент.ЦветФона = ЦветаСтиля.ЦветФонаФормы;
			ТекЭлемент.ЦветТекста = WebЦвета.Красный;
		ИначеЕсли КупонСтатус = 0 Тогда
			ТекЭлемент.ЦветФона = WebЦвета.Лосось;
			ТекЭлемент.ЦветТекста = ЦветаСтиля.ЦветТекстаКнопки;
		ИначеЕсли КупонСтатус = 1 Тогда	
			ТекЭлемент.ЦветФона = ЦветаСтиля.ЦветФонаФормы;
			ТекЭлемент.ЦветТекста = ЦветаСтиля.ЦветТекстаКнопки;
		ИначеЕсли КупонСтатус = 2 Тогда	
			ТекЭлемент.ЦветФона = ЦветФонаОплаченнойСтроки;
			ТекЭлемент.ЦветТекста = WebЦвета.Черный;
		ИначеЕсли КупонСтатус = 3 Тогда	
			ТекЭлемент.ЦветФона = ЦветНеактивнаяКнопка;
			ТекЭлемент.ЦветТекста = ЦветаСтиля.ЦветТекстаКнопки;
		КонецЕсли;
		
		ВыведеноКупонов = ВыведеноКупонов + 1;
		
	КонецЦикла;
	
	ТабДокПП.Очистить();
	ТабДокПП.Вывести(СформироватьТабДокПП(КупоныГостя));
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ДопПараметрыИнфо = Новый Структура("ТабДокПП", ТабДокПП);
	ИнтерфейсРМ.ВыводНаИнфоДисплей("ПоказатьПП", , , , ДопПараметрыИнфо);
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	ИнтерфейсРМ.ВыводНаИнфоДисплей("УбратьПП");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если ТекущаяСтраница = Элементы.ГруппаВыход Тогда
		Закрыть(Неопределено);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗакрытьНажатие(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ПлашкаКупон(Команда)
	
	Закрыть(СтрЗаменить(ТекущийЭлемент.Имя, "Купон_", ""));
	
КонецПроцедуры

&НаКлиенте
Процедура ПлашкаАкция(Команда)
	// Вставить содержимое обработчика.
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СформироватьТабДокПП(КупоныГостя)
	
	ТабДок = Новый ТабличныйДокумент;
	
	Макет = РеквизитФормыВЗначение("Объект").ПолучитьМакет("МакетПП");
	
	ОблМакета = Макет.ПолучитьОбласть("Шапка");
	ОблМакета.Параметры.Заголовок = Параметры.ЗаголовокГостя;
	ТабДок.Вывести(ОблМакета);
	
	ВыведеноКупонов = 0;
	
	Для Сч = 0 По КупоныГостя.ВГраница() Цикл
		ТекПроект = КупоныГостя[Сч].Проект;
		Если НЕ Параметры.ПроектФильтр = 0 И НЕ ТекПроект = 0 И НЕ ТекПроект = Параметры.ПроектФильтр Тогда
			Продолжить;
		КонецЕсли;
		
		КупонСтатус = КупоныГостя[Сч].Статус;
		
		Если КупонСтатус = -1 Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяОбласти = СтрШаблон("КупонСтатус%1|ВидимаяОбласть", КупонСтатус);
		ОблМакета = Макет.ПолучитьОбласть(ИмяОбласти);
		ОблМакета.Параметры.ИнфоГостя = КупоныГостя[Сч].ИнфоГостя;
		
		Если ВыведеноКупонов % КоличествоЭлементовВСтроке = 0 Тогда
			ТабДок.Вывести(ОблМакета);
		Иначе
			ТабДок.Присоединить(ОблМакета);
		КонецЕсли;
		
		ВыведеноКупонов = ВыведеноКупонов + 1;
	КонецЦикла;
	
	ПоследнийКупон = Сч % КоличествоЭлементовВСтроке;
	Если ПоследнийКупон Тогда
		Для Сч = ПоследнийКупон По КоличествоЭлементовВСтроке - 1 Цикл
			ОблМакета = Макет.ПолучитьОбласть("КупонПустой|ВидимаяОбласть");
			ТабДок.Присоединить(ОблМакета);
		КонецЦикла;
	КонецЕсли;
	
	Возврат ТабДок;

КонецФункции

#КонецОбласти

