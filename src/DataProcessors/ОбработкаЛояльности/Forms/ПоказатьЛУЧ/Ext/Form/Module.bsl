
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИндексыКоллекции = Новый Соответствие;
	ИндексыКоллекции.Вставить(3, 15);
	ИндексыКоллекции.Вставить(5, 16);
	ИндексыКоллекции.Вставить(7, 17);
	ИндексыКоллекции.Вставить(10, 18);
	
	Фирма = Параметры.Фирма;
	
	Для каждого ТекСтрока Из Параметры.ЛУЧ Цикл
		НовСтрока = ЛУЧ.Добавить();
		НовСтрока.Проект = ТекСтрока.Проект;
		НовСтрока.МестоРеализации = ЛояльностьКлиентСерверПовтИсп.МестоРеализацииПоПроекту(ТекСтрока.Проект);
		НовСтрока.Коэффициент = ТекСтрока.Коэффициент;
		НовСтрока.КодСУП = ТекСтрока.КодТовара;
		НовСтрока.Лого = ИндексыКоллекции.Получить(ТекСтрока.Коэффициент);
		Если НЕ ЗначениеЗаполнено(НовСтрока.Лого) Тогда
			НовСтрока.Лого = 13;
		КонецЕсли;
	КонецЦикла;
	
    Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВремТЗ", ЛУЧ.Выгрузить(, "КодСУП"));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВремТЗ.КодСУП КАК КодСУП
	|ПОМЕСТИТЬ втЛУЧ
	|ИЗ
	|	&ВремТЗ КАК ВремТЗ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втЛУЧ.КодСУП КАК КодСУП,
	|	Номенклатура.Ссылка КАК Товар
	|ИЗ
	|	втЛУЧ КАК втЛУЧ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
	|		ПО втЛУЧ.КодСУП = Номенклатура.КодСУП
	|			И (НЕ Номенклатура.ПометкаУдаления)";
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	
	Пока РезультатЗапроса.Следующий() Цикл
		СтрокиТов = ЛУЧ.НайтиСтроки(Новый Структура("КодСУП", РезультатЗапроса.КодСУП));
		Для каждого ТекСтрока Из СтрокиТов Цикл
			ТекСтрока.Товар = РезультатЗапроса.Товар;
		КонецЦикла;
	КонецЦикла;
	
	
	Если ЗначениеЗаполнено(Параметры.Фирма) Тогда
		СтруктураОтбор = Новый ФиксированнаяСтруктура("МестоРеализации", Параметры.Фирма.МестоРеализации);
		ПроектФильтр = ЛояльностьКлиентСерверПовтИсп.ПроектПоМестуРеализации(Параметры.Фирма.МестоРеализации);
	Иначе
		СтруктураОтбор = Новый ФиксированнаяСтруктура;
		ПроектФильтр = 0;
	КонецЕсли;
	Элементы.ЛУЧ.ОтборСтрок = СтруктураОтбор;
	
	Заголовок = СтрШаблон("ЛУЧший покупатель %1:", ЛояльностьКлиентСерверПовтИсп.МестоРеализацииПоПроекту(ПроектФильтр, Истина));
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ДопПараметрыИнфо = Новый Структура("ТабДокЛуч", СформироватьТабДокЛуч());
	ИнтерфейсРМ.ВыводНаИнфоДисплей("ПоказатьЛУЧ", , , , ДопПараметрыИнфо);
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	ИнтерфейсРМ.ВыводНаИнфоДисплей("УбратьЛУЧ");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
//Код процедур и функций
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЛУЧ
//Код процедур и функций
#КонецОбласти

#Область ОбработчикиКомандФормы
//Код процедур и функций
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СформироватьТабДокЛуч()
	
	ТабДок = Новый ТабличныйДокумент;
	
	Макет = РеквизитФормыВЗначение("Объект").ПолучитьМакет("МакетЛУЧ");
	
	ПроектФильтр = 0;
	ОблМакета = Макет.ПолучитьОбласть("ШапкаЛУЧ");
	Если НЕ Фирма.Пустая() Тогда
		ПроектФильтр = ЛояльностьКлиентСерверПовтИсп.ПроектПоМестуРеализации(Фирма.МестоРеализации);
	КонецЕсли;
	ФирмаКратко = ЛояльностьКлиентСерверПовтИсп.МестоРеализацииПоПроекту(ПроектФильтр, Истина, 2);
	продукты = "продукты и напитки";
	Если ПроектФильтр = 1 Тогда
		продукты = "напитки";
	ИначеЕсли ПроектФильтр = 2 Тогда
		продукты = "продукты";
	КонецЕсли;
	
	ОблМакета.Параметры.ФирмаКратко = ФирмаКратко;
	ОблМакета.Параметры.продукты = продукты;
	ТабДок.Вывести(ОблМакета);
	
	Сч = 0;
	Для каждого ТекСтрока Из ЛУЧ Цикл
		Если НЕ Фирма.Пустая() И НЕ Фирма.МестоРеализации = ТекСтрока.МестоРеализации Тогда
			Продолжить;
		КонецЕсли;
		Сч = Сч + 1;
		ОблМакета = Макет.ПолучитьОбласть("СтрокаЛУЧ");
		Если ПустаяСтрока(ТекСтрока.Товар.ДляЦенника) Тогда
			ОблМакета.Параметры.Товар = ТекСтрока.Товар.Наименование;
		Иначе	
			ОблМакета.Параметры.Товар = ТекСтрока.Товар.ДляЦенника;
		КонецЕсли;
		ОблМакета.Параметры.Сч = Сч;
		ОблМакета.Область("R1C3").Картинка = БиблиотекаКартинок["ГруппаАкций" + ТекСтрока.Лого];
		Если ТекСтрока.Проект = 1 ИЛИ ТекСтрока.Проект Тогда
			ОблМакета.Область("R1C1").Картинка = БиблиотекаКартинок["ЛогоЛУЧ" + ТекСтрока.Проект];
		КонецЕсли;
		ТабДок.Вывести(ОблМакета);
	КонецЦикла;
	
	Возврат ТабДок;

КонецФункции


#КонецОбласти
