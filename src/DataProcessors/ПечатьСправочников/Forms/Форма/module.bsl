
Процедура КнопкаВыполнитьНажатие(Кнопка)
	Сформировать()
	
КонецПроцедуры

Процедура ЗаполнитьРеквизиты()
	СписокРеквизитов.Очистить();
	Для Каждого Реквизит Из Метаданные.Справочники[Справочник].Реквизиты Цикл
		Если Реквизит = Метаданные.Справочники.Клиенты.Реквизиты.МобТелефон
			ИЛИ Реквизит = Метаданные.Справочники.Клиенты.Реквизиты.Факс Тогда
			Продолжить;
		КонецЕсли;
		СписокРеквизитов.Добавить(Реквизит,,Истина);
	КонецЦикла;
КонецПроцедуры

Процедура ПриОткрытии()
	
	Если Не ЗначениеЗаполнено(Справочник) Тогда
		Возврат;
	КонецЕсли;	
	СинонимСправочника = Метаданные.Справочники[Справочник].Синоним;
	ПостроительОтчета.Текст = "ВЫБРАТЬ
	|	*
	|ИЗ
	|	Справочник." + Справочник + " АВТОУПОРЯДОЧИВАНИЕ";
	ПостроительОтчета.ЗаполнитьНастройки();							  
	
	ЗаполнитьРеквизиты();  
	ЗаполнитьНачальныеНастройки(СписокРеквизитов);
КонецПроцедуры

Процедура Сформировать()
	ПостроительОтчета.Порядок.Очистить();
	Если НЕ ИерархическийСписок Тогда
		ПостроительОтчета.Порядок.Добавить(Порядок);
	КонецЕсли;
	ПостроительОтчета.Выполнить(); 
	ВыборкаСпр = ПостроительОтчета.Результат.Выбрать();
	
	Таб = Новый ТабличныйДокумент;
	
	Макет = ЭтотОбъект.ПолучитьМакет("Макет");
	
	ОбластьЗаголовок	= Макет.ПолучитьОбласть("Заголовок");
	ОбластьШКод      	= Макет.ПолучитьОбласть("Шапка|Код");
	ОбластьШИмя		 	= Макет.ПолучитьОбласть("Шапка|Имя");
	ОбластьШРеквизит 	= Макет.ПолучитьОбласть("Шапка|Реквизит");
	ОбластьСтрКод	 	= Макет.ПолучитьОбласть("Строка|Код");
	ОбластьСтрИмя		= Макет.ПолучитьОбласть("Строка|Имя");
	ОбластьСтрРеквизит 	= Макет.ПолучитьОбласть("Строка|Реквизит");
	ОбластьГалка 		= Макет.ПолучитьОбласть("Строка|Галка");
	ПустаяЯчейка		= Макет.ПолучитьОбласть("Строка|Пусто");
	
	ШрифтК = Новый Шрифт("Arial",9,Истина, Истина);
	
	ОбластьЗаголовок.Параметры.Заголовок = "Справочник """ + СинонимСправочника + """";
	ОбластьЗаголовок.Параметры.СтрУсловие = "";
	Таб.Вывести(ОбластьЗаголовок);
	
	Таб.Вывести(ОбластьШКод);
	Таб.Присоединить(ОбластьШИмя);
	
	Для Каждого Реквизит Из СписокРеквизитов Цикл
		Если Не Реквизит.Пометка Тогда
			Продолжить;
		КонецЕсли;	
		ОбластьШРеквизит.Параметры.Реквизит = Реквизит.Значение;
		Таб.Присоединить(ОбластьШРеквизит);
	КонецЦикла;
	
	Пока ВыборкаСпр.Следующий() Цикл
		
		Ур = ВыборкаСпр.Ссылка.Уровень();
		Если ВыборкаСпр.ЭтоГруппа Тогда
			ОбластьСтрКод.ТекущаяОбласть.ЦветФона = WebЦвета.СветлоЖелтый;
			ОбластьСтрИмя.ТекущаяОбласть.ЦветФона = WebЦвета.СветлоЖелтый;
			ОбластьСтрРеквизит.ТекущаяОбласть.ЦветФона = WebЦвета.СветлоЖелтый;
			ОбластьСтрКод.ТекущаяОбласть.Шрифт = ШрифтК;
			ОбластьСтрИмя.ТекущаяОбласть.Шрифт = ШрифтК;
		Иначе
			ОбластьСтрКод.ТекущаяОбласть.ЦветФона = WebЦвета.Белый;
			ОбластьСтрИмя.ТекущаяОбласть.ЦветФона = WebЦвета.Белый;
			ОбластьСтрРеквизит.ТекущаяОбласть.ЦветФона = WebЦвета.Белый;
			ОбластьСтрКод.ТекущаяОбласть.Шрифт = ШрифтыСтиля.Шрифт8;
			ОбластьСтрИмя.ТекущаяОбласть.Шрифт = ШрифтыСтиля.Шрифт8;
		КонецЕсли;	
		
		ОбластьСтрКод.Параметры.Код = ВыборкаСпр.Код;
		Таб.Вывести(ОбластьСтрКод, Ур);
		
		ОбластьСтрИмя.Параметры.Имя = ВыборкаСпр.Наименование;
		Таб.Присоединить(ОбластьСтрИмя);
		
		Для Каждого Реквизит Из СписокРеквизитов Цикл
			Если Не Реквизит.Пометка Тогда
				Продолжить;
			КонецЕсли;	
			Если ТипЗнч(ВыборкаСпр[Реквизит.Значение.Имя]) = Тип("Булево") Тогда
				Если ВыборкаСпр[Реквизит.Значение.Имя] Тогда
					Таб.Присоединить(ОбластьГалка);
				Иначе
					Таб.Присоединить(ПустаяЯчейка);
				КонецЕсли;	
			ИначеЕсли Реквизит.Значение = Метаданные.Справочники.Клиенты.Реквизиты.Адрес
				ИЛИ Реквизит.Значение = Метаданные.Справочники.Клиенты.Реквизиты.Email
				ИЛИ Реквизит.Значение = Метаданные.Справочники.Клиенты.Реквизиты.Web
				ИЛИ Реквизит.Значение = Метаданные.Справочники.Клиенты.Реквизиты.Телефон Тогда
				
				Запрос = Новый Запрос;
				Запрос.Текст = 
					"ВЫБРАТЬ
					|	КонтактнаяИнформация.Представление
					|ИЗ
					|	Справочник.КонтактнаяИнформация КАК КонтактнаяИнформация
					|ГДЕ
					|	КонтактнаяИнформация.Объект = &Объект
					|	И КонтактнаяИнформация.Тип = &Тип";

				Если Реквизит.Значение = Метаданные.Справочники.Клиенты.Реквизиты.Email Тогда
					Тип = "АдресЭлектроннойПочты";
				ИначеЕсли Реквизит.Значение = Метаданные.Справочники.Клиенты.Реквизиты.Web Тогда
					Тип = "ВебСтраница";
				Иначе
					Тип = Строка(Реквизит.Значение);
				КонецЕсли;
				
				Запрос.УстановитьПараметр("Объект", ВыборкаСпр.Ссылка);
				Запрос.УстановитьПараметр("Тип", Перечисления.ТипыКонтактнойИнформации[Тип]);

				Результат = Запрос.Выполнить();

				ВыборкаДетальныеЗаписи = Результат.Выбрать();
                ОбластьСтрРеквизит.Параметры.Реквизит = "";
				ОбластьСтрРеквизит.ТекущаяОбласть.АвтоВысотаСтроки	= Ложь;
				
				Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
					ОбластьСтрРеквизит.Параметры.Реквизит = ОбластьСтрРеквизит.Параметры.Реквизит + ВыборкаДетальныеЗаписи.представление + Символы.ПС; 
				КонецЦикла;
				ОбластьСтрРеквизит.ТекущаяОбласть.ВысотаСтроки = ОбластьСтрРеквизит.ТекущаяОбласть.ВысотаСтроки * СтрЧислоСтрок(ОбластьСтрРеквизит.Параметры.Реквизит);
				
				Таб.Присоединить(ОбластьСтрРеквизит);

			Иначе	
				ОбластьСтрРеквизит.Параметры.Реквизит = ВыборкаСпр[Реквизит.Значение.Имя];
				Таб.Присоединить(ОбластьСтрРеквизит);
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;	
	
	Таб.ОтображатьСетку = Ложь;
	
	Таб.Показать();
	
КонецПроцедуры

Процедура УстановитьФлажки(Кнопка)
	Для Каждого Реквизит Из СписокРеквизитов Цикл
		Реквизит.Пометка = Истина;
	КонецЦикла;	
КонецПроцедуры

Процедура СнятьФлажки(Кнопка)
	Для Каждого Реквизит Из СписокРеквизитов Цикл
		Реквизит.Пометка = Ложь;
	КонецЦикла;	
КонецПроцедуры

// процедура назначается динамически
Процедура КоманднаяПанель2Действие3(Кнопка)
	Закрыть();
КонецПроцедуры

Процедура ИерархическийСписокПриИзменении(Элемент)
	ЭлементыФормы.Порядок.Доступность = Не ИерархическийСписок;
	
КонецПроцедуры

