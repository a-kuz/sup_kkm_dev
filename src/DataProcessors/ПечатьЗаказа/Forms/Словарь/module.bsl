Перем СписокЯзыков;

// Прочитать словарь служебных слов 
Процедура ПрочитатьСловарь(ЗагрузитьПеревод=Ложь)  Экспорт
	
	СписокЯзыков = Новый СписокЗначений;
	СпрЯзыки = Справочники.Языки.Выбрать();
	Пока СпрЯзыки.Следующий() Цикл 
		Если НЕ СпрЯзыки.Ссылка.ПометкаУдаления Тогда
			СписокЯзыков.Добавить(СпрЯзыки.Ссылка,СпрЯзыки.Код);
		КонецЕсли;
	КонецЦикла;
	
	Если СписокЯзыков.Количество()=0 Тогда
		Предупреждение("Сначала надо заполнить справочник языков...",3);
		Справочники.Языки.ПолучитьФормуСписка().Открыть();
		Возврат;
	КонецЕсли;
	
	Индекс = 1;
	
	Если Словарь.Колонки.Количество()>1 Тогда
		Для н=2 по Словарь.Колонки.Количество() Цикл
			Словарь.Колонки.Удалить(1);
		КонецЦикла;
		Словарь.Очистить();
	КонецЕсли;
	
	
	Для Каждого Язык ИЗ СписокЯзыков Цикл
		Словарь.Колонки.Добавить("Перевод"+Индекс,Новый ОписаниеТипов("Строка"),Язык.Представление,30);
		ИмяКолонки =  "Ссылка"+Индекс;
		Словарь.Колонки.Добавить(ИмяКолонки);
		Индекс = Индекс+1;
	КонецЦикла;	
	
	Таб = ПолучитьМакет("СлужебныеСлова");
	
	Если ЗагрузитьПеревод Тогда
		ТабКодовЯзыков = Новый ТаблицаЗначений;
		ТабКодовЯзыков.Колонки.Добавить("Код");
		ТабКодовЯзыков.Колонки.Добавить("НомерКолонки");
		Для нк=2 По Таб.ШиринаТаблицы  Цикл
			НоваяСтрока = ТабКодовЯзыков.Добавить();
			НоваяСтрока.Код = СокрЛП(Таб.Область(1,нк).Текст);
			НоваяСтрока.НомерКолонки = нк;
		КонецЦикла;
	КонецЕсли;
	
	Для нс = 2 По Таб.ВысотаТаблицы Цикл
		Оригинал = СокрЛП(Таб.Область(нс,1).Текст);
		СтрокаСловаря = Словарь.Добавить();
		СтрокаСловаря.Оригинал = Оригинал;
		нк=1;
		Для Каждого Язык Из СписокЯзыков Цикл
			СсылкаПеревод = Перевод(Оригинал,Язык.Значение,2);
			Если  ЗагрузитьПеревод И Найти(Данные,Язык.Значение.Код+",")>0 Тогда
				ЗначПеревода = "";
				н=0;
				СтрокаПоиска = ТабКодовЯзыков.Найти(СокрЛП(Язык.Значение.Код),"Код");
				Если СтрокаПоиска <> Неопределено Тогда
					ЗначПеревода = СокрЛП(Таб.Область(нс,СтрокаПоиска.НомерКолонки).Текст);
				КонецЕсли;
			Иначе
				ЗначПеревода = СокрЛП(СсылкаПеревод);
			КонецЕсли;
			
			КолонкаПеревод = "Перевод"+нк;
			КолонкаСсылка = "Ссылка"+нк;
			
			СтрокаСловаря[КолонкаПеревод] = ЗначПеревода;
			СтрокаСловаря[КолонкаСсылка] = СсылкаПеревод;
			нк=нк+1;
		КонецЦикла;
	КонецЦикла;
	
	ЭлементыФормы.Словарь.СоздатьКолонки();
	ЭлементыФормы.Словарь.Колонки.Оригинал.Доступность = Ложь;
	Для н = 1 по СписокЯзыков.Количество() Цикл
		КолонкаСсылка = ЭлементыФормы.Словарь.Колонки.Найти("Ссылка"+н);
		КолонкаСсылка.Видимость = ложь;
	КонецЦикла;
КонецПроцедуры

Процедура ОК() Экспорт
	Для Каждого СтрокаСловаря ИЗ Словарь Цикл
		Для нк=1 По СписокЯзыков.Количество() Цикл
			КолонкаПеревод = "Перевод"+нк;
			КолонкаСсылка = "Ссылка"+нк;
			Ссылка = СтрокаСловаря[КолонкаСсылка];
			ЗначПеревода = СтрокаСловаря[КолонкаПеревод];
			
			Если Ссылка = Неопределено Тогда
				// не было такой записи
				Если ЗначениеЗаполнено(ЗначПеревода) Тогда
					НоваяЗапись = РегистрыСведений.СловарьСлужебныхСлов.СоздатьМенеджерЗаписи();
					НоваяЗапись.ОригиналСтр = СтрокаСловаря["Оригинал"];
					НоваяЗапись.Перевод     = ЗначПеревода;
					НоваяЗапись.Язык        = СписокЯзыков[нк-1].Значение;
					НоваяЗапись.Записать();
				КонецЕсли;
			Иначе
				Запись = РегистрыСведений.СловарьСлужебныхСлов.СоздатьМенеджерЗаписи();
				Запись.ОригиналСтр = СтрокаСловаря["Оригинал"];
				Запись.Язык        = СписокЯзыков[нк-1].Значение;
				Запись.Прочитать();
				Если ЗначениеЗаполнено(ЗначПеревода) Тогда
					Запись.Перевод = ЗначПеревода;
					Запись.Записать();
				Иначе
					Запись.Удалить();
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Модифицированность = Ложь;
КонецПроцедуры

Процедура ОКНажатие(Элемент)
	Если Модифицированность Тогда
		ОК();
	КонецЕсли;
	Закрыть();
КонецПроцедуры

Процедура ОсновныеДействияФормыОбновитьСловарь(Кнопка)
	ФормаСпискаЯзыков = Справочники.Языки.ПолучитьФормуСписка();
	Если ФормаСпискаЯзыков.Установить(Истина) Тогда
		ПрочитатьСловарь();
	КонецЕсли;
КонецПроцедуры

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	Если Модифицированность Тогда
		Ответ = Вопрос("Сохранить изменения?",РежимДиалогаВопрос.ДаНетОтмена);
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ОК();
		ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

