
Перем ПараметрыТО Экспорт;   // Параметры торгового оборудования.
Перем Результат Экспорт;     // Результат выполнения действия.
Перем DRV Экспорт;           // Драйвер
Перем КодОшибки, ОписаниеОшибки;

#Если Клиент Тогда

// Производит инициализацию торгового оборудования.
//
Процедура Инициализация() Экспорт
	
	// это наш драйвер, зашит в компоненту защиты
	DRV = РаботаСокнами;
	
	ПрочитатьПараметр("ИспользоватьСервер"	, Ложь );
	ПрочитатьПараметр("СерверIPадрес"		, "" );
	ПрочитатьПараметр("СерверIPпорт"		, 5558 );
	ПрочитатьПараметр("ПортПодключения"		, 1 );
	
КонецПроцедуры

// Выполняет чтение параметра ТО.
//
// Параметры:
//  ИмяПараметра        - имя параметра,
//  ЗначениеПоУмолчанию - значение по умолчанию для данного параметра.
//
// Возвращаемое значение:
//  Значение параметра или значение по умолчанию
//
Процедура ПрочитатьПараметр(ИмяПараметра,ЗначениеПоУмолчанию)
	
	Если НЕ ПараметрыТО.Свойство(ИмяПараметра) Тогда
		ПараметрыТО.Вставить(ИмяПараметра,ЗначениеПоУмолчанию);
	КонецЕсли; 
	
	ЭтотОбъект[ИмяПараметра] = ПараметрыТО[ИмяПараметра];
	
КонецПроцедуры

// Выполняет действие с ТО.
//
// Параметры:
//  Действие - имя действия,
//  ПараметрыДействия - произвольный набор параметров
//
Процедура ВыполнитьДействие(Действие, ПараметрыДействия=Неопределено) Экспорт
	
	Если Действие = "Подключить" Тогда
		// ничего делать не надо
		
	ИначеЕсли Действие = "Отключить" Тогда
		// ничего делать не надо
		
	ИначеЕсли Действие = "РазрешитьВсе" Тогда
		РазрешитьВсе();
		
	ИначеЕсли Действие = "ЗапретитьВсе" Тогда
		ЗапретитьВсе();
		
	ИначеЕсли Действие = "ДобавитьПозицию" Тогда
		ДобавитьПозицию( ПараметрыДействия.НомерПозиции, ПараметрыДействия.Количество);
		
	ИначеЕсли Действие = "УдалитьПозицию" Тогда
		УдалитьПозицию( ПараметрыДействия.НомерПозиции, ПараметрыДействия.Количество);
		
	ИначеЕсли Действие = "ДобавитьТаблицуПозиций" Тогда
		ДобавитьТаблицуПозиций( ПараметрыДействия.ТаблицаПозиций, ПараметрыДействия.Количество);
		
	ИначеЕсли Действие = "УдалитьТаблицуПозиций" Тогда
		УдалитьТаблицуПозиций( ПараметрыДействия.ТаблицаПозиций, ПараметрыДействия.Количество);
		
	Иначе
		Результат.Ошибка	= Истина;
		Результат.Описание	= "Неизвестная команда!";
		Результат.Подробно	= "Команда """+Действие+""" не определена для "+ТО.Наименование;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаОшибки()
	
	Если КодОшибки=0 Тогда
		Возврат;
	КонецЕсли; 
	
	ЗарегистрироватьСобытие("Торговое оборудование.Ошибка", УровеньЖурналаРегистрации.Ошибка, ТО.Метаданные(), ТО.Ссылка, ОписаниеОшибки);
	
	Результат.Ошибка = Истина;
	Результат.Описание = "Ошибка системы розлива!";
	Результат.Подробно = ОписаниеОшибки;
	
КонецПроцедуры

Процедура Подключить()
	
	Если НЕ ИспользоватьСервер Тогда
		СерверIPадрес = "";
		СерверIPпорт = 0;
	КонецЕсли;
	
	Результат.Ошибка = Ложь;
	
КонецПроцедуры

Процедура РазрешитьВсе()  
	
	Подключить();
	
	DRV.AddAll( СерверIPадрес, СерверIPпорт, КодОшибки, ОписаниеОшибки); 
	
	ОбработкаОшибки();
	
КонецПроцедуры

Процедура ЗапретитьВсе()
	
	Подключить();
	
	DRV.StopAll( СерверIPадрес, СерверIPпорт, КодОшибки, ОписаниеОшибки);
	
	ОбработкаОшибки();
	
КонецПроцедуры

Процедура ДобавитьПозицию(НомПоз, Колво=1)
	
	Подключить();
	
	Для н=1 По Колво Цикл
		DRV.AddPos( СерверIPадрес, СерверIPпорт, НомПоз, 0 ,КодОшибки, ОписаниеОшибки); 
	КонецЦикла;
	
	ОбработкаОшибки();
	
КонецПроцедуры

Процедура УдалитьПозицию(НомПоз, Колво=1)
	
	Подключить();
	
	Для н=1 По Колво Цикл
		DRV.DelPos( СерверIPадрес, СерверIPпорт, НомПоз, 0 ,КодОшибки, ОписаниеОшибки); 
	КонецЦикла;
	
	ОбработкаОшибки();
	
КонецПроцедуры

Процедура ДобавитьТаблицуПозиций(ТаблицаПозиций, Колво)
	
	Для каждого СтрокаТаблицы Из ТаблицаПозиций Цикл
		ДобавитьПозицию(СтрокаТаблицы.НомерПозиции, СтрокаТаблицы.Количество * Колво)
	КонецЦикла; 
	
КонецПроцедуры
 
Процедура УдалитьТаблицуПозиций(ТаблицаПозиций, Колво)
	
	Для каждого СтрокаТаблицы Из ТаблицаПозиций Цикл
		УдалитьПозицию(СтрокаТаблицы.НомерПозиции, СтрокаТаблицы.Количество * Колво)
	КонецЦикла; 
	
КонецПроцедуры
 
#КонецЕсли

Результат = Новый Структура("Ошибка,Описание,Подробно", Ложь,"","");
