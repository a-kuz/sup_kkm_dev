////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИУИ СОБЫТИЙ ФОРМЫ

// Обработчик события ПриОткрытии формы.
//
Процедура ПриОткрытии()
	
	Если ПустаяСтрока(Подпись) Тогда
		Подпись = ВосстановитьЗначение("ПодписьСообщенияОтправкаСообщенияРазработчикам");
	КонецЕсли;
	
	Если ВидСообщения = 0 Тогда
		ВидСообщения = 2;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Обработчик события нажатия кнопки Выполнить КП.
//
Процедура КнопкаВыполнитьНажатие(Кнопка)
	
	Почта = Новый Почта;
	
	Сообщение = Новый ПочтовоеСообщение;
	Сообщение.Получатели.Добавить(ЭлАдресРазработчика);
	
	Сообщение.Текст = СформироватьТекстИнформацииОСистеме() + СокрЛП(ТекстСообщения);
	Если Не ПустаяСтрока(Подпись) Тогда
		Сообщение.Текст = Сообщение.Текст + Символы.ПС + Символы.ПС + СокрЛП(Подпись);
	КонецЕсли;
	
	Сообщение.Тема = СформироватьПрефиксТемыСообщения() + ТемаСообщения;
	
	Состояние("Подключение к основной почтовой программе...");
	Попытка
		Почта.Подключиться();
	Исключение
		Сообщить("Не удалось подключиться к основной почтовой программе.");
		Сообщить(ОписаниеОшибки());
		Возврат;
	КонецПопытки;
	
	Состояние("Отправка сообщения...");
	Попытка
		Почта.Послать(Сообщение);
	Исключение
		Сообщить("Не удалось отправить сообщение.");
		Сообщить(ОписаниеОшибки());
		Возврат;
	КонецПопытки;                  
	
	Предупреждение("Сообщение отправлено.",3);
	СохранитьЗначение("ПодписьСообщенияОтправкаСообщенияРазработчикам", Подпись);
	Закрыть();
	
КонецПроцедуры