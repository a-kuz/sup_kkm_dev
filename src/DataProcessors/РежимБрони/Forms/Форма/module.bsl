
Перем ПараметрыОкна Экспорт;	// структура, определяет положение и размеры окна
Перем ГлавнаяФорма;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ МОДУЛЯ

Процедура ПриИзмененииВремени()
	
	Если РежимБронирования Тогда
		ГлавнаяФорма.БроньВремя1 = Время1;
		ГлавнаяФорма.БроньВремя2 = Время2;
		ГлавнаяФорма.ОбновитьПланЗала();
	КонецЕсли; 
	
КонецПроцедуры

Процедура ВыбратьВремя(ИсхВремя)
	
	Время = ИнтерфейсРМ.ВводВремени("Время брони:", ИсхВремя);
	
	Если Время=Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ИсхВремя = Время;
	
	ПриИзмененииВремени();
	
КонецПроцедуры

Процедура ИзменитьВремя(ИсхВремя, Сдвиг, ДопВремя=Неопределено)
	
	ИсхВремя = ИсхВремя + Сдвиг*60;
	
	Если ЗначениеЗаполнено(ДопВремя) Тогда
		ДопВремя = ДопВремя + Сдвиг*60;
	КонецЕсли;
	
	ПриИзмененииВремени();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если РежимБронирования Тогда
		
		//ГлавнаяФорма = глСтекОкон[глСтекОкон.Количество()-1];
		ГлавнаяФорма = глСтекОкон[глСтекОкон.Количество()-1].Форма;
		Если НЕ ГлавнаяФорма.ПланЗалаЗагружен Тогда
			Текст1="Нет доступа!";
			Текст2="Режим бронирования работает только совместно с планом зала...";
			ИнтерфейсРМ.ВопросПредупреждение("Ошибка",Текст1,Текст2,"","ОК","");
			Отказ = Истина;
			Возврат;
		КонецЕсли;
		
		ГлавнаяФорма.ОпределитьПараметрыОкнаБронирования(ПараметрыОкна, Ширина, Высота);
		ГлавнаяФорма.ВыполнитьДействияПриВозврате = Истина;
		
		ЭтаФорма.Стиль = БиблиотекаСтилей[глПараметрыРМ.ИмяСтиля];
		
		Время1 = НачалоЧаса(ТекущаяДата()) + 60*60;
		Время2 = Время1 + 60*60;
		КоличествоПосетителей = 1;
		
	Иначе
		
		Если ТекущаяБронь.Пустая() Тогда
			Время1 = НачалоЧаса(ТекущаяДата()) + 60*60;
			Время2 = Время1 + 60*60;
			КоличествоПосетителей = 1;
			
		Иначе
			Если НЕ ПрочитатьБронь() Тогда
				Отказ = Истина;
				Возврат;
			КонецЕсли; 
			
		КонецЕсли; 
		
		ПараметрыОкна.Центр = Истина;
		
	КонецЕсли; 
	
	ПриИзмененииВремени();
		
КонецПроцедуры

Процедура ПриОткрытии()
	
	ИнтерфейсРМ.ПриОткрытииОкна(ЭтаФорма);
	
	Флаг = ЗначениеЗаполнено(Комментарий);
	ЭлементыФормы.КомментарийСтр.Видимость		= Флаг;
	ЭлементыФормы.КнопкаКомментарий.Видимость	= НЕ Флаг;		
	
КонецПроцедуры

Процедура ПриЗакрытии()
	
	Если РежимБронирования Тогда
		ГлавнаяФорма.БроньВремя1 = Неопределено;
		ГлавнаяФорма.БроньВремя2 = Неопределено;
	Иначе
		Оповестить("ВыходИзБрони", ТекущаяБронь);
	КонецЕсли;
	
	РаботаСокнами.CloseScreenKey();
	
	ИнтерфейсРМ.ПриЗакрытииОкна();
	
КонецПроцедуры

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	ДействияПередЗакрытиемФормы(ЭтаФорма, Отказ, ГлавнаяФорма);
КонецПроцедуры

Процедура ОбновлениеОтображения()
	
	глОтсечкаПростоя();
	
	Заголовок = ?(ТекущаяБронь.Пустая(), "Новая бронь", "Бронь № "+ТекущаяБронь.Номер);
	
	Если ПосадочноеМесто.Пустая() Тогда
		ЭлементыФормы.КнопкаМесто.Заголовок = "выберите место...";
	Иначе
		ЭлементыФормы.КнопкаМесто.Заголовок = НаимПосадочногоМеста(ПосадочноеМесто);
	КонецЕсли;
	
	Если Клиент.Пустая() Тогда
		ЭлементыФормы.КлиентСтр.Видимость	= Истина;
		ЭлементыФормы.тКлиент.Видимость		= Ложь;
	Иначе
		
		ЭлементыФормы.КлиентСтр.Видимость	= Ложь;
		ЭлементыФормы.тКлиент.Видимость		= Истина;
		ЭлементыФормы.тКлиент.Заголовок		= Клиент.Наименование;
	КонецЕсли;
	
	СуммаПредоплаты = НакопленияКлиента(Клиент,ТекущаяБронь).СуммаБезнал;
	ЭлементыФормы.тПредоплата.Заголовок		= ?(СуммаПредоплаты=0, "Нет предоплаты", "Предоплата: "+ФорматСумм(СуммаПредоплаты, глРубли));
	
	ЭлементыФормы.тКолвоГостей.Заголовок = Строка(КоличествоПосетителей) + " чел.";
	
	Если ТаблицаЗаказа.Строки.Количество()=0 Тогда
		ЭлементыФормы.тПредварительноеМеню.Заголовок = "нет заказа";
	Иначе
		ЭлементыФормы.тПредварительноеМеню.Заголовок = "Позиций: " + ТаблицаЗаказа.Строки.Количество() + Символы.ПС+
		"Сумма: " + ФорматСумм(ТаблицаЗаказа.Строки.Итог("Сумма"));
	КонецЕсли;
	
КонецПроцедуры

Процедура ВнешнееСобытие(Источник, Событие, Данные)
	
	глОтсечкаПростоя();
	
	Если Источник="SBLayoutEditor" И Событие="ObjectClicked" Тогда
		
		ИДОбъекта = Данные;
		ТипОбъекта = "";
		ДанныеОбъекта = "";;
		ГлавнаяФорма.SBLayoutEditor.МодельПолучитьТипОбъекта(ИДОбъекта, ТипОбъекта);
		
		Если ТипОбъекта = "ПосадочноеМесто" Тогда
			
			ГлавнаяФорма.SBLayoutEditor.МодельПолучитьДанныеОбъекта(ИДОбъекта, ДанныеОбъекта);
			
			КодМеста = Число(ДанныеОбъекта);
			ВыбМесто = НайтиМестоПоКоду( КодМеста );
			
			Если ВыбМесто.Пустая() Тогда
				Текст1="Нет доступа!";
				Текст2="Посадочного места № "+КодМеста+" нет!
						|Обратитесь к администратору...";
				ИнтерфейсРМ.ВопросПредупреждение("Ошибка",Текст1,Текст2,"","ОК","");
				Возврат;
			КонецЕсли;
			
		ПосадочноеМесто = ВыбМесто;
			
		КонецЕсли; 
		
	Иначе
		
		_Знач = ОбработкаВнешнихСобытий.ПолучитьДанные(Источник,Событие,Данные);
		Если НЕ ЗначениеЗаполнено(_Знач) Тогда
			Возврат;
		КонецЕсли;
		
		ТипПривязки = Новый ОписаниеТипов("СправочникСсылка.Клиенты");
		ФлагПовтора = Ложь;
		_Объект = ИнтерфейсРМ.ИдентификацияПоКарте("Идентификатор_"+_Знач, ТипПривязки, ФлагПовтора);
		
		Если НЕ ЗначениеЗаполнено(_Объект) Тогда
			Возврат;
		КонецЕсли;
		
		Клиент = _Объект;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия="ВыходИзЗаказа" И РежимБронирования Тогда
		ГлавнаяФорма.SBLayoutEditor.МодельУстановитьВидимость(1);
	КонецЕсли; 
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура КнопкаМестоНажатие(Элемент)
	
	ВыбМесто = ИнтерфейсРМ.ВыборПосадочногоМеста(Ложь);
	Если ВыбМесто=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПосадочноеМесто = ВыбМесто;
	
	Если НЕ ПосадочноеМесто.Клиент.Пустая() Тогда
		Клиент = ПосадочноеМесто.Клиент;
	КонецЕсли; 
	
КонецПроцедуры

Процедура КнопкаКлиентНажатие(Элемент)
	
	СуммаПредоплаты = НакопленияКлиента(Клиент,ТекущаяБронь).СуммаБезнал;
	Если СуммаПредоплаты > 0 Тогда
		Текст1="Нет доступа!";
		Текст2="Внесена предоплата, клиента изменить нельзя!";
		ИнтерфейсРМ.ВопросПредупреждение("Ошибка",Текст1,Текст2,"","ОК","");
		Возврат;
	КонецЕсли;
		
	ВыбКлиент = ИнтерфейсРМ.ИдентификацияКлиента();
	Если ВыбКлиент=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Клиент = ВыбКлиент;
	
КонецПроцедуры

Процедура КнопкаРучнойВводНажатие(Элемент)
	
	Клиент = Неопределено;
	
	ЭлементыФормы.КлиентСтр.Видимость	= Истина;
	ТекущийЭлемент = ЭлементыФормы.КлиентСтр;
	
	ИнтерфейсРМ.ЭкраннаяКлавиатура();
	
КонецПроцедуры

Процедура КнопкаОплатаНажатие(Элемент)
	
	Если ТекущаяБронь.Пустая() И НЕ ЗаписатьБронь() Тогда
		Возврат;
	КонецЕсли;
	
	ЗнакОперации = 1;
	// если предоплата уже была введена, то надо спросить что хотят сделать
	СуммаПредоплаты = НакопленияКлиента(Клиент,ТекущаяБронь).СуммаБезнал;
	Если СуммаПредоплаты > 0 Тогда
		
		Текст1="Предоплата уже внесена!";
		Текст2="Сумма предоплаты: "+ФорматСумм(СуммаПредоплаты)+"
				|Внести еще или вернуть?";
		
		Ответ = ИнтерфейсРМ.ВопросПредупреждение("Вопрос",Текст1,Текст2,"Внести","Вернуть","Esc=Отмена");
		Если Ответ="Отмена" Тогда
			Возврат;
		ИначеЕсли Ответ="Вернуть" Тогда
			ЗнакОперации = -1;
		КонецЕсли;
	
	КонецЕсли; 
	
	ВводНачислений = ИнтерфейсРМ.ПолучитьОбъектОбработки("ВводНачислений");
	ВводНачислений.ЗнакОперации = ЗнакОперации;
	ВводНачислений.Клиент		= Клиент;
	ВводНачислений.КлиентСтр	= КлиентСтр;
	ВводНачислений.ДокОснование = ТекущаяБронь;
	
	ВводНачислений.ПолучитьФорму().ОткрытьМодально();
	
КонецПроцедуры

Процедура КнопкаКолвоГостейНажатие(Элемент)
	
	Колво = ИнтерфейсРМ.ВводЧисла("Кол-во посетителей", "Число", 3, 0, КоличествоПосетителей);
	
	Если Колво=Неопределено ИЛИ Колво=0 Тогда
		Возврат;
	КонецЕсли;
	
	КоличествоПосетителей = Колво;
	
КонецПроцедуры

Процедура КнопкаМенюНажатие(Элемент)
	
	Если РежимБронирования Тогда
		ГлавнаяФорма.SBLayoutEditor.МодельУстановитьВидимость(0);
	КонецЕсли; 
	
	УстановитьТипЦен();
	
	ОбработкаЗаказ.ОткрытьРабочуюФормуЗаказа(ЭтаФорма);
	
КонецПроцедуры

Процедура КнопкаПечатьНажатие(Элемент)
	
	Если НЕ ЗаписатьБронь() Тогда
		Возврат;
	КонецЕсли; 
	
	глОжидание.Начало("Выполняется печать...",
					"Идет печать заданий на сервис-принтерах.
					|Пожалуйста, подождите...");
	
	ОбработкаПечати = ИнтерфейсРМ.ПолучитьОбъектОбработки("ПечатьЗаказа");
	ОбработкаПечати.Бронь = ТекущаяБронь;
	
	ОбработкаПечати.ПечатьБрони();
	
	глОжидание.Конец();
	
	Закрыть();
	
КонецПроцедуры

Процедура КнопкаОКНажатие(Элемент)
	
	Если ЗаписатьБронь() Тогда
		Закрыть();
	КонецЕсли; 
	
КонецПроцедуры

Процедура Дата1ПриИзменении(Элемент)
	
	ПриИзмененииВремени();
	
КонецПроцедуры

Процедура КнопкаДата1НазадНажатие(Элемент)
	
	ИзменитьВремя( Время1, -24*60, Время2);
	
КонецПроцедуры

Процедура КнопкаДата1ВпередНажатие(Элемент)
	
	ИзменитьВремя( Время1, 24*60, Время2);
	
КонецПроцедуры

Процедура КнопкаДата2НазадНажатие(Элемент)
	
	ИзменитьВремя( Время2, -24*60);
	
КонецПроцедуры

Процедура КнопкаДата2ВпередНажатие(Элемент)
	
	ИзменитьВремя( Время2, 24*60);
	
КонецПроцедуры

Процедура КнопкаВремя1НазадНажатие(Элемент)
	
	ИзменитьВремя( Время1, -15, Время2);
	
КонецПроцедуры

Процедура КнопкаВремя1ВпередНажатие(Элемент)
	
	ИзменитьВремя( Время1, 15, Время2);
	
КонецПроцедуры

Процедура КнопкаВремя2НазадНажатие(Элемент)
	
	ИзменитьВремя( Время2, -15);
	
КонецПроцедуры

Процедура КнопкаВремя2ВпередНажатие(Элемент)
	
	ИзменитьВремя( Время2, 15);
	
КонецПроцедуры

Процедура КнопкаВремя1ВыбратьНажатие(Элемент)
	
	ВыбратьВремя(Время1);
	
КонецПроцедуры

Процедура КнопкаВремя2ВыбратьНажатие(Элемент)
	
	ВыбратьВремя(Время2);
	
КонецПроцедуры

Процедура КнопкаКомментарийНажатие(Элемент)
	
	ЭлементыФормы.КомментарийСтр.Видимость		= Истина;
	ЭлементыФормы.КнопкаКомментарий.Видимость	= Ложь;
	
	ТекущийЭлемент = ЭлементыФормы.КомментарийСтр;
	
	ИнтерфейсРМ.ЭкраннаяКлавиатура();
	
КонецПроцедуры

Процедура КомментарийСтрПриИзменении(Элемент)
	ВидимостьКнопкиКомментария()
КонецПроцедуры

Процедура ВидимостьКнопкиКомментария()
	
	Флаг = ЗначениеЗаполнено(Комментарий);
	ЭлементыФормы.КомментарийСтр.Видимость		= Флаг;
	ЭлементыФормы.КнопкаКомментарий.Видимость	= НЕ Флаг;		
	
КонецПроцедуры	


////////////////////////////////////////////////////////////////////////////////
// ТЕЛО МОДУЛЯ

ПараметрыОкна = Новый Структура("Центр, Лево, Верх, Ширина, Высота", Ложь);

