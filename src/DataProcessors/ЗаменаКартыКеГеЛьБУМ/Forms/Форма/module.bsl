
Перем ПараметрыОкна Экспорт;	// структура, определяет положение и размеры окна

////////////////////////////////////////////////////////////////////////////////
// ОБЩИЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	ЭлементыФормы.тФИО.Заголовок 			= Клиент.Наименование;
	ЭлементыФормы.тДатаРождения.Заголовок 	= Формат(Клиент.ДатаРождения, "ДФ=dd.MM.yyyy");
	ЭлементыФормы.тКартаДоступа.Заголовок 	= КартаДоступа.Наименование;
	
КонецПроцедуры

Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если глФлагБлокировка И Модифицированность Тогда
		
		Отказ = Истина;
		глФлагБлокировка = Ложь;
		
		БылПользователь = глПользователь;
		глПользователь = Справочники.Сотрудники.ПустаяСсылка();	// чтобы не сработала повторно автоблокировка
		глПользователь = ИнтерфейсРМ.ИдентификацияПользователя();
		
		Пока НЕ( глПользователь=БылПользователь ИЛИ ИнтерфейсРМ.ПроверкаПраваДоступа("ЗаменаКартыКеГеЛьБУМ", Истина) ) Цикл
			глПользователь = ИнтерфейсРМ.ИдентификацияПользователя();
		КонецЦикла;
		
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	ИнтерфейсРМ.ПриОткрытииОкна(ЭтаФорма);
	
КонецПроцедуры

Процедура ПриЗакрытии()
	
	ИнтерфейсРМ.ПриЗакрытииОкна();
	
КонецПроцедуры

Процедура ВнешнееСобытие(Источник, Событие, Данные)
	
	Если НЕ ВводДоступен() Тогда
		Возврат;
	КонецЕсли; 
	
	_Знач = ОбработкаВнешнихСобытий.ПолучитьДанные(Источник,Событие,Данные);
	Если НЕ ЗначениеЗаполнено(_Знач) Тогда
		Возврат;
	КонецЕсли;
	
	// проверка - карты КеГеЛьБУМ не регистрируются в справочнике карт доступа. 
	// Проверим, что считанной карты нет в справочнике.
	
	КодПоиска = СокрЛП(_Знач);
	КартаДоступаСсылка = Справочники.КартыДоступа.НайтиПоРеквизиту("Идентификатор",КодПоиска);
	
	Если НЕ КартаДоступаСсылка.Пустая() Тогда
		Текст1="Это не КеГеЛьБУМ!";
		Текст2="Считана "+КартаДоступаСсылка.ТипКарты+", а не карта КеГеЛьБУМ!"; 
		ИнтерфейсРМ.ВопросПредупреждение("Ошибка",Текст1,Текст2,"","ОК","");
		Возврат;
	КонецЕсли; 
	
	ИдентификаторКеГеЛьБУМ = СокрЛП(_Знач);
	
	
КонецПроцедуры

Процедура ОбновлениеОтображения()
	
	глОтсечкаПростоя();
	
	ЭлементыФормы.тКартаДоступаКеГеЛьБУМ.Заголовок = ?(ЗначениеЗаполнено(ИдентификаторКеГеЛьБУМ), СокрЛП(ИдентификаторКеГеЛьБУМ), "Считайте карту лояльности КеГеЛьБУМ");
	ЭлементыФормы.тКартаДоступаКеГеЛьБУМ.ЦветТекста = ?(ЗначениеЗаполнено(ИдентификаторКеГеЛьБУМ), Новый Цвет(0,0,0), Новый Цвет(0,0,255));
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура КнопкаОКНажатие(Элемент)
	
	// Проверки карты КеГеЛьБУМ
	Если НЕ ЗначениеЗаполнено(ИдентификаторКеГеЛьБУМ) Тогда
		Текст1="Ошибка!";
		Текст2="Не считана карта КеГеЛьБУМ!";
		ИнтерфейсРМ.ВопросПредупреждение("Ошибка",Текст1,Текст2,"","ОК","");
		Возврат;
	КонецЕсли;
	
	КартаДоступаСсылка = Справочники.КартыДоступа.НайтиПоРеквизиту("Идентификатор",СокрЛП(ИдентификаторКеГеЛьБУМ));
	
	Если НЕ КартаДоступаСсылка.Пустая() Тогда
		Текст1="Это не КеГеЛьБУМ!";
		Текст2="Считана "+КартаДоступаСсылка.ТипКарты+", а не карта КеГеЛьБУМ!"; 
		ИнтерфейсРМ.ВопросПредупреждение("Ошибка",Текст1,Текст2,"","ОК","");
		Возврат;
	КонецЕсли; 
	
	// Проверки карты доступа
	Если НЕ ЗначениеЗаполнено(КартаДоступа.Код) Тогда
		Текст1="Ошибка!";
		Текст2="Не считана постоянная карта клиента!";
		ИнтерфейсРМ.ВопросПредупреждение("Ошибка",Текст1,Текст2,"","ОК","");
		Возврат;
	КонецЕсли;
	
	Если КартаДоступа.ТипКарты <> Перечисления.ТипыКартДоступа.Постоянная Тогда
		Текст1="Это не постоянная карта!";
		Текст2="Считана "+КартаДоступа.ТипКарты+",
		|а не постоянная карта клиента!"; 
		ИнтерфейсРМ.ВопросПредупреждение("Ошибка",Текст1,Текст2,"","ОК","");
		Отказ = Истина;
		Возврат;
	КонецЕсли; 
	
	НачатьТранзакцию();
	
	Док = Документы.СменаКартДоступа.СоздатьДокумент();
	Док.Дата					= ТекущаяДата();
	Док.Клиент 					= Клиент.Ссылка;
	Док.ИдентификаторКеГеЛьБУМ	= ИдентификаторКеГеЛьБУМ;
	Док.ПостояннаяКартаКлиента	= КартаДоступа.Ссылка;
	Док.Автор					= глПользователь;
	
	Если НЕ ИнтерфейсРМ.ПопыткаДействияСОбъектом( Док, "Объект.Записать(РежимЗаписиДокумента.Запись)" ) Тогда
		ОтменитьТранзакцию();
		Возврат;
	КонецЕсли; 
		
	ЗафиксироватьТранзакцию();
	
	Текст1="Регистрация";
	Текст2="Регистрация замены выполнена успешно!";
	ИнтерфейсРМ.ВопросПредупреждение("Предупреждение",Текст1,Текст2,"","ОК","");
	
	ИнтерфейсРМ.ЗаписьСобытия( Справочники.События.КлиентыЗаменаКарты, Клиент.Ссылка, Клиент.Наименование);
	
	Закрыть("ОК");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ТЕЛО МОДУЛЯ

ПараметрыОкна = Новый Структура("Центр, Лево, Верх, Ширина, Высота", Истина);
