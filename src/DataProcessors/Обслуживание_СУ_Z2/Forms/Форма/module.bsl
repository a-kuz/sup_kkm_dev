Перем СписокСимволов,Префиксы, Суффиксы;

Процедура ОтобразитьРезультат()
	
	Если Результат.Ошибка Тогда
		ТестРезультат = Результат.Описание+Символы.ВК+Символы.ПС+Результат.Подробно;
	Иначе
		ТестРезультат = "Ошибок нет";
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбновлениеПрефиксовСуффиксов(Обновление=Ложь);
	ЭлементыФормы.СписокОписанийСуффиксов.СписокВыбора.Очистить();
	ЭлементыФормы.СписокОписанийПрефиксов.СписокВыбора.Очистить(); 
	
	// заполние суффиксов и префиксов
	ЭлементыФормы.СписокОписанийПрефиксов.СписокВыбора.Добавить(0,"#0");
	ЭлементыФормы.СписокОписанийСуффиксов.СписокВыбора.Добавить(0,"#0");
	Таб = ПолучитьМакет("Символы");
	Для НомСтр=1 По 255 Цикл
		Если ЭлементыФормы.PortNumber.Значение = 100 Тогда // клавиатура, для нее UniCode
			Представление	=СокрЛП(Таб.Область(НомСтр,1).Текст);
		Иначе
			Представление	=СокрЛП(Таб.Область(НомСтр,2).Текст);
		КонецЕсли;
		
		Если НЕ ПустаяСтрока(Представление) Тогда
			Представление = "("+Представление+")";
		КонецЕсли;
		ЭлементыФормы.СписокОписанийСуффиксов.СписокВыбора.Добавить(НомСтр,"#"+НомСтр+" "+Представление);
		ЭлементыФормы.СписокОписанийПрефиксов.СписокВыбора.Добавить(НомСтр,"#"+НомСтр+" "+Представление);
	КонецЦикла;
	
	Если НЕ Обновление Тогда
		Префиксы = СоздатьСписок(Prefix);
		Суффиксы = СоздатьСписок(Suffix);
	КонецЕсли;
			
	ЭлементыФормы.СтрПрефикс.Значение =  ПолучитьСтрокуОписанияСимволов(Префиксы);
	ЭлементыФормы.СтрСуффикс.Значение =  ПолучитьСтрокуОписанияСимволов(Суффиксы);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(ОписаниеПрофиля) ТОгда
		
		Предупреждение("Данная обработка используется для работы конфигурации. Вручную ее вызывать запрещено.");	
		Отказ = Истина;
		Возврат;  		
	КонецЕсли;   
	
	DRV.CurrentDeviceNumber	= CurrentDeviceNumber;
	ФлажокУстройствоВкл = DRV.DeviceEnabled;
	
	// заполнение списка портов
	Для н=1 По 32 Цикл
		ЭлементыФормы.PortNumber.СписокВыбора.Добавить(н, "COM"+н);
	КонецЦикла;
	//ЭлементыФормы.PortNumber.СписокВыбора.Добавить(100,"Клавиатура");
	
	ОбновлениеПрефиксовСуффиксов();
			
	// заполнение списка скоростей обмена
	СписокСкоростей = ЭлементыФормы.BaudRate.СписокВыбора;
	СписокСкоростей.Добавить(1 ,"300");
	СписокСкоростей.Добавить(2 ,"600");
	СписокСкоростей.Добавить(3 ,"1200");
	СписокСкоростей.Добавить(4 ,"2400");
	СписокСкоростей.Добавить(5 ,"4800");
	СписокСкоростей.Добавить(7 ,"9600");
	СписокСкоростей.Добавить(10,"19200");
	СписокСкоростей.Добавить(12,"38400");
	
	// заполнение списка битов данных
	СписокБитовДанных = ЭлементыФормы.DataBits.СписокВыбора;
	СписокБитовДанных.Добавить(3, "7 бит");
	СписокБитовДанных.Добавить(4, "8 бит");
	
	// заполнение списка четностей
	СписокЧетностей = ЭлементыФормы.Parity.СписокВыбора;
	СписокЧетностей.Добавить(0, "Нет");
	СписокЧетностей.Добавить(1, "Нечетность");    
	СписокЧетностей.Добавить(2, "Четность");
	СписокЧетностей.Добавить(3, "Установлен");
	СписокЧетностей.Добавить(4, "Сброшен");
	
	// заполнение списка стоп-битов
	СписокСтопБитов = ЭлементыФормы.StopBits.СписокВыбора;
	СписокСтопБитов.Добавить(0, "1 бит");
	СписокСтопБитов.Добавить(1, "2 бита");
	
КонецПроцедуры

Функция СоздатьСписок(Стр)
	СписокСимволов = Новый СписокЗначений;
	// 
	Для  н = 1 По СтрДлина(Стр)  Цикл
		Если ЭлементыФормы.PortNumber.Значение = 100 Тогда
			СписокСимволов.Добавить(КодСимвола(Стр,н));
		Иначе
			Таб = ПолучитьМакет("Символы");
			ЗначВТаблице = Таб.НайтиТекст(Строка(КодСимвола(Стр,н)),,Таб.Область(1,3,255,3));
			Если ЗначВТаблице = Неопределено Тогда
				СписокСимволов.Добавить(КодСимвола(Стр,н));
			Иначе
				СписокСимволов.Добавить(ЗначВТаблице.Верх);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	Возврат СписокСимволов;
	    
КонецФункции


Функция ПолучитьСтрокуОписанияСимволов(СписокКодовСимволов)
	Стр = "";
	Для Каждого КодСимволов Из СписокКодовСимволов Цикл
		Если СписокКодовСимволов = Префиксы Тогда
			Стр=Стр+ЭлементыФормы.СписокОписанийПрефиксов.СписокВыбора.Получить(КодСимволов.Значение).Представление+" ";
		Иначе
			Стр=Стр+ЭлементыФормы.СписокОписанийСуффиксов.СписокВыбора.Получить(КодСимволов.Значение).Представление+" ";
		КонецЕсли;
	КонецЦикла;
	Возврат Стр;
КонецФункции

Функция ПолучитьСтрокуСимволов(СписокКодСимволов)
	
	Стр="";
	Если PortNumber=100 Тогда // клавиатура, вес просто
		Для Каждого КодСимволов Из СписокКодСимволов Цикл
			Стр=Стр+Символ(КодСимволов.Значение);
		КонецЦикла;
	Иначе 
		Таб=ПолучитьМакет("Символы");
		Для Каждого КодСимволов Из СписокКодСимволов Цикл
			Если КодСимволов.Значение <=32 Тогда
				Стр=Стр+Символ(КодСимволов.Значение);
			ИначеЕсли КодСимволов = 127 Тогда   // Del
				Стр=Стр+Символ(46);
			Иначе 
				Стр=Стр+Таб.Область(КодСимволов.Значение,2).Текст;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Возврат Стр;
	
КонецФункции

Процедура ВнешнееСобытие(Источник, Событие, Данные)
	
	Если (Источник="BarCodeScaner") ИЛИ (Источник="MagneticStripeCardReader") ИЛИ (Лев(Источник,11)="RadioReader") Тогда
		
		// заплатка от странного глюка, когда в "Данные" приходит не номер сообщения а сами данные, как будто OldVersion=1
		Попытка
			НомерСообщения = Число(Данные);
		Исключение
			НомерСообщения = 1;
		КонецПопытки;
		
		DRV.EventNumber = НомерСообщения;
		
		ТестРезультат = "Устройство: "+DRV.CurrentDeviceName+Символы.ВК+Символы.ПС+
		"Порт: "+?(DRV.PortNumber=100, "Клавиатура", "COM "+Строка(DRV.PortNumber))+Символы.ВК+Символы.ПС+
		"Данные: "+DRV.ScanData;
		
		DRV.DeleteEvent();
		DRV.DataEventEnabled	= 1;
		флВнешнееСобытиеВФорме = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура ОК(Кнопка)
	
	Prefix = ПолучитьСтрокуСимволов(Префиксы);
	Suffix = ПолучитьСтрокуСимволов(Суффиксы);
	ЗаполнитьЗначенияСвойств(ПараметрыТО,ЭтотОбъект);
	
	Закрыть("ОК");
КонецПроцедуры

Процедура КнопкаПараметрыДействие(Элемент)
	
	Пока DRV.DeviceCount < CurrentDeviceNumber Цикл
		DRV.AddDevice();
	КонецЦикла;
	
	УстановитьСвойстваДрайвера();
	
	DRV.ShowProperties();
	
	ПрочитатьСвойстваДрайвера();
	
КонецПроцедуры

Процедура ФлажокУстройствоВклПриИзменении(Элемент)
	
	Если ФлажокУстройствоВкл Тогда
		Подключить();
	Иначе
		Отключить();
	КонецЕсли; 
	
	ОтобразитьРезультат();
	ФлажокУстройствоВкл = DRV.DeviceEnabled;
	
КонецПроцедуры

Процедура ОбновлениеОтображения()
	Флаг = PortNumber<=32;
	ЭлементыФормы.ПанельПорт.Страницы.Клавиатура.Видимость = НЕ Флаг;
	ЭлементыФормы.ПанельПорт.Страницы.ComПорт.Видимость = Флаг;
КонецПроцедуры

Процедура ПрефиксОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Префиксы.Добавить(ВыбранноеЗначение);
	ЭлементыФормы.СтрПрефикс.Значение = ПолучитьСтрокуОписанияСимволов(Префиксы);
КонецПроцедуры

Процедура СуффиксОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Суффиксы.Добавить(ВыбранноеЗначение);
	ЭлементыФормы.СтрСуффикс.Значение = ПолучитьСтрокуОписанияСимволов(Суффиксы);
КонецПроцедуры

Процедура ПрефиксОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ЭлементыФормы.СтрПрефикс.Значение = "";
	Префиксы.Очистить();
КонецПроцедуры

Процедура СуффиксОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ЭлементыФормы.СтрСуффикс.Значение = "";
	Суффиксы.Очистить();
КонецПроцедуры

Процедура ПриСменеСтраницы(Элемент, ТекущаяСтраница)
	Если ЭлементыФормы.Панель1.ТекущаяСтраница = ЭлементыФормы.Панель1.Страницы.Тест Тогда
		Prefix = ПолучитьСтрокуСимволов(Префиксы);
		Suffix = ПолучитьСтрокуСимволов(Суффиксы);
		ЗаполнитьЗначенияСвойств(DRV,ЭтотОбъект);
	КонецЕсли;
КонецПроцедуры


Процедура PortNumberПриИзменении(Элемент)
	ОбновлениеПрефиксовСуффиксов(Истина);
КонецПроцедуры

