
Перем ТаблицаЗадания;

Процедура ПоказатьПараметрыТаблицы(Таб,Стр)
	
	ИндексСтроки = 1;
	Для Каждого СтрокаШапки ИЗ Таб Цикл
		Если СтрокаШапки.ТипДанных="Строка" Тогда
			Стр=Стр+?(ИндексСтроки=1,"",Символы.ПС)+СтрокаШапки.Данные;
		КонецЕсли;
		ИндексСтроки = ИндексСтроки+1;	
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(ОписаниеПрофиля) ТОгда
		Предупреждение("Данная обработка открывается только из справочника торгового оборудования!",5);
		Отказ = Истина;
		Возврат;  		
	КонецЕсли;   
	
	// заполнение списка COM портов
	Для н=1 По 32 Цикл
		ЭлементыФормы.PortNumber.СписокВыбора.Добавить(н, "COM"+н);
	КонецЦикла;
	
	// заполнение списка скоростей обмена
	СписокСкоростей = ЭлементыФормы.BaudRate.СписокВыбора;
	СписокСкоростей.Добавить(0 ,"2400");
	СписокСкоростей.Добавить(1 ,"4800");
	СписокСкоростей.Добавить(2 ,"9600");
	СписокСкоростей.Добавить(3 ,"19200");
	СписокСкоростей.Добавить(4 ,"38400");
	СписокСкоростей.Добавить(5 ,"57600");
	СписокСкоростей.Добавить(6 ,"115200");
	
	// заполнение списка символов в строке
	СписокСимволовВСтроке = ЭлементыФормы.СимволовВСтроке.СписокВыбора;
	СписокСимволовВСтроке.Добавить(32);
	СписокСимволовВСтроке.Добавить(36);
	СписокСимволовВСтроке.Добавить(40);
	СписокСимволовВСтроке.Добавить(44);
	СписокСимволовВСтроке.Добавить(48);
	
	// управление закладками
	Страницы = ЭлементыФормы.Панель1.Страницы;
	
	Если СокрП(ТО.КодВида) = "ФР" Тогда
		Страницы.Удалить(Страницы.ПД);
		ПоказатьПараметрыТаблицы(ТаблицаШапкиФД,ШапкаФД);
		// заполнение таблицы кассиров
		Для н=1 По 28 Цикл
			ТаблицаКассиров.Добавить().Кассир = "Кассир "+н;
		КонецЦикла;
		ТаблицаКассиров.Добавить().Кассир = "Админ.";
		ТаблицаКассиров.Добавить().Кассир = "Сис.админ.";
		
		ЭлементыФормы.ШапкаФД.Доступность = ПечатьШапкиФД;
		
	ИначеЕсли СокрП(ТО.КодВида) = "ПРН" Тогда
		Страницы.Удалить(Страницы.СписокКассиров);
		Страницы.Удалить(Страницы.Время);
		Страницы.Удалить(Страницы.ШапкаФД);
		Если НЕ Найти("ШтрихКомбо,Штрих950К",СокрП(ТО.КодМодели)) Тогда
			Страницы.Удалить(Страницы.ПД);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры


Процедура СохранитьПараметрыТаблицы(Таб,Стр)
	
	Если Таб.Колонки.Количество() = 0 Тогда
		Таб.Колонки.Добавить("Данные");
		Таб.Колонки.Добавить("ТипДанных");
		Таб.Колонки.Добавить("Параметры");
	Иначе
		Таб.Очистить();
	КонецЕсли;
	
	Для н=1 По СтрЧислоСтрок(Стр) Цикл
		НоваяСтрока = Таб.Добавить();
		НоваяСтрока.Данные		=СтрПолучитьСтроку(Стр,н);
		НоваяСтрока.ТипДанных	="Строка";
		НоваяСтрока.Параметры	="Центр,Переносить,ПереводСтроки";
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

// Сохранение параметров и закрытие формы
//
// Параметры
//  <Параметр1>  – <Тип.Вид> – <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  – <Тип.Вид> – <описание параметра>
//                 <продолжение описания параметра>
//
Процедура ОсновныеДействияФормыОК(Кнопка)
	
	СохранитьПараметрыТаблицы(ТаблицаШапкиФД,ШапкаФД);
	ЗаполнитьЗначенияСвойств(ПараметрыТО,ЭтотОбъект);
	
	Закрыть("ОК");
	
КонецПроцедуры

Процедура КнопкаПараметрыККМНажатие(Элемент)
	
	Если НЕ Подключить(Истина, Password) Тогда
		Предупреждение(Результат.Описание + Символы.ПС + Результат.Подробно);
	КонецЕсли;
	
	DRV.ShowProperties();
	
	Отключить();
	
КонецПроцедуры

Процедура КнопкаОбновитьВремяНажатие(Элемент)
	
	Если НЕ Подключить(Истина, Password) Тогда
		Предупреждение(Результат.Описание + Символы.ПС + Результат.Подробно);
		Возврат;
	КонецЕсли;
	
	ЭлементыФормы.тВремяККМ.Заголовок = Формат(Дата(Drv.Date),"ДЛФ=Д")+" "+DRV.TimeStr;
	
	ЭлементыФормы.тВремяСистемы.Заголовок = Формат(ТекущаяДата(), "ДЛФ=ДВ");
	
	Отключить();
	
КонецПроцедуры

Процедура КнопкаСинхронизироватьВремяНажатие(Элемент)
	
	Текст2 = "Подтвердите системные дату, время:"+Символы.ПС+Формат(ТекущаяДата(),"ДЛФ=ДВ");
	Если Вопрос(Текст2,РежимДиалогаВопрос.ОКОтмена) = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	Парам = Новый Структура;
	Парам.Вставить("Пароль"					, Password);
	Парам.Вставить("ИгнорироватьОбщийПароль", Истина);
	
	ВыполнитьДействие("СинхронизироватьВремя", Парам);
	Если Результат.Ошибка Тогда
		Предупреждение(Результат.Описание + Символы.ПС + Результат.Подробно);
	КонецЕсли;
	
	КнопкаОбновитьВремяНажатие(Элемент);
	
КонецПроцедуры

Процедура СписокКассиров(Режим)
	
	ПарольСА=0;
	Если НЕ ВвестиЧисло(ПарольСА,"Введите пароль системного администратора") Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Подключить(Истина, ПарольСА) Тогда
		Предупреждение(Результат.Описание+Символы.ПС+Результат.Подробно);
		Возврат;
	КонецЕсли;
	
	DRV.TableNumber = 2; // таблица кассиров
	
	ИндексСтроки = 1;
	Для Каждого Кассир Из ТаблицаКассиров Цикл
		DRV.RowNumber = ИндексСтроки;
		DRV.FieldNumber=1; // пароль
		DRV.GetFieldStruct();
		Если Режим="Прочитать" Тогда
			DRV.ReadTable();
			Кассир.Пароль = DRV.ValueOfFieldInteger;
		Иначе
			DRV.ValueOfFieldInteger = ?(ПустаяСтрока(Кассир.Пароль),0,Число(Кассир.Пароль));
			DRV.WriteTable();
		КонецЕсли;
		
		DRV.FieldNumber=2; // реквизиты кассиров
		DRV.GetFieldStruct();
		Если Режим="Прочитать" Тогда
			DRV.ReadTable();
			Сотрудник = Справочники.Сотрудники.НайтиПоНаименованию(DRV.ValueOfFieldString,Истина);
			Кассир.ФИО = Сотрудник;
		Иначе
			DRV.ValueOfFieldString=Кассир.ФИО.Наименование;
			DRV.WriteTable();
		КонецЕсли;
		
		Если Ошибка() Тогда
			Предупреждение(Результат.Описание+Символы.ПС+Результат.Подробно);
			Возврат;
		КонецЕсли;	
		
		ИндексСтроки = ИндексСтроки+1;
	КонецЦикла;
	
КонецПроцедуры

Процедура КнопкаКассирыПрочитатьНажатие(Элемент)
	
	СписокКассиров("Прочитать");	
	Отключить();
	
КонецПроцедуры

Процедура КнопкаКассирыЗагрузитьНажатие(Элемент)
	
	СписокКассиров("Загрузить");
	Отключить();
	
КонецПроцедуры

Процедура КнопкаКассирыОчиститьВсеНажатие(Элемент)
	Для Каждого Кассир Из ТаблицаКассиров  Цикл
		Кассир.ФИО = Неопределено;
		Кассир.Пароль = "";
	КонецЦикла;
КонецПроцедуры

Процедура ТаблицаКассировФИОПриИзменении(Элемент)
	СтрокаТаблицы        = ЭлементыФормы.ТаблицаКассиров.ТекущаяСтрока;
	СтрокаТаблицы.Пароль = СтрокаТаблицы.ФИО.КодДоступа;
КонецПроцедуры

Процедура ПечатьШапкиФДПриИзменении(Элемент)
	
	ЭлементыФормы.ШапкаФД.Доступность = ПечатьШапкиФД;
	
КонецПроцедуры
